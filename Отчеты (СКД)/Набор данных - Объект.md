#ОТЧЕТЫ 

Хрусталева (Отчеты) => Страница 183 => Пример 15

В качестве объекта можно использовать:
• Таблицу значений
• Набор записей регистра
• Табличную часть
• Результат запроса
• Область ячеек табличного документа

```bsl
// Набор данных - Объект (Используем внешний источник данных для отчета в СКД)

// 1. Создаем отчет и открываем схему компоновки данных:
// 2. Добавляем набор данных "Объект"
// 3. Указываем Имя объекта, содержащего данные: "ТаблицаДолгов"
// 4. Добавляем поле "Контрагент", Тип значения: (Строка)
// 5. Добавляем поле "Долг", Тип значения: (Число)

// 6. Открываем модуль объекта отчета:
// 7. Получаем данные текстового файла
// 8. Формируем на его основании "ТаблицаДолгов" (ТаблицаЗначений)
// 9. Добаляем нашу таблицу значений в схему компоновки данных.

// Модуль объекта отчета
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	// Создание таблицы значений
	КЧ = Новый КвалификаторыЧисла(10, 2);
	ТаблицаДолгов = Новый ТаблицаЗначений;
	ТаблицаДолгов.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("Строка"));
	ТаблицаДолгов.Колонки.Добавить("Долг", Новый ОписаниеТипов("Число",,,КЧ));
	
	// Чтение данных файла
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать("C:\Users\Oleg\Desktop\ТаблицаДолгов.csv", КодировкаТекста.UTF8);
	
	// Заполнение таблицы значений данными текстового файла
	Для НомерСтроки = 2 По Текст.КоличествоСтрок() Цикл
		ТекущаяСтрока				= Текст.ПолучитьСтроку(НомерСтроки);
		ЧастиСтроки					= СтрРазделить(ТекущаяСтрока, ";");
		НоваяСтрока					= ТаблицаДолгов.Добавить();
		НоваяСтрока.Контрагент	= ЧастиСтроки[0];
		НоваяСтрока.Долг			= Число(ЧастиСтроки[1]);
	КонецЦикла;

	// Формирование макета компоновки данных на основании схемы и настроек
	СтандартнаяОбработка = Ложь;
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	// Выполнение компоновки данных
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаДолгов", ТаблицаДолгов); // Добавляем нашу ТЗ в СКД
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	// Вывод результата компоновки данных в табличный документ
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
```