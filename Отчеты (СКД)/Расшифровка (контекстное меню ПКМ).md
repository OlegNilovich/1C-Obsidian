#ОТЧЕТЫ 

Хрусталева (Отчеты) => Страница 215 => Изменение действий расшифровки
Хрусталева (Отчеты) => Страница 309 => Расшифровка

---
### Задача:

Для отчета **Прайс-Лист**, требуется добавить в контекстное меню расшифровки две команды:
- **Показать продажи по товару** - открывает отчет **Продажи** с отбором по выбранному товару
- **Показать остатки по товару** - открывает отчет **Остатки** с отбором по выбранному товару
![[Pasted image 20240919015043.png]]
![[Pasted image 20240919015143.png]]

---
### Решение:

##### Макет СКД:
1. Открываем макет **ОсновнаяСхемаКомпоновкиДанных**
2. Вкладка **Наборы данных** => Создаем набор данных **Запрос**
```bsl
ВЫБРАТЬ
	ЦеныНоменклатуры.Номенклатура КАК									Номенклатура,
	ЦеныНоменклатуры.Номенклатура.Артикул КАК						Артикул,
	ЦеныНоменклатуры.Номенклатура.ВидНоменклатуры КАК		ВидНоменклатуры,
	ЦеныНоменклатуры.ВидЦен КАК												ВидЦены,
	ЦеныНоменклатуры.Цена КАК													Цена
ИЗ
	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
```
3. Вкладка **Ресурсы** => Поле **Цена** => Выражение **Сумма(Цена)**
4. Вкладка **Настройки** => Добавляем группировку **Детальные записи**
5. **Настройки** => **Выбранные поля**:
- Номенклатура
- ВидНоменклатуры
- Артикул
- ВидЦены
- Цена
6. **Настройки** => **Сортировка** => Поле: *Цена*, Направление сортировки: *По убыванию*
7. **Настройки** => **Макет оформления** => *Античный*
##### Форма отчета:
1. Создаем форму **ФормаОтчета**
2. Для элемента **Результат** создаем событие **РезультатОбработкаДополнительнойРасшифровки**
![[Pasted image 20240919020210.png]]
3. Модуль формы:
```bsl
&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораДействия", ЭтотОбъект, Расшифровка);
	
	ДоступныеДействия = Новый Массив();
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Отфильтровать);
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Сгруппировать);
	ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Упорядочить);
	
	ДополнительныеПунктыМеню = Новый СписокЗначений;
	ДополнительныеПунктыМеню.Добавить("ОткрытьОтчетПродажи", "Показать продажи по товару");
	ДополнительныеПунктыМеню.Добавить("ОткрытьОтчетОстатки", "Показать остатки по товару");
	
	Настройки = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Отчет);
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Настройки);
	ОбработкаРасшифровки.ПоказатьВыборДействия(	Оповещение,
															Расшифровка,
															ДоступныеДействия,
															ДополнительныеПунктыМеню);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДействия(ВыбранноеДействие, ПараметрДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеДействие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ID_Расшифровки = ДополнительныеПараметры;
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	Если ВыбранноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет Тогда
		Возврат;
	ИначеЕсли ВыбранноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		ПоказатьЗначение(Неопределено, ПараметрДействия);
	ИначеЕсли ВыбранноеДействие = "ОткрытьОтчетПродажи" Тогда
		Номенклатура = ПолучитьЗначенииеРасшифровки(ID_Расшифровки);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Номенклатура", Номенклатура));
		ОткрытьФорму("Отчет.Продажи.Форма", ПараметрыФормы, ЭтаФорма, Истина,,,, РежимОткрытия);
	ИначеЕсли ВыбранноеДействие = "ОткрытьОтчетОстатки" Тогда
		Номенклатура = ПолучитьЗначенииеРасшифровки(ID_Расшифровки);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Номенклатура", Номенклатура));
		ОткрытьФорму("Отчет.ОстаткиТоваров.Форма", ПараметрыФормы, ЭтаФорма, Истина,,,, РежимОткрытия);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыФормы.Вставить("Расшифровка", 
		Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, ID_Расшифровки, 
		ПараметрДействия));
		ОткрытьФорму("Отчет.ПрайсЛистПрограммнаяРасшифровка.Форма", ПараметрыФормы, ЭтаФорма, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьЗначенииеРасшифровки(ID_Расшифровки)
	КоллекцияРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	НайденныйЭлементРасшифровки = КоллекцияРасшифровки.Элементы.Получить(ID_Расшифровки);
	ПоляРасшифровки = НайденныйЭлементРасшифровки.ПолучитьПоля();
	ЗначениеПоляРасшифровки = ?(ПоляРасшифровки.Количество() > 0, ПоляРасшифровки[0].Значение, Неопределено);
	Возврат ЗначениеПоляРасшифровки;
КонецФункции
```