#ОТЧЕТЫ 

Модуль формы:
```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаОтчета = КонецДня(ТекущаяДата());
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	//1.1 Получение данных для отчета
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваров.Склад						КАК Склад,
		|	ОстаткиТоваров.Номенклатура			КАК Номенклатура,
		|	ОстаткиТоваров.КоличествоОстаток	КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаОтчета, ) КАК ОстаткиТоваров
		|ИТОГИ
		|	СУММА(Количество)
		|ПО
		|	ОБЩИЕ,
		|	Склад,
		|	Номенклатура";
	
	//1.2 Установка параметров и выполнение запроса
	Если ДатаОтчета = '00010101' Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаОтчета", "");
	Иначе
		Запрос.УстановитьПараметр("ДатаОтчета", ДатаОтчета);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();

//===========================================================================

	//2. Получение макета и Очищение табличного документа перед заполнением
	Макет = Отчеты.ОстаткиТоваров_КроссТаблица.ПолучитьМакет("КроссТаблица");
	ТабДок.Очистить();
	
//===========================================================================

	#Область ШапкаТаблицы
	//3.1 Выводим первую колонку шапки, текстом "Номенклатура/Склад"
	ШапкаНоменклатура = Макет.ПолучитьОбласть("Шапка|Номенклатура");
	ТабДок.Вывести(ШапкаНоменклатура);
	
	//3.2 Получаем итоговую выборку "Склады"
	ВыборкаСклады = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Склад");
	Пока ВыборкаСклады.Следующий() Цикл
		//3.3 Выводим склады из выборки. Справа от первой колонки шапки
		ШапкаСклад = Макет.ПолучитьОбласть("Шапка|Склад");
		ШапкаСклад.Параметры.Заполнить(ВыборкаСклады);
		ТабДок.Присоединить(ШапкаСклад);
	КонецЦикла;
	
	//3.4 Выводим последнюю колонку шапки, текстом "Итоги по номенклатуре". Справа от последнего склада
	ШапкаИтогНоменклатура = Макет.ПолучитьОбласть("Шапка|ИтогНоменклатура");
	ТабДок.Присоединить(ШапкаИтогНоменклатура);
	#КонецОбласти
	
//===========================================================================

	#Область СтрокиТаблицы
	//4.1 Получаем итоговою выборку "Номенклатура"
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Номенклатура");
	Пока ВыборкаНоменклатура.Следующий() Цикл
		//4.2 Выводим список номенклатуры в первой колонке таблицы "Номенклатура"
		ДетальныеЗаписиНоменклатура = Макет.ПолучитьОбласть("ДетальныеЗаписи|Номенклатура");
		ДетальныеЗаписиНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
		ТабДок.Вывести(ДетальныеЗаписиНоменклатура);
		
		//4.3 Получаем количество номенклатуры на конкретных складах. Если остатка нет, то получаем 0.
		ТипОбхода = ОбходРезультатаЗапроса.ПоГруппировкам;
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать(ТипОбхода, "Склад", "ВСЕ");
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			//4.4 Выводим количество номенклатуры для конкретных складов. Где остатка нет, выводим 0.
			ДетальныеЗаписиСклад = Макет.ПолучитьОбласть("ДетальныеЗаписи|Склад");
			ДетальныеЗаписиСклад.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ТабДок.Присоединить(ДетальныеЗаписиСклад);
		КонецЦикла;
		
		//4.5 Выводим итоговое количество в колонке "Итог по номенлкатуре"
		ДетальныеЗаписиИтогНоменклатура = Макет.ПолучитьОбласть("ДетальныеЗаписи|ИтогНоменклатура");
		ДетальныеЗаписиИтогНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
		ТабДок.Присоединить(ДетальныеЗаписиИтогНоменклатура);
	КонецЦикла;
	#КонецОбласти

//===========================================================================
	
	#Область ПодвалТаблицы
	//5.1 Выводим первую колонку подвала, текстом "Итоги по складам"
	ИтогСкладНоменклатура = Макет.ПолучитьОбласть("ИтогСклад|Номенклатура");
	ТабДок.Вывести(ИтогСкладНоменклатура);
	
	//5.2 Получаем итоговое количество для складов.
	ВыборкаСклады = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Склад");
	Пока ВыборкаСклады.Следующий() Цикл
		//5.3 Выводим итоговое количество в строке "Итог по складам"
		ИтогСкладСклад = Макет.ПолучитьОбласть("ИтогСклад|Склад");
		ИтогСкладСклад.Параметры.Заполнить(ВыборкаСклады);
		ТабДок.Присоединить(ИтогСкладСклад);
	КонецЦикла;
	
	//5.4 Получаем выборку "ОбщиеИтоги" (самый верхний уровень)
	ВыборкаОбщиеИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
	ВыборкаОбщиеИтоги.Следующий();
	//5.5 Выводим общий итог на пересечении "Итогов по складам" и "Итогов по номенклатуре"
	ИтогСкладИтогНоменклатура = Макет.ПолучитьОбласть("ИтогСклад|ИтогНоменклатура");
	ИтогСкладИтогНоменклатура.Параметры.Заполнить(ВыборкаОбщиеИтоги);
	ТабДок.Присоединить(ИтогСкладИтогНоменклатура);
	#КонецОбласти

КонецПроцедуры
```