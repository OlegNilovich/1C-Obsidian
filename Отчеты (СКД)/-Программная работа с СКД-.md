#ОТЧЕТЫ

Хрусталева (Отчеты) => Страница 476

---
### Установка схемы компоновки данных

###### Программное получение схемы компоновки
```bsl
СхемаКомпоновкиДанных = Отчеты.СписокНоменклатуры.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
```

###### Процедура программной установки схемы компоновки
```bsl
СКД = Новый СхемаКомпоновкиДанных;
ИД = СКД.ИсточникиДанных.Добавить();
ИД.Имя = "ИсточникДанных";
ИД.ТипИсточникаДанных = "Local";

НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
НаборДанных.Имя = "НаборДанных1";
НаборДанных.Запрос = 
					"ВЫБРАТЬ
					|	ЦеныСрезПоследних.Период,
					|	ЦеныСрезПоследних.Номенклатура,
					|	ЦеныСрезПоследних.Цена
					|ИЗ
					|	РегистрСведений.Цены.СрезПоследних КАК ЦеныСрезПоследних";
НаборДанных.ИсточникДанных = "ИсточникДанных";

Поле1 = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
Поле1.Поле = "Номенклатура";
Поле1.ПутьКДанным = "Номенклатура";
Поле1.Заголовок = "Номенклатура";

Поле2 = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
Поле2.Поле = "Период";
Поле2.ПутьКДанным = "Период";
Поле2.Заголовок = "Период";

Поле3 = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
Поле3.Поле = "Цена";
Поле3.ПутьКДанным = "Цена";
Поле3.Заголовок = "Цена";
Поле3.Оформление.УстановитьЗначениеПараметра("Формат", "ЧЦ=6; ЧДЦ=0");

ПолеРесурса = СКД.ПоляИтога.Добавить();
ПолеРесурса.ПутьКДанным = "Цена";
ПолеРесурса.Выражение = "Сумма(Цена)";
```

---
### Редактирование стандартных настроек

**Стандартные настройки** – это настройки, которые делает разработчик в схеме компоновки данных
###### Процедура программной установки настроек
```bsl
Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;

ГруппировкаНоменклатура = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
ГруппировкаНоменклатура.Использование = Истина;

ГруппировкаПериод = ГруппировкаНоменклатура.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
ГруппировкаПериод.Использование = Истина;

ПолеНоменклатура = ГруппировкаНоменклатура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
ПолеНоменклатура.Использование = Истина;
ПолеНоменклатура.Поле = Новый ПолеКомпоновкиДанных("Номенклатура");

ПолеПериод = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
ПолеПериод.Использование = Истина;
ПолеПериод.Поле = Новый ПолеКомпоновкиДанных("Период");

ВыбранныеПоляДляНоменклатуры = ГруппировкаНоменклатура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
ВыбранныеПоляДляНоменклатуры.Использование = Истина;

ПорядокДляНоменклатуры = ГруппировкаНоменклатура.Порядок.Элементы.
ПорядокДляНоменклатуры.Использование = Истина;Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));

ВыбранныеПоляДляПериода = ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
ВыбранныеПоляДляПериода.Использование = Истина;

ПорядокДляПериода = ГруппировкаПериод.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
ПорядокДляПериода.Использование = Истина;

ВыбранныеПоля = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
ВыбранныеПоля.Использование = Истина;
ВыбранныеПоля.Заголовок = "Цена";
ВыбранныеПоля.Поле = Новый ПолеКомпоновкиДанных("Цена");

Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", "Цены комплектующих");
Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("МакетОформления", "Зеленый");
```
### Результат:
![[Pasted image 20240924062607.png]]

---
### Работа с параметрами схемы компоновки

Можно задать значения параметров схемы компоновки данных по умолчанию перед формированием отчета
Для этого нужно использовать обработчик события формы отчета **ПередЗагрузкойВариантаНаСервере**
###### Обработчик события *ПередЗагрузкойВариантаНаСервере*
```bsl
&НаСервере
Процедура ПередЗагрузкойВариантаНаСервере(Настройки)
	Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", "Продажа системных блоков");
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", '20111201');
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", '20111231');
КонецПроцедуры
```

Если настройки являются пользовательскими (например, это параметры отчетного периода) и пользователь хоть раз задавал их значения в форме отчета, то нужно использовать обработчик события **ПередЗагрузкойПользовательскихНастроекНаСервере**, который
вызывается после загрузки настроек варианта отчета
###### Обработчик события **ПередЗагрузкойПользовательскихНастроекНаСервере**
```bsl
&НаСервере
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(Настройки)
	Настройки.Элементы[0].Значение = ‘20111201’;
	Настройки.Элементы[0].Использование = Истина;
	Настройки.Элементы[1].Значение = ‘20111231’;
	Настройки.Элементы[1].Использование = Истина;
КонецПроцедуры
```

---
### Редактирование настроек схемы компоновки

В приведенном примере создается отбор по номенклатуре из группы Комплектующие и включается вывод информации об отборе, затем измененные настройки загружаются в компоновщик настроек отчета, и формируется отчет

###### Процедура программной установки настроек
```bsl
Настройки = Отчет.КомпоновщикНастроек.Настройки;

ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура");
ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
ЭлементОтбора.ПравоеЗначение = Справочники.Номенклатура.НайтиПоНаименованию("Комплектующие");

Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.Выводить);
Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);

СкомпоноватьРезультат();
```

---
### Редактирование настроек не основной схемы компоновки

###### Инициализация компоновщика настроек источником доступных настроек
В данном примере во временное хранилище помещается схема компоновки данных, находящаяся в переменной *Схема*
```bsl
АдресСхемы = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
```

###### Инициализация компоновщика настроек источником доступных настроек
В данном примере схема будет получаться из базы данных, из реквизита с именем *СхемаКомпоновкиДанных* объекта со ссылкой Ссылка
```bsl
АдресСхемы = ПолучитьНавигационнуюСсылку(Ссылка, "СхемаКомпоновкиДанных");
КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
```

---
### Вывод отчета

После установки схемы компоновки данных и ее настройки, можно программно сформировать отчет
```bsl
Процедура СформироватьОтчет() Экспорт
	
	СхемаКомпоновкиДанных = УстановитьСхему();
	
	Настройки = УстановитьНастройки(СхемаКомпоновкиДанных);
	
	// В этот объект помещается информация о расшифровке данных.
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	// Формирование макета компоновки данных с помощью компоновщика макета.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	// В макет компоновки передаются СхемаКомпоновкиДанных, ее настройки и
	// ДанныеРасшифровки для заполнения информации о расшифровке.
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	// Выполнение компоновки данных с помощью процессора компоновки.
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	// Очистка поля табличного документа – Результат.
	Результат.Очистить();
	
	// Вывод результатов компоновки в табличный документ.
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры
```

###### Вывод отчета в табличный документ
```bsl
ДокументРезультат = Новый ТабличныйДокумент;
ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
```

###### Вывод отчета в дерево значений
```bsl
КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, 
	Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);

ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
ПроцессорВывода.УстановитьОбъект(ДеревоРезультата);
ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
```