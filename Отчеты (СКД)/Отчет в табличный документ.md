#ОТЧЕТЫ 
### Задача:
В форме списка справочника **Номенклатура**, требуется создать команду **Печать**, которая будет открывать отчет **СписокНоменклатуры** в виде табличного документа
![[Pasted image 20240910185120.png]]
### Решение:
1. Создаем форму списка для справочника **Номенклатура**
2. Открываем форму списка, создаем команду: **НапечататьСписокНоменклатуры**
3. Создаем процедуру-обработчик действия команды
4. Модуль формы списка:
```bsl
&НаКлиенте
Процедура НапечататьСписокНоменклатуры(Команда)
	Результат = ВывестиСписокНоменклатуры();
	Результат.ОтображатьСетку = Ложь;
	Результат.ОтображатьЗаголовки = Ложь;
	Результат.Показать();
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВывестиСписокНоменклатуры()
	
	//в качестве схемы компоновки данных используем макет отчета «Список Номенклатуры»
	СхемаКомпоновкиДанных = Отчеты.СписокНоменклатуры.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	//получим настройки по умолчанию
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;

	//сформируем макет компоновки данных	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки);
      
	//выполним компоновку данных
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	
	//выведем результат компоновки в табличный документ
	ДокументРезультат = Новый ТабличныйДокумент;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

	Возврат ДокументРезультат;
	
КонецФункции
```