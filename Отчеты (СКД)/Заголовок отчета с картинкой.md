#ОТЧЕТЫ 
### Задача:
Требуется создать шапку отчета с **Логотипом** и параметром **Дата**, с помощью отдельного макета.
![[Pasted image 20240910154428.png]]
### Решение:
1. Создаем макет **МакетШапка**. Формируем макет, области и создаем параметры
![[Pasted image 20240910153925.png]]
2. Открываем модуль объекта отчета
![[Pasted image 20240910154035.png]]
3. Cоздаем процедуру-событие **ПриКомпоновкеРезультата**
```bsl
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	ДокументРезультат.Очистить();
	
	//++ Заполнение параметра "Период" и вывод Шапки Отчета
	ПараметрПериод = Новый ПараметрКомпоновкиДанных("Период");
	ДатаОтчета = '00010101';
	ВсеПараметры = Настройки.ПараметрыДанных.Элементы;
	Для каждого ТекПараметр Из ВсеПараметры Цикл
		Если ТекПараметр.Параметр = ПараметрПериод Тогда  
			ДатаОтчета = ТекПараметр.Значение.Дата;		
		КонецЕсли;
	КонецЦикла;
	
	МакетШапка = ПолучитьМакет("МакетШапка");
	ОбластьШапка = МакетШапка.ПолучитьОбласть("ШапкаОтчета");
	ОбластьШапка.Параметры.Дата = Формат(ДатаОтчета, "ДФ=dd.MM.yyyy");
	ДокументРезультат.Вывести(ОбластьШапка);
	//-- вывод шапки отчета
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

КонецПроцедуры
```