#ОТЧЕТЫ 
### Задача:
Требуется разработать отчет без использования СКД, с помощью фиксированного макета.
В отчете должны присутствовать группировки по **Организации** и **Контрагенту**
![[Pasted image 20240909152719.png]]
### Решение:
##### Отчет:
1. Создаем отчет **Продажи_ФиксированныйМакет**
2. Создаем реквизит **Период** тип (СтандартныйПериод)
![[Pasted image 20240909142221.png]]
##### Макет:
1. Создаем **Макет** отчета
![[Pasted image 20240909142526.png]]
2. Открываем макет отчета. Устанавливаем ширину колонок
![[Pasted image 20240909142840.png]]
3. Выделяем 34 колонки => Контекстное меню ПКМ => Ширина колонок => 3.00
![[Pasted image 20240909142757.png]]
4. Создаем макет, назначаем именованные области
![[Pasted image 20240909144510.png]]
5. Для выделенных ячеек устанавливаем параметр **Заполнение** равным **Параметр**
![[Pasted image 20240909145045.png]]
6. Для выделенных ячеек устанавливаем параметр **Размещение текста** равным **Переносить**
![[Pasted image 20240909145212.png]]
7. Убеждаемся что для выделенных строк установлена **Автовысота строки**
![[Pasted image 20240909145425.png]]
##### Форма:
1. Создаем форму отчета
![[Pasted image 20240909145754.png]]
2. Создаем реквизит формы **ТабДок** (*ТабличныйДокумент*), переносим его на форму
3. У элемента **ТабДок** в свойствах выбираем **Положение заголовка** равно **Нет**
![[Pasted image 20240909150008.png]]
4. Создаем команду **Сформировать** и переносим ее на форму.
5. Создаем процедуры-обработчики действия команды с директвами **&НаКлиенте** и **&НаСервере**
![[Pasted image 20240909150441.png]]
##### Модуль формы:
```bsl
// Модуль формы
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	//1.1 Получение данных для отчета
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Продажи.Организация 														КАК Организация,
	|	Продажи.Контрагент 														КАК Контрагент,
	|	Продажи.Номенклатура 													КАК Номенклатура,
	|	Продажи.КоличествоОборот												КАК Количество,
	|	Продажи.СуммаОборот 														КАК Выручка,
	|	Продажи.СебестоимостьОборот											КАК Себестоимость,
	|	Продажи.СуммаОборот - Продажи.СебестоимостьОборот	КАК Прибыль
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(&ДатаНачала, &ДатаОкончания) КАК Продажи
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Выручка),
	|	СУММА(Себестоимость),
	|	СУММА(Прибыль)
	|ПО
	|	ОБЩИЕ,
	|	Организация,
	|	Контрагент";
	
	//1.2 Установка параметров и выполнение запроса
	Если Отчет.Период.ДатаНачала = '00010101' Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаНачала", "");
	Иначе
		Запрос.УстановитьПараметр("ДатаНачала", Отчет.Период.ДатаНачала);
	КонецЕсли;
	
	Если Отчет.Период.ДатаОкончания = '00010101' Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаОкончания", "");
	Иначе
		Запрос.УстановитьПараметр("ДатаОкончания", Отчет.Период.ДатаОкончания);
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	
//===========================================================================

	//2. Получение макета и Очищение табличного документа перед заполнением
	Макет = Отчеты.Продажи_ФиксированныйМакет.ПолучитьМакет("Макет");
	ТабДок.Очистить();
	
	//3. Вывод шапки
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(Шапка);
	
	//4.1 Начало группировки строк
	ТабДок.НачатьАвтогруппировкуСтрок();
	
	//5.1 Получение выборки "Общие Итоги" (Уровень 0)
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	
	//6.1 Получение выборки "Организация" (Уровень 1)
	ВыборкаОрганизация = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизация.Следующий() Цикл
		//6.2 Вывод группировки "Организация" (Уровень 1)
		Организация = Макет.ПолучитьОбласть("Организация");
		Организация.Параметры.Заполнить(ВыборкаОрганизация);
		ТабДок.Вывести(Организация, ВыборкаОрганизация.Уровень());
		
		//7.1 Получение выборки "Контрагент" (Уровень 2)
		ВыборкаКонтрагент = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагент.Следующий() Цикл
			//7.2 Вывод группировки "Контрагент" (Уровень 2)
			Контрагент = Макет.ПолучитьОбласть("Контрагент");
			Контрагент.Параметры.Заполнить(ВыборкаКонтрагент);
			ТабДок.Вывести(Контрагент, ВыборкаКонтрагент.Уровень());
		
			//8.1 Получение выборки "ДетальныеЗаписи" (Уровень 3)
			ВыборкаНоменклатура = ВыборкаКонтрагент.Выбрать();
			Пока ВыборкаНоменклатура.Следующий() Цикл
				//8.2 Вывод группировки "ДетальныеЗаписи" (Уровень 3)
				Номенклатура	= Макет.ПолучитьОбласть("Номенклатура");
				Номенклатура.Параметры.Заполнить(ВыборкаНоменклатура);
				ТабДок.Вывести(Номенклатура, ВыборкаНоменклатура.Уровень());
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	//4.2 Окончание группировки строк
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	
	//5.2 Вывод группировки "ОбщиеИтоги" (Уровень 0)
	Подвал = Макет.ПолучитьОбласть("Подвал");
	Подвал.Параметры.Заполнить(ВыборкаОбщийИтог);
	ТабДок.Вывести(Подвал);	

КонецПроцедуры
```