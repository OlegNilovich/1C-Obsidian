#ФОРМЫ 
### Задача:
Требуется передать данные из подчиненной формы в главную форму с помощью метода [[Закрыть()]]
![[Pasted image 20240826024756.png]]
### Решение:
Создаем обработку "ПередачаДанныхВГлавнуюФормуМетодомЗакрыть"
Создаем две формы: "ГлавнаяФорма" и "ПодчиненнаяФорма"
##### Главная форма:
1. Открываем редактор Главной формы
2. Создаем реквизит "Товары". С колонками: "Товар", "Количество", "Цена"
3. Создаем команду "ОткрытьПодчиненнуюФорму" и обработчик ее действия (&НаКлиенте)
![[Pasted image 20240826022821.png]]
4. Модуль формы:
```bsl
&НаКлиенте
Процедура ОткрытьПодчиненнуюФорму(Команда)
	ПутьКФорме = "Обработка.ПередачаДанныхВГлавнуюФорму.Форма.ПодчиненнаяФорма";
	Оповещение = Новый ОписаниеОповещения("ПринятьДанныеПодчиненнойФормы", ЭтотОбъект);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ПутьКФорме ,, ЭтотОбъект,,,, Оповещение, Режим);
КонецПроцедуры

&НаКлиенте
Процедура ПринятьДанныеПодчиненнойФормы(РезультатЗакрытия, ДопПараметры) Экспорт
	Если РезультатЗакрытия <> Неопределено Тогда
		ТоварыИзПодчиненнойФормы = РезультатЗакрытия;
		Для каждого ТоварИзПодчиненнойФормы Из ТоварыИзПодчиненнойФормы Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТоварИзПодчиненнойФормы);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
```
##### Подчиненная форма:
1. Открываем редактор Подчиненной формы
2. Создаем реквизит "Товары" (ТаблицаЗначений) и колонки: "Товар", "Количество", "Цена"
3. Создаем команду "ПередатьДанныеВГлавнуюФорму"
4. Создаем обработчик события формы "ПриСозданииНаСервере"
![[Pasted image 20240826024357.png]]
5. Модуль формы:
```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	НоваяСтрока = Товары.Добавить();
	НоваяСтрока.Товар = "Ложка";
	НоваяСтрока.Количество = 5;
	НоваяСтрока.Цена = 100;
	
	НоваяСтрока = Товары.Добавить();
	НоваяСтрока.Товар = "Вилка";
	НоваяСтрока.Количество = 7;
	НоваяСтрока.Цена = 150;
	
	НоваяСтрока = Товары.Добавить();
	НоваяСтрока.Товар = "Нож";
	НоваяСтрока.Количество = 9;
	НоваяСтрока.Цена = 200;

КонецПроцедуры

&НаКлиенте
Процедура ПередатьДанныеВГлавнуюФорму(Команда)
	Закрыть(Товары);
КонецПроцедуры
```