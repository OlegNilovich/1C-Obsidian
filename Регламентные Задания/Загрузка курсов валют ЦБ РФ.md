### Задача:

Требуется разработать функционал автоматической загрузки курсов валют с сайта ЦБ РФ
1. В форме настроек должна присутствовать возможность включения авто загрузки курса
![[Pasted image 20240829051230.png]]
2. При включении параметра, должно активироваться регламентное задание
![[Pasted image 20240829053311.png]]
3. У пользователя должна быть возможность получить и загрузить курс валют вручную
![[Pasted image 20240829051556.png]]
### Решение:

##### WS-Ссылка:
1. Общие => WS-ссылки => Создаем WS-ссылку "cbrDailyInfo". 
2. Вводим URL импортируемого WSDL:
```
https://www.cbr.ru/DailyInfoWebServ/DailyInfo.asmx?wsdl
```
![[Pasted image 20240829034352.png]]

##### Обработка:
1. Создаем обработку "ЗагрузкаКурсовВалютCBR"
![[Pasted image 20240829033628.png]]
2. Создаем реквизит: "ДатаЗагрузки" тип (Дата, Дата)
3. Создаем табличную часть: "Курсы". Создаем реквизиты табличной части:
- "Наименование" тип (Строка, 50)
- "Код" тип (Строка, 10)
- "СимвольныйКод" тип (Строка, 10)
- "Курс" тип (Число, 10, 4)
- "Кратность" тип (Число, 5, 1)
- "Валюта" тип (СправочникСсылка.Валюты)
###### Модуль объекта обработки:
1. Открываем модуль объекта обработки.
![[Pasted image 20240829035727.png]]
2. Создаем две экспортные процедуры:
```bsl
Процедура ПолучитьКурсы() Экспорт
	
	WSПрокси = WSСсылки.cbrDailyInfo.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");
	ПакетXDTO									= WSПрокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/");
	ТипGetCourseOnDate					= ПакетXDTO.Получить("GetCursOnDate");
	ПараметрВебСервиса					= WSПрокси.ФабрикаXDTO.Создать(ТипGetCourseOnDate);	
	ПараметрВебСервиса.On_date		= ДатаЗагрузки;
	Результат									= WSПрокси.GetCursOnDate(ПараметрВебСервиса);

	Если Результат = Неопределено Тогда
		Сообщить("Не удалось получить курсы валют");    
		Возврат;
	КонецЕсли;
	
	СписокВалют = Результат.GetCursOnDateResult.diffgram.ValuteData.ValuteCursOnDate;
	
	Курсы.Очистить();
	
	Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Валюты.Код КАК						Код,
			|	Валюты.СимвольныйКод КАК		СимвольныйКод,
			|	Валюты.Ссылка КАК					Ссылка
			|ИЗ
			|	Справочник.Валюты КАК			Валюты
			|ГДЕ
			|	НЕ Валюты.ПометкаУдаления");
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаВалют = РезультатЗапроса.Выгрузить();
	ТаблицаВалют.Индексы.Добавить("Код");
	
	Для каждого ОбъектXDTO Из СписокВалют Цикл
		
		СтрокаКурса = Курсы.Добавить();
		СтрокаКурса.Наименование		= ОбъектXDTO.Vname;
		СтрокаКурса.Код						= ОбъектXDTO.Vcode;
		СтрокаКурса.СимвольныйКод	= ОбъектXDTO.VchCode;
		СтрокаКурса.Курс					= ОбъектXDTO.Vcurs;
		СтрокаКурса.Кратность			= ОбъектXDTO.Vnom;
		
		НайденнаяСтрока = ТаблицаВалют.Найти(СтрокаКурса.Код, "Код");
		Если НайденнаяСтрока <> Неопределено Тогда
		  	СтрокаКурса.Валюта = НайденнаяСтрока.Ссылка;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

//====================================================================

Процедура ЗаписатьКурсы() Экспорт
	
	Для каждого СтрокаКурса Из Курсы Цикл
		Если НЕ СтрокаКурса.Валюта.Пустая() Тогда
			Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
			Запись.Период		= ДатаЗагрузки;
			Запись.Валюта		= СтрокаКурса.Валюта;
			Запись.Курс			= СтрокаКурса.Курс;
			Запись.Кратность	= СтрокаКурса.Кратность;
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
```
###### Форма обработки:
1. Открываем окно редактирования формы обработки
2. Переносим реквизиты на форму
![[Pasted image 20240829040947.png]]
3. Создаем две команды: "ЗаписатьКурсы" и "ПолучитьКурсы"
![[Pasted image 20240829041207.png]]
4. Создаем процедуры-обработчики действия команд (нажимаем на лупу)
![[Pasted image 20240829041332.png]]
5. Модуль формы:
```bsl
&НаКлиенте
Процедура ПолучитьКурсы(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаЗагрузки) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите дату загрузки!";
		Сообщение.Поле = "Объект.ДатаЗагрузки";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПолучитьКурсыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьКурсыНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПолучитьКурсы();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьКурсы(Команда)
	ЗаписатьКурсыНаСервере();
	Сообщить("Курсы валют загружены успешно");
КонецПроцедуры

&НаСервере
Процедура ЗаписатьКурсыНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаписатьКурсы();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры
```
##### Общий модуль:
1. Создаем общий модуль "РегламентныеЗаданияСервер".
2. Палитра свойств => Сервер => Устанавливаем флажок
![[Pasted image 20240829042031.png]]
3. Код модуля:
```bsl
Процедура ЗагрузкаКурсовВалютРФ() Экспорт
	Обработка = Обработки.ЗагрузкаКурсовВалютCBR.Создать();
	Обработка.ДатаЗагрузки = ТекущаяДата();
	Обработка.ПолучитьКурсы();
	Обработка.ЗаписатьКурсы();
КонецПроцедуры
```
##### Регламентное задание:
1. Создаем регламентное задание "ЗагрузкаКурсовВалют".
![[Pasted image 20240829044852.png]]
2. Указываем имя метода: РегламентныеЗаданияСервер.ЗагрузкаКурсовВалютРФ
3. Отключаем свойство "Использование"
4. Отрываем "Расписание" и устанавливаем требуемую периодичность и частоту выполнения
![[Pasted image 20240829042647.png]]
##### Константа:
1. Создаем константу "ЗагружатьКурсыВалютРФ" тип (Булево)
![[Pasted image 20240829045038.png]]
2. Указываем основную форму константы: "ФормаКонстант"
![[Pasted image 20240829050308.png]]
3. Открываем Модуль менеджера значения. Код модуля:
```bsl
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ВключитьАвтоматическуюЗагрузкуВалют(Значение);	
КонецПроцедуры

Процедура ВключитьАвтоматическуюЗагрузкуВалют(ЗагрузкаВключена)
	
	СтруктураОтбора = Новый Структура("Ключ", "ЗагрузкаКурсовВалютРФ");
	НайденныеЗадания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(СтруктураОтбора);
	
	Если ЗагрузкаВключена Тогда
		Если НайденныеЗадания.Количество() = 0 Тогда
			//создать новое задание
			РегЗадание = Метаданные.РегламентныеЗадания.ЗагрузкаКурсовВалютРФ;
			Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание(РегЗадание);	
			Задание.Использование = Истина;   
			Задание.Записать();
		КонецЕсли;
	Иначе
		//удалить существующее задание
		Если НайденныеЗадания.Количество() > 0 Тогда
			Задание = НайденныеЗадания[0];
			Задание.Удалить();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
```
