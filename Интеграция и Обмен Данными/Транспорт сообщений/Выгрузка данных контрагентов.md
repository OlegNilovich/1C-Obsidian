```bsl
// Выгрузка данных (контрагентов)

//============================================================================
// Получение данных для выгрузки
Процедура ПолучитьДанные() Экспорт
	
	УзелОбмена_БП = ПланыОбмена.IS_ОбменСБухгалтерией.НайтиПоКоду("БП");
	
	Данные.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.РегистрационныйНомер КАК ОГРН,
		|	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты.Изменения КАК КонтрагентыИзменения
		|		ПО Контрагенты.Ссылка = КонтрагентыИзменения.Ссылка
		|			И (КонтрагентыИзменения.Узел = &УзелОбмена)
		|ГДЕ
		|	Контрагенты.ЭтоГруппа = ЛОЖЬ
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена_БП);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаКонтрагентов = РезультатЗапроса.Выгрузить();
	МассивКонтрагентов = ТаблицаКонтрагентов.ВыгрузитьКолонку("Ссылка");
	
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивКонтрагентов);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Данные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
		//заполнение контактной информации контрагента в строке
		КонтактнаяИнформация = ТаблицаКИ.НайтиСтроки(Новый Структура("Объект", Выборка.Ссылка));
		Для каждого СтрокаКИ Из КонтактнаяИнформация Цикл
			Если СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента Тогда
				НоваяСтрока.ЮрАдрес = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента Тогда
				НоваяСтрока.ФактАдрес = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
				НоваяСтрока.Телефон = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
				НоваяСтрока.ЭлПочта = СтрокаКИ.Представление; 
			КонецЕсли;	
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры


//=============================================================================
// Формирование текстового файла с данными
Процедура ВыгрузитьДанные() Экспорт
	
	Текст = Новый ТекстовыйДокумент; 
	Текст.ДобавитьСтроку("Наименование;НаименованиеПолное;ИНН;КПП;ВидКонтрагента;ОГРН;ЮрАдрес;ФактАдрес;Телефон;ЭлПочта");
	Для каждого СтрокаДанных Из Данные Цикл
		
		МассивДанных = Новый Массив;
		МассивДанных.Добавить(СтрокаДанных.Наименование);
		МассивДанных.Добавить(СтрокаДанных.НаименованиеПолное);
		МассивДанных.Добавить(СтрокаДанных.ИНН);
		МассивДанных.Добавить(СтрокаДанных.КПП);
		МассивДанных.Добавить(СтрокаДанных.ВидКонтрагента);
		МассивДанных.Добавить(СтрокаДанных.ОГРН);
		МассивДанных.Добавить(СтрокаДанных.ЮрАдрес);
		МассивДанных.Добавить(СтрокаДанных.ФактАдрес);
		МассивДанных.Добавить(СтрокаДанных.Телефон);
		МассивДанных.Добавить(СтрокаДанных.ЭлПочта);

		СтрокаКонтрагента = СтрСоединить(МассивДанных, ";");
		Текст.ДобавитьСтроку(СтрокаКонтрагента);
	
	КонецЦикла; 
	
	ВыгрузкаВыполнена = Ложь;		
	
	Если СпособПередачиДанных = "Каталог" Тогда
		ВыгрузитьДанные_Каталог(Текст, ВыгрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "FTP" Тогда
		ВыгрузитьДанные_FTP(Текст, ВыгрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "ЭлПочта" Тогда
		ВыгрузитьДанные_ЭлектроннаяПочта(Текст, ВыгрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "COM" Тогда
		ВыгрузитьДанные_ПрямоеПодключение(Текст, ВыгрузкаВыполнена);             
	КонецЕсли;
		
	//Если ВыгрузкаВыполнена Тогда
	//	//удалить регистрацию изменений
	//	УзелОбменаБП = ПланыОбмена.IS_ОбменСБухгалтерией.НайтиПоКоду("БП");
	//	Если НЕ УзелОбменаБП.Пустая() Тогда
	//		ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбменаБП, Метаданные.Справочники.Контрагенты);
	//	КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры


//============================================================================
// Выгрузка данных в Каталог
Процедура ВыгрузитьДанные_Каталог(Текст, ВыгрузкаВыполнена)
	
	Если ПустаяСтрока(КаталогВыгрузки) Тогда
		КаталогВыгрузки = "D:\Обмен";
	КонецЕсли;

	Файл = Новый Файл(КаталогВыгрузки);
	Если Файл.Существует() Тогда
		ИмяФайла = КаталогВыгрузки + "\Message_UNF_BP.csv";
		Текст.Записать(ИмяФайла, КодировкаТекста.UTF8);
		ВыгрузкаВыполнена = Истина;		
	Иначе                                                                                                    
		ТекстОшибки = "На сервере не найден каталог выгрузки!";
		ЗаписьЖурналаРегистрации("IRONSKILLS_ВыгрузкаКонтрагентовИзУНФ", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
	КонецЕсли;

КонецПроцедуры    


//============================================================================
// Выгрузка данных на FTP-сервер
Процедура ВыгрузитьДанные_FTP(Текст, ВыгрузкаВыполнена)
	
	КаталогВремФайлов = КаталогВременныхФайлов();
	ИмяВремФайла = КаталогВремФайлов + "\Message_UNF_BP.csv";
	Текст.Записать(ИмяВремФайла, КодировкаТекста.UTF8);
	
	ФТП_Сервер = Новый FTPСоединение("195.69.187.68",, "user40650", "lC0DL2E4z9hb",, , 30, ,);
	
	Попытка
		
		ФТП_Сервер.УстановитьТекущийКаталог("/Обмен");   
		ФТП_Сервер.Записать(ИмяВремФайла, "/Обмен/Message_UNF_BP.csv"); 
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Данные успешно записаны на FTP-сервер";
		Сообщение.Сообщить();

	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось записать файл на FTP-сервер, описание ошибки: " + ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
		
КонецПроцедуры


//============================================================================
// Выгрузка данных на Email
Процедура ВыгрузитьДанные_ЭлектроннаяПочта(Текст, ВыгрузкаВыполнена)
	
	КаталогВремФайлов = КаталогВременныхФайлов();
	ИмяВремФайла = КаталогВремФайлов + "\Message_UNF_BP.csv";
	Текст.Записать(ИмяВремФайла, КодировкаТекста.UTF8);    
	
	//описание параметров подключения к почтовому серверу
	ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
	ПочтовыйПрофиль.АдресСервераSMTP = "smtp.yandex.ru";
	ПочтовыйПрофиль.ИспользоватьSSLSMTP = Истина;
	ПочтовыйПрофиль.ПортSMTP = 465;
	ПочтовыйПрофиль.ПользовательSMTP = "testmail.ironskills07@yandex.ru";	
	ПочтовыйПрофиль.ПарольSMTP = "fpkfveitvpvxcwnn";  
	ПочтовыйПрофиль.Таймаут = 30; 
	
	//проверка подключения
	Почта = Новый ИнтернетПочта;        
	
	ПодключениеУстановлено = Ложь;
	
	Попытка
		Почта.Подключиться(ПочтовыйПрофиль);
		ПодключениеУстановлено = Истина;
	Исключение
		Сообщить("Не удалось подключиться к почтовому серверу, причина: " + ОписаниеОшибки());
	КонецПопытки;            
	
	Если НЕ ПодключениеУстановлено Тогда
		Возврат;
	КонецЕсли; 
	
	//создание письма
	Письмо = Новый ИнтернетПочтовоеСообщение;
	Письмо.Тема = "Обмен 1С (УНФ -> БП)";
	Письмо.Тексты.Добавить("В приложении файл с данными из УНФ");
	Письмо.Отправитель = "testmail.ironskills07@yandex.ru";	
	Письмо.ИмяОтправителя = "1С-Обмен";
	Письмо.Получатели.Добавить("testmail.ironskills07@yandex.ru");
	Письмо.Вложения.Добавить(ИмяВремФайла, "КонтрагентыУНФ");
	
	//попытка отправки письма
	Попытка
		Почта.Послать(Письмо);
		Сообщить("Данные успешно отправлены на email");
	Исключение
		Сообщить("Не удалось отправить письмо с данными, причина: " + ОписаниеОшибки());
	КонецПопытки;	
	
	Почта.Отключиться();
	
КонецПроцедуры


//============================================================================
// Выгрузка данных прямым подключением (через COMОбъект)
Процедура ВыгрузитьДанные_ПрямоеПодключение(Текст, ВыгрузкаВыполнена)
	
	ПутьКБазе	 = "D:\Bases_1C\AccountingT"; 
	Пользователь = "Администратор";
	Пароль		 = "";
	
	// Подключение к файловой базе
	Соединение = Новый COMОбъект("V83.COMConnector");
	ПараметрыДоступа = СтрШаблон("File=""%1"";Usr=""%2"";Pwd=""%3"";", ПутьКБазе, Пользователь, Пароль);
	Попытка
		БазаПриемник = Соединение.Connect(ПараметрыДоступа);
	Исключение
		Сообщить("Проблемы при подключении: " + ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
	
	Если БазаПриемник = Неопределено Тогда
		Возврат;
	КонецЕсли;       
	
	ДанныеКонтрагентов = Текст.ПолучитьТекст();
	Результат = БазаПриемник.IS_ОбменДанными.ОбменДаннымиСУНФ(ДанныеКонтрагентов);	
	
	Сообщить(Результат);
	
КонецПроцедуры
```