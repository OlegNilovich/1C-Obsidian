```bsl
// Загрузка данных (контрагентов)


Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт 
	ПрочитатьДанные(); 
	СопоставитьДанные();   
	ЗаписатьДанные();
КонецПроцедуры 


Процедура ПрочитатьДанные() Экспорт
	
	ЗагрузкаВыполнена = Ложь;
	Если СпособПередачиДанных = "Каталог" Тогда
		ЗагрузитьДанные_Каталог(ЗагрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "FTP" Тогда
		ЗагрузитьДанные_FTP(ЗагрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "ЭлПочта" Тогда
		ЗагрузитьДанные_ЭлектроннаяПочта(ЗагрузкаВыполнена);             
	ИначеЕсли СпособПередачиДанных = "COM" Тогда
		ЗагрузитьДанные_ПрямоеПодключение(ЗагрузкаВыполнена);             
	КонецЕсли;
	
КонецПроцедуры

//============================================================================
// Загрузка и чтение данных текстового файла из Каталога
Процедура ЗагрузитьДанные_Каталог(ЗагрузкаВыполнена)

	Если ПустаяСтрока(КаталогЗагрузки) Тогда
		КаталогЗагрузки = "D:\Обмен";
	КонецЕсли;
	
	ПутьКФайлу = КаталогЗагрузки + "\Message_UNF_BP.csv";
	Файл = Новый Файл(ПутьКФайлу);
	
	Если НЕ Файл.Существует() Тогда
		ТекстСообщения = "В каталоге обмена нет файла для загрузки";
		ЗаписьЖурналаРегистрации("IRONSKILLS_ЗагрузкаКонтрагентовВБухгалтерию", УровеньЖурналаРегистрации.Предупреждение,,,ТекстСообщения);
		Возврат;
	КонецЕсли;  

	ПрочитатьТекстовыйФайл(ПутьКФайлу);
		
	ЗагрузкаВыполнена = Истина;

КонецПроцедуры

Процедура ПрочитатьТекстовыйФайл(ПутьКФайлуСДанными)

	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать(ПутьКФайлуСДанными, КодировкаТекста.UTF8);
	
	КоличествоСтрок = Текст.КоличествоСтрок();
	
	Данные.Очистить();
	
	Для НомерСтроки=2 По КоличествоСтрок Цикл
		
		ТекСтрока = Текст.ПолучитьСтроку(НомерСтроки);
	    МассивДанных = СтрРазделить(ТекСтрока, ";");

		Если МассивДанных.Количество() >= 10 Тогда
			НоваяСтрока = Данные.Добавить();
			НоваяСтрока.Наименование		= МассивДанных[0];
			НоваяСтрока.НаименованиеПолное	= МассивДанных[1];
			НоваяСтрока.ИНН					= МассивДанных[2];
			НоваяСтрока.КПП					= МассивДанных[3];
			НоваяСтрока.ВидКонтрагента		= МассивДанных[4];
			НоваяСтрока.ОГРН				= МассивДанных[5];
			НоваяСтрока.ЮрАдрес				= МассивДанных[6];
			НоваяСтрока.ФактАдрес			= МассивДанных[7];
			НоваяСтрока.Телефон				= МассивДанных[8];
			НоваяСтрока.ЭлПочта				= МассивДанных[9];
		КонецЕсли;

	КонецЦикла;


КонецПроцедуры


//============================================================================
// Загрузка и чтение данных текстового файла из FTP-сервера
Процедура ЗагрузитьДанные_FTP(ЗагрузкаВыполнена)

	ФТП_Сервер = Новый FTPСоединение("195.69.187.68",, "user40650", "lC0DL2E4z9hb",, , 30, ,);
	
	Попытка
		
		МассивФайлов = ФТП_Сервер.НайтиФайлы("/Обмен/Message_UNF_BP.csv");
		
		Если МассивФайлов.Количество() > 0 Тогда
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
			
			ФТП_Сервер.Получить("/Обмен/Message_UNF_BP.csv", ИмяВременногоФайла); 
			
			Файл = Новый Файл(ИмяВременногоФайла);
			Если Файл.Существует() Тогда
            	ПрочитатьТекстовыйФайл(ИмяВременногоФайла);
			КонецЕсли;
			
		Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "На FTP-сервере не найден файл с данными";
			Сообщение.Сообщить();
		КонецЕсли;
	
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось подключиться к FTP-серверу";
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры


//============================================================================
// Загрузка и чтение данных текстового файла из электронной почты
Процедура ЗагрузитьДанные_ЭлектроннаяПочта(ЗагрузкаВыполнена)
	
	ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
	ПочтовыйПрофиль.АдресСервераIMAP = "imap.yandex.ru";
	ПочтовыйПрофиль.ИспользоватьSSLIMAP = Истина;
	ПочтовыйПрофиль.ПортIMAP = 993;
	ПочтовыйПрофиль.ПользовательIMAP = "testmail.ironskills07@yandex.ru";
	ПочтовыйПрофиль.ПарольIMAP = "fpkfveitvpvxcwnn";	
	ПочтовыйПрофиль.Таймаут = 30;
	
	Почта = Новый ИнтернетПочта;
	
	ПодключениеУстановлено = Ложь;
	Попытка
		Почта.Подключиться(ПочтовыйПрофиль, ПротоколИнтернетПочты.IMAP);
		ПодключениеУстановлено = Истина;
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось подключиться к почтовому серверу, причина: " + ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;

	Если НЕ ПодключениеУстановлено Тогда
		Возврат;
	КонецЕсли;
	
	МассивПисем = Почта.Выбрать(Истина,,);
	
	Для каждого Письмо Из МассивПисем Цикл
		
		Если Письмо.ИмяОтправителя <> "1С-Обмен" Тогда
			Продолжить;
		КонецЕсли;
		
		ВложенияПисьма = Письмо.Вложения;
		Если ВложенияПисьма.Количество() > 0 Тогда
			
			ВложениеДанные = ВложенияПисьма[0];
			Если СтрНайти(ВложениеДанные.ИмяФайла, "Message_UNF_BP") Тогда
				
				ДанныеФайла = ВложениеДанные.Данные;
				КаталогВремФайлов = КаталогВременныхФайлов();
				ИмяВремФайла = КаталогВремФайлов + "\" + ВложениеДанные.ИмяФайла; 
				
				ДанныеФайла.Записать(ИмяВремФайла);
				
				ПрочитатьТекстовыйФайл(ИмяВремФайла);    
				
				МассивКУдалению = Новый Массив;
				МассивКУдалению.Добавить(Письмо.ИдентификаторСообщения);
				Почта.УдалитьСообщения(МассивКУдалению);
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла; 
	
	Почта.ОчиститьУдаленныеСообщения();
	
КонецПроцедуры          


//============================================================================
// Загрузка и чтение данных текстового файла из FTP-сервера
Процедура ЗагрузитьДанные_ПрямоеПодключение(ЗагрузкаВыполнена)

КонецПроцедуры


//============================================================================
// Сопоставление данных по ИНН и Наименованию
Процедура СопоставитьДанные() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент,
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.ИНН КАК ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ПометкаУдаления = ЛОЖЬ
		|	И Контрагенты.ЭтоГруппа = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаКонтрагентов = РезультатЗапроса.Выгрузить();
	ТаблицаКонтрагентов.Индексы.Добавить("Наименование");	
	ТаблицаКонтрагентов.Индексы.Добавить("ИНН");	
	
	Для каждого СтрокаДанных Из Данные Цикл
		
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
		Если НЕ ПустаяСтрока(СтрокаДанных.ИНН) Тогда
			СтрокаКонтрагента = ТаблицаКонтрагентов.Найти(СтрокаДанных.ИНН, "ИНН");
			Если СтрокаКонтрагента <> Неопределено Тогда
				Контрагент = СтрокаКонтрагента.Контрагент;			
			КонецЕсли;
		КонецЕсли;
		
		Если Контрагент.Пустая() Тогда
			СтрокаКонтрагента = ТаблицаКонтрагентов.Найти(СтрокаДанных.Наименование, "Наименование");
			Если СтрокаКонтрагента <> Неопределено Тогда
				Контрагент = СтрокаКонтрагента.Контрагент;			
			КонецЕсли;
		КонецЕсли;
		
		СтрокаДанных.Контрагент = Контрагент;
	
	КонецЦикла;

КонецПроцедуры


//============================================================================
// Запись новых контрагентов в справочник
Процедура ЗаписатьДанные() Экспорт

	ВидКИ_ЮрАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
	ВидКИ_ФактАдрес = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	ВидКИ_Телефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	ВидКИ_ЭлПочта = Справочники.ВидыКонтактнойИнформации.EmailКонтрагенты;
	
	РФ = Справочники.СтраныМира.Россия;
	
	Для каждого СтрокаДанных Из Данные Цикл
		
		Если НЕ СтрокаДанных.Контрагент.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		НовыйКонтрагент.Наименование = СтрокаДанных.Наименование;
		НовыйКонтрагент.НаименованиеПолное = СтрокаДанных.НаименованиеПолное;
		НовыйКонтрагент.ИНН = СтрокаДанных.ИНН;
		НовыйКонтрагент.КПП = СтрокаДанных.КПП; 
		НовыйКонтрагент.СтранаРегистрации = РФ; 
		
		Если СтрокаДанных.ВидКонтрагента = "Физическое лицо"
			ИЛИ СтрокаДанных.ВидКонтрагента = "Индивидуальный предприниматель" Тогда
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		ИначеЕсли СтрокаДанных.ВидКонтрагента = "Юридическое лицо" Тогда
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		Иначе
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка();
		КонецЕсли; 
		
		НовыйКонтрагент.ЮридическоеФизическоеЛицо = ВидКонтрагента;
		НовыйКонтрагент.РегистрационныйНомер = СтрокаДанных.ОГРН;
		
		Если НЕ ПустаяСтрока(СтрокаДанных.ЮрАдрес) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ЮрАдрес, ВидКИ_ЮрАдрес);
		КонецЕсли;

		Если НЕ ПустаяСтрока(СтрокаДанных.ФактАдрес) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ФактАдрес, ВидКИ_ФактАдрес);
		КонецЕсли;

		Если НЕ ПустаяСтрока(СтрокаДанных.Телефон) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.Телефон, ВидКИ_Телефон);
		КонецЕсли;

		Если НЕ ПустаяСтрока(СтрокаДанных.Телефон) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ЭлПочта, ВидКИ_ЭлПочта);
		КонецЕсли;
		
		НовыйКонтрагент.ОбменДанными.Загрузка = Истина;
		
		НовыйКонтрагент.Записать();    
		
		СтрокаДанных.Контрагент = НовыйКонтрагент.Ссылка;	
		
	КонецЦикла;

КонецПроцедуры
```