```bsl
// Простой обмен (Выгрузка данных из базы источника)

// Выгружаем данные контрагентов из базы-источника в текстовый файл
// Загружаем данные контрагентов в базу-приемник из текстового файла
// Выполняем сопостовление данных файла и данных в базе-приемнике
// Создаем новых контрагентов, которых нет в базе-приемнике


// 1.1 Создаем Обработку "ВыгрузкаКонтрагентовИзУНФ"
// - Создаем реквизит "КаталогВыгрузки" (Строка, 150)

// - Создаем табличную часть "Данные"
// - Создаем реквизит ТЧ "Наименование" (Строка, 100)
// - Создаем реквизит ТЧ "НаименованиеПолное" (Строка, 150)
// - Создаем реквизит ТЧ "ИНН" (Строка, 20)
// - Создаем реквизит ТЧ "КПП" (Строка, 20)
// - Создаем реквизит ТЧ "ВидКонтрагента" (Строка, 50)
// - Создаем реквизит ТЧ "ОГРН" (Строка, 20)
// - Создаем реквизит ТЧ "ЮрАдрес" (Строка, 150)
// - Создаем реквизит ТЧ "ФактАдрес" (Строка, 150)
// - Создаем реквизит ТЧ "Телефон" (Строка, 50)
// - Создаем реквизит ТЧ "ЭлПочта" (Строка, 50)

// 1.2 Модуль объекта обработки:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    
    ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
    РегистрационныеДанные = Новый Структура;
    РегистрационныеДанные.Вставить("Вид","ДополнительнаяОбработка");
    РегистрационныеДанные.Вставить("Наименование", Синоним);
    РегистрационныеДанные.Вставить("Версия", "1.0");
    РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
    РегистрационныеДанные.Вставить("Информация", "Обработка """ + Синоним + """");
    
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовСерверногоМетода", Истина, );
        
    РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
    
    Возврат РегистрационныеДанные;
                                       
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
    
    Команды = Новый ТаблицаЗначений;
    Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
    Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	
    Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(	ТаблицаКоманд, 
								Представление, 
								Идентификатор, 
								Использование = "ОткрытиеФормы", 
								ПоказыватьОповещение = Ложь, 
								Модификатор = "ПечатьMXL")
    
    НоваяКоманда = ТаблицаКоманд.Добавить();
    НоваяКоманда.Представление = Представление;
    НоваяКоманда.Идентификатор = Идентификатор;
    НоваяКоманда.Использование = Использование;
    НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
    НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры 

#КонецОбласти

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт 
	
	ПолучитьДанные();   
	ВыгрузитьДанные();

КонецПроцедуры

//===========================================================================

Процедура ПолучитьДанные() Экспорт
	
	Данные.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Наименование КАК Наименование,
		|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
		|	Контрагенты.ИНН КАК ИНН,
		|	Контрагенты.КПП КАК КПП,
		|	Контрагенты.РегистрационныйНомер КАК ОГРН,
		|	Контрагенты.ВидКонтрагента КАК ВидКонтрагента,
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ЭтоГруппа = ЛОЖЬ
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаКонтрагентов = РезультатЗапроса.Выгрузить();
	МассивКонтрагентов = ТаблицаКонтрагентов.ВыгрузитьКолонку("Ссылка");
	
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивКонтрагентов,,, ТекущаяДата());
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Данные.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
		//заполнение контактной информации контрагента в строке
		КонтактнаяИнформация = ТаблицаКИ.НайтиСтроки(Новый Структура("Объект", Выборка.Ссылка));
		Для каждого СтрокаКИ Из КонтактнаяИнформация Цикл
			Если СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента Тогда
				НоваяСтрока.ЮрАдрес = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента Тогда
				НоваяСтрока.ФактАдрес = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
				НоваяСтрока.Телефон = СтрокаКИ.Представление; 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
				НоваяСтрока.ЭлПочта = СтрокаКИ.Представление; 
			КонецЕсли;	
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

//===========================================================================

Процедура ВыгрузитьДанные() Экспорт
	
	Текст = Новый ТекстовыйДокумент; 
	Текст.ДобавитьСтроку("Наименование;НаименованиеПолное;ИНН;КПП;ВидКонтрагента;ОГРН;ЮрАдрес;ФактАдрес;Телефон;ЭлПочта");
	Для каждого СтрокаДанных Из Данные Цикл
		
		МассивДанных = Новый Массив;
		МассивДанных.Добавить(СтрокаДанных.Наименование);
		МассивДанных.Добавить(СтрокаДанных.НаименованиеПолное);
		МассивДанных.Добавить(СтрокаДанных.ИНН);
		МассивДанных.Добавить(СтрокаДанных.КПП);
		МассивДанных.Добавить(СтрокаДанных.ВидКонтрагента);
		МассивДанных.Добавить(СтрокаДанных.ОГРН);
		МассивДанных.Добавить(СтрокаДанных.ЮрАдрес);
		МассивДанных.Добавить(СтрокаДанных.ФактАдрес);
		МассивДанных.Добавить(СтрокаДанных.Телефон);
		МассивДанных.Добавить(СтрокаДанных.ЭлПочта);

		СтрокаКонтрагента = СтрСоединить(МассивДанных, ";");
		Текст.ДобавитьСтроку(СтрокаКонтрагента);
	
	КонецЦикла; 
	
	Если НЕ ЗначениеЗаполнено(КаталогВыгрузки) Тогда
		КаталогВыгрузки = "C:\Обмен";
	КонецЕсли;
	
	Файл = Новый Файл(КаталогВыгрузки);
	Если Файл.Существует() Тогда
		ИмяФайла = КаталогВыгрузки + "\Message_UNF_BP.csv";
		Текст.Записать(ИмяФайла, КодировкаТекста.UTF8);
	Иначе                                                                                                    
		ТекстОшибки = "На сервере не найден каталог выгрузки!";
		ЗаписьЖурналаРегистрации("IRONSKILLS_ВыгрузкаКонтрагентовИзУНФ", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

//============================================================================

// 1.3 Создаем форму обработки:
// - Создаем команду "ВыгрузитьДанные" и размещаем ее на форме
// - Создаем команду "ПолучитьДанные" и размещаем ее на форме
// - Размещаем на форме реквизит "КаталогВыгрузки" и таблицу "Данные"
// Модуль формы:

&НаКлиенте
Асинх Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Проводник.Заголовок = "Выберите куда сохранить файл";
	
	МассивКаталогов = ЖДАТЬ Проводник.ВыбратьАсинх();
	
	Если МассивКаталогов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КаталогВыгрузки = МассивКаталогов[0];
	
КонецПроцедуры

//===========================================================================

&НаКлиенте
Процедура ПолучитьДанные(Команда)
	ПолучитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ПолучитьДанные();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

//===========================================================================

&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	ВыгрузитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДанныеНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ВыгрузитьДанные();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

```