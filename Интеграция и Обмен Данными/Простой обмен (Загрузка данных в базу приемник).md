```bsl
// 1.2 Простой обмен (Загрузка данных в базу приемник)

// Выгружаем данные контрагентов из базы-источника в текстовый файл
// Загружаем данные контрагентов в базу-приемник из текстового файла
// Выполняем сопостовление данных файла и данных в базе-приемнике
// Создаем новых контрагентов, которых нет в базе-приемнике


// 1.1 Создаем Обработку "ВыгрузкаКонтрагентовИзУНФ"
// - Создаем реквизит "КаталогЗагрузки" (Строка, 150)

// - Создаем табличную часть "Данные"
// - Создаем реквизит ТЧ "Наименование" (Строка, 100)
// - Создаем реквизит ТЧ "НаименованиеПолное" (Строка, 150)
// - Создаем реквизит ТЧ "ИНН" (Строка, 20)
// - Создаем реквизит ТЧ "КПП" (Строка, 20)
// - Создаем реквизит ТЧ "ВидКонтрагента" (Строка, 50)
// - Создаем реквизит ТЧ "ОГРН" (Строка, 20)
// - Создаем реквизит ТЧ "ЮрАдрес" (Строка, 150)
// - Создаем реквизит ТЧ "ФактАдрес" (Строка, 150)
// - Создаем реквизит ТЧ "Телефон" (Строка, 50)
// - Создаем реквизит ТЧ "ЭлПочта" (Строка, 50)
// - Создаем реквизит ТЧ "Контрагент" (СправочникСсылка.Контрагенты)

// 1.2 Модуль объекта обработки:

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Вид","ДополнительнаяОбработка");
	РегистрационныеДанные.Вставить("Наименование", Синоним);
	РегистрационныеДанные.Вставить("Версия", "1.0");
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	РегистрационныеДанные.Вставить("Информация", "Обработка """ + Синоним + """");
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовСерверногоМетода", Истина, );
	
	РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(	ТаблицаКоманд, 
								Представление, 
								Идентификатор, 
								Использование = "ОткрытиеФормы", 
								ПоказыватьОповещение = Ложь, 
								Модификатор = "ПечатьMXL")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

#КонецОбласти

//===========================================================================

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт 
	ПрочитатьДанные(); 
	СопоставитьДанные();   
	ЗаписатьДанные();
КонецПроцедуры 


Процедура ПрочитатьДанные() Экспорт
	
	Если НЕ ЗначениеЗаполнено(КаталогЗагрузки) Тогда
		КаталогЗагрузки = "C:\Обмен";
	КонецЕсли;
	
	ПутьКФайлу = КаталогЗагрузки + "\Message_UNF_BP.csv";
	Файл = Новый Файл(ПутьКФайлу);
	
	Если НЕ Файл.Существует() Тогда
		ТекстСообщения = "В каталоге обмена нет файла для загрузки";
		ЗаписьЖурналаРегистрации("IRONSKILLS_ЗагрузкаКонтрагентовВБухгалтерию", УровеньЖурналаРегистрации.Предупреждение,,,ТекстСообщения);
		Возврат;
	КонецЕсли;  
	
	Данные.Очистить();
	
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать(ПутьКФайлу, КодировкаТекста.UTF8);
	КоличествоСтрок = Текст.КоличествоСтрок();
	
	Для НомерСтроки=2 По КоличествоСтрок Цикл
	
		ТекСтрока = Текст.ПолучитьСтроку(НомерСтроки);
		МассивДанных = СтрРазделить(ТекСтрока, ";");
		
		Если МассивДанных.Количество() >= 10 Тогда
			НоваяСтрока							= Данные.Добавить();
			НоваяСтрока.НаименованиеПолное	= МассивДанных[1];
			НоваяСтрока.Наименование			= МассивДанных[0];
			НоваяСтрока.ИНН						= МассивДанных[2];
			НоваяСтрока.КПП						= МассивДанных[3];
			НоваяСтрока.ВидКонтрагента			= МассивДанных[4];
			НоваяСтрока.ОГРН						= МассивДанных[5];
			НоваяСтрока.ЮрАдрес					= МассивДанных[6];
			НоваяСтрока.ФактАдрес				= МассивДанных[7];
			НоваяСтрока.Телефон					= МассивДанных[8];
			НоваяСтрока.ЭлПочта					= МассивДанных[9];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура СопоставитьДанные() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК			Контрагент,
	|	Контрагенты.Наименование КАК	Наименование,
	|	Контрагенты.ИНН КАК				ИНН
	|ИЗ
	|	Справочник.Контрагенты КАК		Контрагенты
	|ГДЕ
	|	Контрагенты.ПометкаУдаления = ЛОЖЬ
	|	И Контрагенты.ЭтоГруппа = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаКонтрагентов = РезультатЗапроса.Выгрузить();
	ТаблицаКонтрагентов.Индексы.Добавить("Наименование");	
	ТаблицаКонтрагентов.Индексы.Добавить("ИНН");	
	
	Для каждого СтрокаДанных Из Данные Цикл
		
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
		Если НЕ ПустаяСтрока(СтрокаДанных.ИНН) Тогда
			СтрокаКонтрагента = ТаблицаКонтрагентов.Найти(СтрокаДанных.ИНН, "ИНН");
			Если СтрокаКонтрагента <> Неопределено Тогда
				Контрагент = СтрокаКонтрагента.Контрагент;			
			КонецЕсли;
		КонецЕсли;
		
		Если Контрагент.Пустая() Тогда
			СтрокаКонтрагента = ТаблицаКонтрагентов.Найти(СтрокаДанных.Наименование, "Наименование");
			Если СтрокаКонтрагента <> Неопределено Тогда
				Контрагент = СтрокаКонтрагента.Контрагент;			
			КонецЕсли;
		КонецЕсли;
		
		СтрокаДанных.Контрагент = Контрагент;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ЗаписатьДанные() Экспорт
	
	ВидКИ_ЮрАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
	ВидКИ_ФактАдрес = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
	ВидКИ_Телефон = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
	ВидКИ_ЭлПочта = Справочники.ВидыКонтактнойИнформации.EmailКонтрагенты;
	
	РФ = Справочники.СтраныМира.Россия;
	
	Для каждого СтрокаДанных Из Данные Цикл
		
		Если НЕ СтрокаДанных.Контрагент.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		НовыйКонтрагент.Наименование = СтрокаДанных.Наименование;
		НовыйКонтрагент.НаименованиеПолное = СтрокаДанных.НаименованиеПолное;
		НовыйКонтрагент.ИНН = СтрокаДанных.ИНН;
		НовыйКонтрагент.КПП = СтрокаДанных.КПП; 
		НовыйКонтрагент.СтранаРегистрации = РФ; 
		
		Если СтрокаДанных.ВидКонтрагента = "Физическое лицо"
			ИЛИ СтрокаДанных.ВидКонтрагента = "Индивидуальный предприниматель" Тогда
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		ИначеЕсли СтрокаДанных.ВидКонтрагента = "Юридическое лицо" Тогда
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		Иначе
			ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка();
		КонецЕсли; 
		
		НовыйКонтрагент.ЮридическоеФизическоеЛицо = ВидКонтрагента;
		НовыйКонтрагент.РегистрационныйНомер = СтрокаДанных.ОГРН;
		
		Если НЕ ПустаяСтрока(СтрокаДанных.ЮрАдрес) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ЮрАдрес, ВидКИ_ЮрАдрес);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(СтрокаДанных.ФактАдрес) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ФактАдрес, ВидКИ_ФактАдрес);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(СтрокаДанных.Телефон) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.Телефон, ВидКИ_Телефон);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(СтрокаДанных.Телефон) Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, СтрокаДанных.ЭлПочта, ВидКИ_ЭлПочта);
		КонецЕсли;
		
		НовыйКонтрагент.ОбменДанными.Загрузка = Истина;
		
		НовыйКонтрагент.Записать();    
		
		СтрокаДанных.Контрагент = НовыйКонтрагент.Ссылка;	
		
	КонецЦикла;
	
КонецПроцедуры

//============================================================================

// 1.3 Создаем форму обработки:
// - Создаем команду "ПрочитатьФайл" и размещаем ее на форме
// - Создаем команду "СопоставитьДанные" и размещаем ее на форме
// - Создаем команду "ЗаписатьДанные" и размещаем ее на форме
// - Размещаем на форме реквизит "КаталогЗагрузки" и таблицу "Данные"
// Модуль формы:

&НаКлиенте
Асинх Процедура КаталогЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Проводник.Заголовок = "Выберите каталог с данными для загрузки";
	
	ВыбранныеКаталоги = ЖДАТЬ Проводник.ВыбратьАсинх();
	
	Если ВыбранныеКаталоги = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КаталогЗагрузки = ВыбранныеКаталоги[0];

КонецПроцедуры

//===========================================================================

&НаКлиенте
Процедура ПрочитатьФайл(Команда)
	ПрочитатьФайлНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФайлНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ПрочитатьДанные();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

//===========================================================================

&НаКлиенте
Процедура СопоставитьДанные(Команда)
	СопоставитьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура СопоставитьДанныеНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.СопоставитьДанные();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

//===========================================================================

&НаКлиенте
Процедура ЗаписатьДанные(Команда)
	ЗаписатьДанныеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаписатьДанные();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

```