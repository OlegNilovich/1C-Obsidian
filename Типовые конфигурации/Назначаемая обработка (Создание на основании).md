```bsl
// Назначаемая обработка (Создание на основании: Команда)

// Копируем шаблон "СозданиеНаОсновании" с названием "ОтчетПроизводстваНаОснованииСчета"
// Открываем обработку в конфигураторе
// Открываем модуль объекта обработки => Функция СведенияОВнешнейОбработке()
// Указываем назначение в строчке: Назначение.Добавить("Документ.СчетНаОплатуПокупателю");
// Открываем справочник "Дополнительные отчеты и обработки" В 1С:Предприятии
// Нажимаем кнопку создать и указываем путь до нашей обработки


// Модуль объекта:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	Назначения = Новый Массив ;
	Назначения.Добавить("Документ.СчетНаОплатуПокупателю") ;
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Вид","СозданиеСвязанныхОбъектов");
	РегистрационныеДанные.Вставить("Наименование", Синоним);
	РегистрационныеДанные.Вставить("Версия", "1.0");
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	РегистрационныеДанные.Вставить("Информация", "Заполнение объекта");
	РегистрационныеДанные.Вставить("Назначение",Назначения);
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовКлиентскогоМетода", Истина, );
	
	РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, 
	Представление, 
	Идентификатор, 
	Использование = "ОткрытиеФормы", 
	ПоказыватьОповещение = Ложь, 
	Модификатор = "ПечатьMXL")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

#КонецОбласти

//============================================================================

// Модуль формы:
&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив, СозданныеОбъекты) Экспорт
	
	Если ОбъектыНазначенияМассив.Количество() > 0 Тогда
		
		СсылкаНаСчет = ОбъектыНазначенияМассив[0];
		СсылкаНаОтчетПроизводства = СоздатьОтчетПроизводстваНаОснованииСчета(СсылкаНаСчет);
		
		ПараметрыФормы = Новый Структура("Ключ", СсылкаНаОтчетПроизводства);
		ОткрытьФорму("Документ.ОтчетПроизводстваЗаСмену.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьОтчетПроизводстваНаОснованииСчета(СсылкаНаСчет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетНаОплатуПокупателюТовары.Номенклатура,
		|	СчетНаОплатуПокупателюТовары.Количество
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
		|ГДЕ
		|	СчетНаОплатуПокупателюТовары.Ссылка = &СсылкаНаСчет";
	
	Запрос.УстановитьПараметр("СсылкаНаСчет", СсылкаНаСчет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	ДокОбъект = Документы.ОтчетПроизводстваЗаСмену.СоздатьДокумент();
	ДокОбъект.Дата = ТекущаяДатаСеанса();

	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДокОбъект.Продукция.Добавить();		
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.Количество = Выборка.Количество;
		
	КонецЦикла;
	
	ДокОбъект.Комментарий = "Создан на основании: " + СсылкаНаСчет;
	ДокОбъект.Записать();
	
	Возврат ДокОбъект.Ссылка;

КонецФункции	
```