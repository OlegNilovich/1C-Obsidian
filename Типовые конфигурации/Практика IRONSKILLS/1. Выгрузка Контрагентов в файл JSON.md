Пример: [[Создание JSON]]
Шаблон: [[ОткрытиеФормы_ДополнительнаяОбработка]]
### Задача:
Требуется создать обработку для выгрузки данных *Контрагентов* в файл JSON. 
Команда *Заполнить* - заполняет таблицу сведениями о контрагентах.
Команда *Выгрузить* - создает JSON-файл в выбранном пользователем каталоге.
![[Pasted image 20241114091412.png]]
### Решение:

#### Конфигуратор:

1. Копируем шаблон [[ОткрытиеФормы_ДополнительнаяОбработка]] и называем его *ПР_ВыгрузкаКонтрагенотвJSON*
2. Открываем форму обработки, создаем реквизит *ТаблицаКонтрагентов* (ТаблицаЗначений) с колонками:
- Наименование (Строка, 150)
- ИНН (Строка, 20)
- КПП (Строка, 20)
- НомерТелефона (Строка, 50)
3. Переносим таблицу контрагентов на форму со всеми колонками
![[Pasted image 20241114084820.png]]

4. Определяем необходимый состав команд на командной панели таблицы
![[Pasted image 20241114084957.png]]

5. Создаем две команды *Заполнить* и *Выгрузить*
6. Модуль формы:
```bsl

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Наименование КАК						Наименование,
		|	Контрагенты.НомерТелефонаДляПоиска КАК		НомерТелефона,
		|	Контрагенты.ИНН КАК										ИНН,
		|	Контрагенты.КПП КАК										КПП
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ЭтоГруппа
		|	И НЕ Контрагенты.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Проводник.Заголовок = "Выберите каталог для выгрузки данных";

	Если НЕ Проводник.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	МассивКонтрагентов = Новый Массив;
	
	Для каждого ТекСтрока Из ТаблицаКонтрагентов Цикл
		СтруктураКонтрагента = Новый Структура;
		СтруктураКонтрагента.Вставить("Наименование", ТекСтрока.Наименование);
		СтруктураКонтрагента.Вставить("ИНН", ТекСтрока.ИНН);
		СтруктураКонтрагента.Вставить("КПП", ТекСтрока.КПП);
		СтруктураКонтрагента.Вставить("НомерТелефона", ТекСтрока.НомерТелефона);
		МассивКонтрагентов.Добавить(СтруктураКонтрагента);
	КонецЦикла;                                               
	
	ПутьКФайлу = Проводник.Каталог + "\" + "Список контрагентов.json";
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлу,,, Новый ПараметрыЗаписиJSON(, Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, МассивКонтрагентов); 
	ЗаписьJSON.Закрыть();
	
	Текст= "Данные выгружены успешно!";
	Нажатие	= Новый ОписаниеОповещения("ПослеВырузкиКонтрагентов", ЭтотОбъект, Проводник.Каталог);
	Пояснение = "Нажмите, чтобы открыть каталог выгрузки данных";
	Картинка = БиблиотекаКартинок.Оповещения;
	Статус = СтатусОповещенияПользователя.Важное;
	ПоказатьОповещениеПользователя(Текст, Нажатие, Пояснение, Картинка, Статус, Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВырузкиКонтрагентов(Каталог) Экспорт
	ЗапуститьПриложение(Каталог);
КонецПроцедуры
```

#### 1С:Предприятие

1. Открываем 1С:Предприятие => Настройки => Администрирование => Печатные формы, отчеты и обработки
![[Pasted image 20241114085822.png]]

2. Включаем функциональную опцию *Дополнительные отчеты и обработки*
3. Переходим по гиперссылке *Дополнительные отчеты и обработки*
![[Pasted image 20241114085956.png]]

4. Нажимаем команду *Добавить из файла...*
![[Pasted image 20241114090211.png]]

5. В открывшемся окне выбираем *Продолжить*
![[Pasted image 20241114090315.png]]

6. В диалоге выбора файла выбираем нашу внешнюю обработку
![[Pasted image 20241114090404.png]]

7. Выбираем раздел (подсистему), где будет отображаться наша обработка, например раздел *Продажи*
![[Pasted image 20241114090535.png]]

8. Добавляем необходимых пользователей в быстрый доступ, чтобы они могли увидеть обработку
![[Pasted image 20241114090709.png]]

9. Записываем и закрываем форму обработки
![[Pasted image 20241114090812.png]]

10. Переходим в раздел *Продажи* => *Дополнительные обработки*
![[Pasted image 20241114090958.png]]

11. Для пользователей добавленных в быстрый доступ (см пункт 8.), наша обработка будет видна в списке сразу
![[Pasted image 20241114091149.png]]
Для других пользователей обработка будет доступна по команде *Настроить список*
![[Pasted image 20241114091315.png]]