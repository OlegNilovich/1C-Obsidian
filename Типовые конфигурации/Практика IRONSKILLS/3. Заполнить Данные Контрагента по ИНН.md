Пример: [[Заполнить Данные Контрагента по ИНН (egrul.itsoft.ru)]]
Шаблон: [[ЗаполнениеОбъекта_БезЗаписи]]
### Задача:

В справочник "Контрагенты", требуется добавить кнопку для заполнения реквизитов по ИНН
![[Pasted image 20241114125048.png]]
### Решение:

#### Конфигуратор:

1. Копируем шаблон [[ЗаполнениеОбъекта_БезЗаписи]] и называем его *ПР_ЗаполнитьРеквизитыКонтрагентаПоИНН*
2. Открываем обработку в конфигураторе и переходим в *Модуль объекта*
![[Pasted image 20241114101516.png]]

3. В функции *СведенияОВнешнейОбработке()* меняем назначение на *"Справочник.Контрагенты"*
![[Pasted image 20241114101817.png]]

4. Модуль объекта:
```bsl

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);

	Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиЗаполнениеОбъекта();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Вид						= Вид;
	ПараметрыРегистрации.Версия					= "1.0.0.1";
	ПараметрыРегистрации.Наименование		= Синоним;
	ПараметрыРегистрации.БезопасныйРежим	= Ложь;
	ПараметрыРегистрации.Информация			= "Обработка заполнения";
	
	ПараметрыРегистрации.Назначение.Добавить("Справочник.Контрагенты");

	Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление					= Синоним;
	Команда.Идентификатор		 			= ИмяОбработки;
	Команда.Использование					= Использование;
	Команда.ПоказыватьОповещение	= Истина;
	
	Возврат ПараметрыРегистрации; 
	
КонецФункции

Процедура ВыполнитьКоманду(ИмяКоманды, ОбъектыНазначения, ПараметрыВыполнения) Экспорт
	
	ЭтаФорма = ПараметрыВыполнения.ЭтаФорма;
	
	ДанныеКонтрагента = ЗК_ПолучитьДанныеКонтрагентаПоИНН(ЭтаФорма.Объект.ИНН);
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма.Объект, ДанныеКонтрагента);
	
	Если ДанныеКонтрагента.Свойство("ЮридическийАдрес") 
		И ДанныеКонтрагента.ЮридическийАдрес <> Неопределено Тогда
		
		ДанныеЗаполненияКИ = Новый Структура;
		ДанныеЗаполненияКИ.Вставить("Представление", ДанныеКонтрагента.ЮридическийАдрес);
		ДанныеЗаполненияКИ.Вставить("Комментарий", "");
		ДанныеЗаполненияКИ.Вставить("Значение", "");
		
		ВидКИ = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		КонтактнаяИнформацияУНФ.ЗаполнитьЗначениеКонтактнойИнформации(ЭтаФорма, ВидКИ, 
		ДанныеЗаполненияКИ, Истина);

		ВидКИ = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
		КонтактнаяИнформацияУНФ.ЗаполнитьЗначениеКонтактнойИнформации(ЭтаФорма, ВидКИ, 
		ДанныеЗаполненияКИ, Ложь);		
		
	КонецЕсли;
	
	ПараметрыВыполнения.ЭтаФорма.Модифицированность = Истина;
	ТекстСообщения = НСтр("ru = 'Данные контрагента успешно заполнены'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти


Функция ЗК_ПолучитьДанныеКонтрагентаПоИНН(ИНН)
	
	ДанныеКонтрагента = Новый Структура;
	ДанныеКонтрагента.Вставить("ИНН", ИНН); 
	
	АдресСервера = "egrul.itsoft.ru";
	АдресРесурса = "/short_data/?" + ИНН;
	
	Соеденения = Новый HTTPСоединение(АдресСервера,,,,, 30, Новый ЗащищенноеСоединениеOpenSSL);
	
	Запрос = Новый HTTPЗапрос(АдресРесурса);
	
	Попытка
		Ответ = Соеденения.Получить(Запрос);	
	Исключение
		Сообщить("Не удалось получить данные по ИНН по причине: " + ОписаниеОшибки());
		Возврат ДанныеКонтрагента;
	КонецПопытки;
	
	ДанныеЕГРБЛ = Неопределено;
	
	Если Ответ.КодСостояния = 200 Тогда
		
		СтрокаJSON = Ответ.ПолучитьТелоКакСтроку();
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		
		ДанныеЕГРБЛ = ПрочитатьJSON(ЧтениеJSON, Истина);
		
	Иначе
		
		Текст = СтрШаблон("Не удалось получать данные по ИНН...
		|Код состояния: %1
		|Ответ сервера: %2", Ответ.КодСостояния, СтрокаJSON);
		
		Сообщить(Текст);
		Возврат ДанныеКонтрагента;
		
	КонецЕсли; 
	
	Если ДанныеЕГРБЛ["short_form"] <> Неопределено И ДанныеЕГРБЛ["short_form"] = "ИП" Тогда
		ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель;
	Иначе
		ВидКонтрагента = Перечисления.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо;
	КонецЕсли;
	
	ДанныеКонтрагента.Вставить("ВидКонтрагента"				, ВидКонтрагента);
	ДанныеКонтрагента.Вставить("КПП"								, ДанныеЕГРБЛ["kpp"]);
	ДанныеКонтрагента.Вставить("РегистрационныйНомер"	, ДанныеЕГРБЛ["ogrn"]);
	ДанныеКонтрагента.Вставить("Наименование"				, ДанныеЕГРБЛ["short_name"]);
	ДанныеКонтрагента.Вставить("НаименованиеПолное"		, ДанныеЕГРБЛ["full_name"]);
	
	Если ДанныеЕГРБЛ["address"] <> Неопределено Тогда
		ДанныеКонтрагента.Вставить("ЮридическийАдрес", ДанныеЕГРБЛ["address"]);	
	КонецЕсли;
	
	Возврат ДанныеКонтрагента;
	
КонецФункции // ЗК_ПолучитьДанныеКонтрагентаПоИНН()
```