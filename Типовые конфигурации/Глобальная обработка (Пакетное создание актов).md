```bsl
// Глобальная обработка "Пакетное создание актов"

// Копируем шаблона "ВнешняяОбработка" с названием "СозданиеАктовВыполненныхРабот"
// Открываем копию шаблона в конфигураторе
// Открываем справочник "Дополнительные отчеты и обработки" В 1С:Предприятии
// Нажимаем кнопку создать и указываем путь до нашей обработки
// Во вкладке "Команды" указываем раздел для нашей обработки, например "Продажи"
// В той же вкладке настраиваем быстрый доступ для всех пользователей нашей обработки
// Наша обработка находится по пути: "Продажи" => "Сервис" => "Дополнительные обработки"


//=============================================================================

// Модуль объекта обработки:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    
    ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
    РегистрационныеДанные = Новый Структура;
    РегистрационныеДанные.Вставить("Вид","ДополнительнаяОбработка");
    РегистрационныеДанные.Вставить("Наименование", Синоним);
    РегистрационныеДанные.Вставить("Версия", "1.0");
    РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
    РегистрационныеДанные.Вставить("Информация", "Обработка """ + Синоним + """");
    
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ОткрытиеФормы", Истина, );
        
    РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
    
    Возврат РегистрационныеДанные;
                                       
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
    
    Команды = Новый ТаблицаЗначений;
    Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
    Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
    Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
    Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, 
						  Представление, 
						  Идентификатор, 
						  Использование = "ОткрытиеФормы", 
						  ПоказыватьОповещение = Ложь, 
						  Модификатор = "ПечатьMXL")
    
    НоваяКоманда = ТаблицаКоманд.Добавить();
    НоваяКоманда.Представление = Представление;
    НоваяКоманда.Идентификатор = Идентификатор;
    НоваяКоманда.Использование = Использование;
    НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
    НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры 

#КонецОбласти

//============================================================================

// Модуль формы обработки:
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗаказы(Команда)
	ПолучитьЗаказыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗаказыНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Проведен
		|	И ЗаказКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ДатаНачала", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Объект.Заказы.Очистить();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Заказы.Добавить();
		НоваяСтрока.ЗаказКлиента = Выборка.Ссылка;
		НоваяСтрока.Пометка = Истина;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьАкты(Команда)
	СоздатьАктыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьАктыНаСервере()

	Для каждого СтрокаТЧ Из Объект.Заказы Цикл
		
		Если СтрокаТЧ.Пометка = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		//1. Перевод товаров в статус "Отгрузить"
		ЗаказОбъект = СтрокаТЧ.ЗаказКлиента.ПолучитьОбъект();
		Для каждого СтрокаТовары Из ЗаказОбъект.Товары Цикл
			СтрокаТовары.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
		КонецЦикла;                                                                      
		
		Попытка
			ЗаказОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
		  	Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Ошибка при проведении документа: " + СтрокаТЧ.ЗаказКлиента + ": " + ОписаниеОшибки();
			Сообщение.Сообщить();
			Продолжить;
		КонецПопытки;       
		
		//2. Создание акта выполненных работ
		НовыйАкт = Документы.АктВыполненныхРабот.СоздатьДокумент();
		НовыйАкт.Заполнить(СтрокаТЧ.ЗаказКлиента);
		НовыйАкт.Дата = ТекущаяДата();
		
		Попытка
			НовыйАкт.Записать();
			СтрокаТЧ.Акт = НовыйАкт.Ссылка;
		Исключение
		  	Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Ошибка при создании акта по заказу: " + СтрокаТЧ.ЗаказКлиента + ": " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
	
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьФлагиВТаблице(ЗначениеПометки)

	Для каждого СтрокаТЧ Из Объект.Заказы Цикл
		СтрокаТЧ.Пометка = ЗначениеПометки;
	КонецЦикла;

КонецПроцедуры // ИзменитьФлагиВТаблице()


&НаКлиенте
Процедура УстановитьФлаги(Команда)

	ИзменитьФлагиВТаблице(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлаги(Команда)

	ИзменитьФлагиВТаблице(Ложь);
	
КонецПроцедуры

```