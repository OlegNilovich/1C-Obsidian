```bsl
// HTTP-сервис "Hyper Text Transfer Protocol"

// 1. В конфигураторе создаем HTTP-сервис "ДемоСервис"
// 2. Корневой URL: demo
// 3. Создаем шаблон URL. Имя: DemoAPI, Шаблон: /v1/{ИмяМетода}
// 4. Для шаблона "DemoAPI" создаем метод. Имя: "GET", HTTPметод "GET"
// 5. Создаем обработчик метода "GET".
// 6. Администрирование => Публикация на веб-сервере => "ДемоСервис" => Опубликовать

// Примеры запросов:
// Получить прайс без отбора: http://localhost/Education/hs/demo/v1/pricelist
// С отбором http://localhost/Education/hs/demo/v1/pricelist?category=Мобильные телефоны

// Модуль сервиса:
Функция DemoAPIGET(Запрос)

	ИмяМетода = Запрос.ПараметрыURL.Получить("ИмяМетода");
	
	Если ИмяМетода = "pricelist" Тогда
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Content-Type", "application/json");
		Категория = Запрос.ПараметрыЗапроса.Получить("category");
		Ответ.УстановитьТелоИзСтроки(ДанныеДляСайта(Категория));
	ИначеЕсли ИмяМетода = "test" Тогда
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
		Ответ.УстановитьТелоИзСтроки("Тест: Успех");
	Иначе
		Ответ = Новый HTTPСервисОтвет(404);
		Ответ.Заголовки.Вставить("Content-Type", "text/html; charset=utf-8");
		Ответ.УстановитьТелоИзСтроки("Error 404: Content Not Found");
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции


Функция ДанныеДляСайта(Категория)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпрНоменклатура.Артикул КАК Артикул,
		|	СпрНоменклатура.Наименование КАК Наименование,
		|	СпрНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СпрНоменклатура.ВидНоменклатуры) КАК ВидНоменклатуры,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦенИМ) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ЦеныНоменклатурыСрезПоследних.Номенклатура = СпрНоменклатура.Ссылка
		|ГДЕ
		|	НЕ СпрНоменклатура.ЭтоГруппа
		|	И НЕ СпрНоменклатура.ПометкаУдаления";
	
	Если Категория <> Неопределено Тогда
		
		ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию(Категория, Истина);	
		
		Запрос.Текст = Запрос.Текст + "
		|	И СпрНоменклатура.ВидНоменклатуры = &ВидНоменклатуры";
		Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);	
		
	КонецЕсли;
		 
	Запрос.УстановитьПараметр("ВидЦенИМ", Справочники.ВидыЦен.Оптовая);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку();
	
	МассивТоваров = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		СтруктураТовара = Новый Структура;
		СтруктураТовара.Вставить("article", Выборка.Артикул);		
		СтруктураТовара.Вставить("name", Выборка.Наименование);		
		СтруктураТовара.Вставить("name_full", Выборка.НаименованиеПолное);		
		СтруктураТовара.Вставить("category", Выборка.ВидНоменклатуры);		
		СтруктураТовара.Вставить("price", Выборка.Цена);	
		
		МассивТоваров.Добавить(СтруктураТовара);
		
	КонецЦикла;  
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("data", МассивТоваров);
	
	ЗаписатьJSON(ЗаписьJSON, СтруктураРезультат); 
	
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;

КонецФункции
```