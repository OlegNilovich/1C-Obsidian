```bsl
//============================================================================
// Получение курса валют ЦБР:

// 1. Создаем WS-Ссылку "cbrDailyInfo"
// 2. Указываем URL => https://www.cbr.ru/DailyInfoWebServ/DailyInfo.asmx?wsdl
// 3. Создаем обработку и форму. Модуль формы:

&НаСервере
Процедура ПолучитьКурсы()

	//1. получить прокси по статической ссылке, которую мы создали выше.
	WSПрокси = WSСсылки.cbrDailyInfo.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");
	
	//2. Получить пакет XDTO, в котором описаны типы объектов
	ПакетXDTO = WSПрокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/");
	
	//3. Получить тип объекта XDTO, который используется для задания параметра операции веб-сервиса
	ТипGetCourseOnDate = ПакетXDTO.Получить("GetCursOnDate");
	
	//4. Создать ОбъектXTDO с типом параметра
	ПараметрВебСервиса = WSПрокси.ФабрикаXDTO.Создать(ТипGetCourseOnDate);	
	ПараметрВебСервиса.On_date = ТекущаяДата();
	
	//5. Вызов метода веб-сервиса
	Результат = WSПрокси.GetCursOnDate(ПараметрВебСервиса);

	Если Результат = Неопределено Тогда
		Сообщить("Не удалось получить курсы валют");    
		Возврат;
	КонецЕсли;
	
	КурсВалют = Результат.GetCursOnDateResult.diffgram.ValuteData.ValuteCursOnDate;
	
	Для каждого Валюта Из КурсВалют Цикл
		Текст = "Валюта: %1   Код: %2 (%3)   Курс: %4";
		Наименование = СокрЛП(Валюта.Vname);
		СимвольныйКод = СокрЛП(Валюта.VchCode);
		Код = СокрЛП(Валюта.Vcode);
		Курс = СокрЛП(Валюта.Vcurs);
		Сообщить(СтрШаблон(Текст, Наименование, СимвольныйКод, Код, Курс));
	КонецЦикла;

КонецПроцедуры


```