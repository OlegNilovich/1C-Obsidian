
Модуль объекта обработки:
```bsl
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);

	Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = Вид;
	ПараметрыРегистрации.Версия = "1.0.0.1";
	ПараметрыРегистрации.Наименование = Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Информация = "Внешняя печатная форма """ + Синоним + """";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПокупателя");

	Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовКлиентскогоМетода();
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = Синоним;
	Команда.Идентификатор = ИмяОбработки;
	Команда.Использование = Использование;
	Команда.Модификатор = "ПечатьMXL";
	Команда.ЗаменяемыеКоманды = ""; // если нужно заменить текущую команду печати, укажите её идентификатор
	Команда.ПоказыватьОповещение = Истина;
    
    Возврат ПараметрыРегистрации; 

КонецФункции

Процедура Печать(ДанныеДляПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 

	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			ЭтотОбъект.Метаданные().Имя,
			НСтр("ru = 'Тест'"),
			СформироватьПечатнуюФормуТест(ДанныеДляПечати),
			,
			);
	//ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);

КонецПроцедуры 

#КонецОбласти

#Область ФункцииФормированияПечатныхФорм

Функция СформироватьПечатнуюФормуТест(ДанныеДляПечати)
	
	МассивОбъектов			= ДанныеДляПечати.ОбъектыНазначения;
	ДополнительныеПараметры = ДанныеДляПечати.ДополнительныеПараметры;
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Тест");
	Область = Макет.ПолучитьОбласть("Тест");
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл
		Область.Параметры.Параметр = СсылкаНаОбъект;
		Область.Параметры.Заполнить(ДополнительныеПараметры);
		ТабДок.Вывести(Область);
	КонецЦикла;	
	
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
	
КонецФункции	

#КонецОбласти

```

Форма обработки:
![[Pasted image 20241112061003.png]]

Модуль формы обработки:
```bsl

&НаКлиенте
Процедура Печать(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ОткрытьФорму("ВнешняяОбработка.Демо_ПечатнаяФорма_СПараметрами.Форма.ФормаВводаПараметров", 
					ПараметрыФормы,,,,,Новый ОписаниеОповещения("ПечатьЗавершение", ЭтотОбъект, ОбъектыНазначенияМассив),
					РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	

КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОбъектыНазначения = Новый Структура;
	СтруктураОбъектыНазначения.Вставить("ОбъектыНазначения", Параметры.ОбъектыНазначения);
	СтруктураОбъектыНазначения.Вставить("ДополнительныеПараметры", Результат);
	
	ПараметрыИсточника = Новый Структура;
	ПараметрыИсточника.Вставить("ИдентификаторКоманды", Параметры.ИдентификаторКоманды);
	ПараметрыИсточника.Вставить("ОбъектыНазначения",	СтруктураОбъектыНазначения);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИсточникДанных",	 Параметры.ДополнительнаяОбработкаСсылка);	
	ПараметрыОткрытия.Вставить("ПараметрыИсточника", ПараметрыИсточника);

	ОткрытьФорму("ОбщаяФорма.ПечатьДокументов", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаПечати(Команда)

	МассивОбъектов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаДокумент);
	Печать(Параметры.ИдентификаторКоманды, МассивОбъектов);

КонецПроцедуры

```

Форма ввода параметров:
![[Pasted image 20241112061110.png]]

Модуль формы ввода параметров:
```bsl
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Директор", Директор);
	СтруктураПараметров.Вставить("Должность", Должность);
	
	Закрыть(СтруктураПараметров);
	
КонецПроцедуры
	
#КонецОбласти
```