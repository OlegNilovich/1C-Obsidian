Данные о резервах будет хранить регистр накопления "Товары в резерве" вид: Остатки
Документ "Заказ покупателя" резервирует товар и добавляет движение-приход в регистр
Документ "Расходная накладная" снимает товар с резерва и добавляет  движение-расход в регистр

### Создание регистра
![[Pasted image 20240820191627.png]]
1. Создаем регистр накопления "ТоварыВРезерве" вид: Остатки
2. Создаем измерение: ЗаказКлиента (ДокументСсылка.ЗаказКлиента)
3. Создаем измерение: Номенклатура (СправочникСсылка.Номенклатура)
4. Создаем измерение: Организация (СправочникСсылка.Организации)
5. Создаем измерение: Склад (СправочникСсылка.Склады)
6. Создаем ресурс: Количество (Число, 10, 3)

---
### Создание документа "ЗаказКлиента"
![[Pasted image 20240820192415.png]]
1. Создаем документ "ЗаказКлиента"
2. Создаем реквизиты документа:
- Организация (СправочникСсылка.Организации)
- Контрагент (СправочникСсылка.Контрагенты)
- Договор (СправочникСсылка.ДоговорыКонтрагентов)
- Склад (СправочникСсылка.Склады)
- Статус (ПеречислениеСсылка.СтатусыЗаказовКлиентов)
3. Создаем табличную часть "Товары" и реквизиты табличной части:
- Номенклатура (СправочникСсылка.Номенклатура)
- Количество (Число, 10, 3)
- Цена (Число, 10, 2)
- Сумма (Число, 10, 2)
4. Переходим во вкладку "Движения" в окне редактирования документа "ЗаказКлиента"
5. Указываем регистр накопления "ТоварыВРезерве"
![[Pasted image 20240820193224.png]]
6. Вызываем конструктор движений. Модуль объекта документа:
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ТоварыВРезерве.Записывать = Истина;
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ТоварыВРезерве.ДобавитьПриход();
		Движение.Период				= Дата;
		Движение.ЗаказКлиента	= Ссылка;
		Движение.Организация		= Организация;
		Движение.Склад				= Склад;
		Движение.Номенклатура	= ТекСтрокаТовары.Номенклатура;
		Движение.Количество		= ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры
```

---
### Создание документа "РасходнаяНакладная"
1. Создаем документ "РасходнаяНакладная"
2. Создаем реквизиты документа:
- Организация (СправочникСсылка.Организации)
- Контрагент (СправочникСсылка.Контрагенты)
- Договор (СправочникСсылка.ДоговорыКонтрагентов)
- Склад (СправочникСсылка.Склады)
- ЗаказКлиента (ДокументСсылка.ЗаказКлиента)
1. Создаем табличную часть "Товары" и реквизиты табличной части:
- Номенклатура (СправочникСсылка.Номенклатура)
- Количество (Число, 10, 3)
- Цена (Число, 10, 2)
- Сумма (Число, 10, 2)
2. Переходим во вкладку "Движения" в окне редактирования документа "РасходнаяНакладная"
3. Указываем регистр накопления "ТоварыВРезерве"
4. Переходим во вкладку "Ввод на основании" в окне редактирования документа "РасходнаяНакладная"
5. Указываем документ "Заказ клиента" и вызываем конструктор ввода на основании. Модуль объекта:
```bsl
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
	
		// Заполнение шапки
		Договор = ДанныеЗаполнения.Договор;
		Контрагент = ДанныеЗаполнения.Контрагент;
		Организация = ДанныеЗаполнения.Организация;
		Склад = ДанныеЗаполнения.Склад;
		ЗаказКлиента = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		
		//1. Получаем таблицу товаров из заказа
		ТоварыВЗаказе = ДанныеЗаполнения.Товары;
		
		//2. Получаем список товаров из заказа
		МассивНоменклатуры = ТоварыВЗаказе.ВыгрузитьКолонку("Номенклатура");
		
		//3. Получаем остатки резервов (что осталось отгрузить)
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЗаказКлиента", ЗаказКлиента);
		СтруктураОтбора.Вставить("Организация", Организация);
		СтруктураОтбора.Вставить("Склад", Склад);
		СтруктураОтбора.Вставить("Номенклатура", МассивНоменклатуры);
		
		ТаблицаОстатковРезервов = РегистрыНакопления.ТоварыВРезерве.Остатки(, СтруктураОтбора);
		
		//4. Заполняем табличную часть накладной остатками резервов
		Для каждого СтрокаОстатков Из ТаблицаОстатковРезервов Цикл
			
			НоваяСтрока = Товары.Добавить();
			НоваяСтрока.Номенклатура = СтрокаОстатков.Номенклатура;
			НоваяСтрока.Количество = СтрокаОстатков.Количество;
		
			//цену, берем из заказа
			СтрокаЗаказа = ТоварыВЗаказе.Найти(НоваяСтрока.Номенклатура, "Номенклатура");
			Если СтрокаЗаказа <> Неопределено Тогда
				НоваяСтрока.Цена = СтрокаЗаказа.Цена;
				НоваяСтрока.Сумма = СтрокаЗаказа.Цена * СтрокаОстатков.Количество;
			КонецЕсли; 
			
		КонецЦикла; 
	
	КонецЕсли;
	
КонецПроцедуры
```
6. Переходим во вкладку "Движения" в окне редактирования документа "Расходная накладная"
7. Указываем регистр накопления "ТоварыВРезерве". Формируем движения в модуле объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	// Движения по регистру "ТоварыВРезерве"
	Движения.ТоварыВРезерве.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ТоварыВРезерве.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Организация = Организация;
		Движение.ЗаказКлиента = ЗаказКлиента;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
КонецПроцедуры
```