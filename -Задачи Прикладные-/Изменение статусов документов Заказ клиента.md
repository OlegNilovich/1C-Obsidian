#ФОРМЫ 
### Задача:
На форме списка документа "ЗаказКлиентов", требуется создать команду "ИзменитьСтатус", которая сможет изменять статус нескольких выделенных документов одновременно.
![[Pasted image 20240822000755.png]]

### Решение:
1. Создаем команду "ИзменитьСтатус" и обработчик команды в модуле формы.
![[Pasted image 20240822001249.png]]
2. Модуль формы:
```bsl
&НаКлиенте
Асинх Процедура ИзменитьСтатусы(Команда)
	
	//1. Получаем ссылки выделенных документов в списке
	МассивЗаказов = Элементы.Список.ВыделенныеСтроки;
	
	//2. Открываем окно выбора статуса. Получаем выбранный статус.
	ПараметрТип = Тип("СправочникСсылка.СтатусыЗаказовКлиентов");
	ВыбранныйСтатус = Ждать ВвестиЗначениеАсинх(,"Выберите статус", ПараметрТип);
	
	Если ВыбранныйСтатус = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//3. Изменяем статус выделенных документов в списке
	ИзменитьСтатусНаСервере(ВыбранныйСтатус, МассивЗаказов);
	
	//4. Обновляем список документов, чтобы отобразился новый статус
	ОповеститьОбИзменении(Тип("ДокументСсылка.ЗаказКлиента"));

КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатусНаСервере(ВыбранныйСтатус, МассивЗаказов)
	
	Для каждого Заказ Из МассивЗаказов Цикл
		ЗаказОбъект = Заказ.ПолучитьОбъект();
		ЗаказОбъект.Статус = ВыбранныйСтатус;
		РежимЗаписи = ?(ЗаказОбъект.Проведен, РежимЗаписиДокумента.Проведение, 
		РежимЗаписиДокумента.Запись);
		ЗаказОбъект.Записать(РежимЗаписи);
	КонецЦикла; 
	
КонецПроцедуры
```