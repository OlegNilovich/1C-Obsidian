#ФОРМЫ 

### Задача:

Для документа **Регистрация заказа**, в табличной части *Погашение сертификатов*, для реквизита *Номер* (тип число), требуется сформировать список выбора из номеров **Непогашенных** сертификатов.

При добавлении сертификата в табличную часть *Блюда*, сумма сертификата добавляется к итоговой сумме заказа.

При добавлении сертификата в табличную часть *Погашение сертификатов*:
- Должна автоматически заполняться его сумма. Сумма сертификата должна быть вычтена из итоговой суммы заказа.
- Сертификат должен удаляться из списка выбора, чтобы у пользователя не было возможность добавить несколько сертификатов с одним номером.

При удалении сертификата из табличной части *Погашение сертификатов*, он должен возвращаться в список выбора.

Сертификаты продаются на вкладке *Блюда*
![[Pasted image 20241103162444.png]]

Сертификаты погашаются на вкладке *Погашение сертификатов*
![[Pasted image 20241103161201.png]]

**Сертификаты** хранятся в регистре сведений, каждый из них имеет свой уникальный *Номер* и признак *Погашен* с типом Булево.
![[Pasted image 20241103161250.png]]

### Решение:
1. Для документа **РегистрацияЗаказа** создаем табличную часть *ПогашениеСертификатов*
2. Создаем реквизит табличной части *Номер* (Число, 10, 0)
2. Создаем реквизит табличной части *Сумма* (Число, 10, 2)
3. Модуль объекта документа:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = СписокБлюд.Итог("Сумма") - ПогашениеСертификатов.Итог("Сумма");
	СуммаДокумента = ?(СуммаДокумента > 0, СуммаДокумента, 0);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Сертификаты.Записать();
	Движения.Сертификаты.Записывать = Истина;
	
	// Выдача Сертификата
	НомерПоследнегоСертификата = ПоследнийНомерСертификата();
	Для Каждого ТекСтрокаСписокБлюд Из СписокБлюд Цикл
		Если ТекСтрокаСписокБлюд.Блюдо = Справочники.Блюда.Сертификат Тогда
			Движение = Движения.Сертификаты.Добавить();
			Движение.Период = Дата;
			Движение.Номер = НомерПоследнегоСертификата + 1;
			Движение.ДатаВыдачи = Дата;
			Движение.Сумма = ТекСтрокаСписокБлюд.Сумма;
			НомерПоследнегоСертификата = НомерПоследнегоСертификата + 1;
		КонецЕсли;
	КонецЦикла;
	
	// Погашение Сертификата
	Для Каждого ТекСтрока Из ПогашениеСертификатов Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сертификаты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.Сертификаты КАК Сертификаты
		|ГДЕ
		|	Сертификаты.Номер = &Номер";
		Запрос.УстановитьПараметр("Номер", ТекСтрока.Номер);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ДокументВыдавшийСертификат = Выборка.Регистратор;
		НаборЗаписей = РегистрыСведений.Сертификаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументВыдавшийСертификат);
		НаборЗаписей.Прочитать(); 
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Если ТекСтрока.Номер = Запись.Номер Тогда
				Запись.СуммаЗаказа = СписокБлюд.Итог("Сумма");
				Запись.ДатаПогашения = Дата;
				Запись.Погашен = Истина;
			КонецЕсли;
		КонецЦикла; 
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Отмена погашения Сертификата
	Для Каждого ТекСтрока Из ПогашениеСертификатов Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сертификаты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.Сертификаты КАК Сертификаты
		|ГДЕ
		|	Сертификаты.Номер = &Номер";
		Запрос.УстановитьПараметр("Номер", ТекСтрока.Номер);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ДокументВыдавшийСертификат = Выборка.Регистратор;
		НаборЗаписей = РегистрыСведений.Сертификаты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументВыдавшийСертификат);
		НаборЗаписей.Прочитать(); 
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Если ТекСтрока.Номер = Запись.Номер Тогда
				Запись.СуммаЗаказа = 0;
				Запись.ДатаПогашения = Дата(1,1,1);
				Запись.Погашен = Ложь;
			КонецЕсли;
		КонецЦикла; 
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры


Функция ПоследнийНомерСертификата()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(Сертификаты.Номер), 0) КАК Номер
		|ИЗ
		|	РегистрСведений.Сертификаты КАК Сертификаты";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Номер;
	КонецЕсли;
	
КонецФункции
```

4. В редакторе формы, для элемента *ПогашениеСертификатовНомер* включаем *РежимВыбораИзСписка*
![[Pasted image 20241103170423.png]]

5. Создаем событие формы: *ПриСозданииНаСервере*
6. Создаем три события для элемента формы *ПогашениеСертификатов*:
- *ПриИзменении*
- *ПередУдалением*
- *ПриОкончанииРедактирования*
![[Pasted image 20241103170712.png]]

7. Модуль формы документа:
```bsl

// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СформироватьСписокНомеровСертификатов();
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеСертификатовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	УдалитьСертификатИзСпискаВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеСертификатовПередУдалением(Элемент, Отказ)
	ВернутьСертификатВСписокВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ПогашениеСертификатовНомерПриИзменении(Элемент)
	ТекСтрока = Элементы.ПогашениеСертификатов.ТекущиеДанные;
	ТекСтрока.Сумма = СуммаСертификата(ТекСтрока.Номер);
КонецПроцедуры


// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура СформироватьСписокНомеровСертификатов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сертификаты.Номер КАК Номер,
		|	Сертификаты.Сумма КАК Сумма
		|ИЗ
		|	РегистрСведений.Сертификаты КАК Сертификаты
		|ГДЕ
		|	НЕ Сертификаты.Погашен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Представление = СтрШаблон("%1 (%2 руб.)", Выборка.Номер, Выборка.Сумма);
		Элементы.ПогашениеСертификатовНомер.СписокВыбора.Добавить(Выборка.Номер, Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СуммаСертификата(НомерСертификата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сертификаты.Сумма КАК Сумма
		|ИЗ
		|	РегистрСведений.Сертификаты КАК Сертификаты
		|ГДЕ
		|	Сертификаты.Номер = &Номер
		|	И НЕ Сертификаты.Погашен";
	Запрос.УстановитьПараметр("Номер", НомерСертификата);
	
	Выборка =  Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Сумма;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура УдалитьСертификатИзСпискаВыбора()
	Номер = Элементы.ПогашениеСертификатов.ТекущиеДанные.Номер;
	Для Каждого ТекСтрока Из Объект.ПогашениеСертификатов Цикл
		УдаляемыйЭлемент = Элементы.ПогашениеСертификатовНомер.СписокВыбора.НайтиПоЗначению(Номер);
		Если УдаляемыйЭлемент <> Неопределено Тогда
			Элементы.ПогашениеСертификатовНомер.СписокВыбора.Удалить(УдаляемыйЭлемент);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьСертификатВСписокВыбора()
	ТекСтрока = Элементы.ПогашениеСертификатов.ТекущиеДанные;
	Если ТекСтрока.Номер <> 0 Тогда
		Представление = СтрШаблон("%1 (%2 руб.)", ТекСтрока.Номер, ТекСтрока.Сумма);
		Элементы.ПогашениеСертификатовНомер.СписокВыбора.Добавить(ТекСтрока.Номер, Представление);
	КонецЕсли;
КонецПроцедуры
```