#ФОРМЫ 
### Задача:
В документе "РасходнаяНакладная", требуется создать команду "Подбор". Которая будет открывать форму подбора товаров и после окончания подбора будет переносить выбранные товары в табличную часть документа
### Решение:
1. Создаем обработку "ФормаПодбораТоваров"
2. Создаем первую и основную форму "СписокРасходныхНакладных"
3. Создаем вторую форму "РасходнаяНакладная"
4. Создаем третью форму "ФормаПодбора"
![[Pasted image 20240823081616.png]]

---
##### Форма "СписокРасходныхНакладных"
1. Открываем редактор формы "СписокРасходныхНакладных"
2. Создаем реквизит "Список" с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Документ.РасходнаяНакладная
4. Переносим реквизит "Список" на форму
![[Pasted image 20240823072253.png]]
5. Для элемента формы "Список", создаем событие "Выбор"
![[Pasted image 20240823072758.png]]
6. Модуль формы:
```bsl
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПутьКФорме	= "Обработка.ФормаПодбораТоваров_Гордынец.Форма.РасходнаяНакладная";
	Параметры	= Новый Структура("Ключ", ВыбраннаяСтрока);
	Режим			= РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ПутьКФорме, Параметры,,,,,, Режим);
КонецПроцедуры
```

---
##### Форма "РасходнаяНакладная"
1. Открываем редактор формы "РасходнаяНакладная"
2. Создаем реквизит "Объект" с типом (ДокументОбъект.РасходнаяНакладная)
3. Переносим необходимые реквизиты из шапки документа на форму
![[Pasted image 20240823073130.png]]
4. Переносим табличную часть "Товары" с необходимыми реквизитами
![[Pasted image 20240825013106.png]]
5. Создаем обработчик события формы "ОбработкаВыбора"
![[Pasted image 20240825012035.png]]
6. Создаем команду "Подбор" и обработчик команды
![[Pasted image 20240823073536.png]]
7. Модуль формы:
```bsl
&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ИмяФормыПодбора = "Обработка.ФормаПодбораТоваров_Гордынец.Форма.ФормаПодбора";
	Если ИсточникВыбора.ИмяФормы = ИмяФормыПодбора Тогда
		Объект.Товары.Очистить();
		Для каждого СтрокаКорзины Из ВыбранноеЗначение Цикл
			НоваяСтрока = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКорзины); 
		КонецЦикла;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ПутьКФорме = "Обработка.ФормаПодбораТоваров_Гордынец.Форма.ФормаПодбора";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТоварыИзДокумента", Объект.Товары);
	ПараметрыФормы.Вставить("ВидЦены", Объект.ВидЦен);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ПутьКФорме, ПараметрыФормы, ЭтотОбъект,,,,, Режим);
КонецПроцедуры
```

---
##### Форма "ФормаПодбора"
Открываем редактор формы "ФормаПодбора"
###### Реквизит "Товары"
1. Создаем реквизит "Товары" с типом (ДинамическийСписок)
2. Свойства => Объект => Основная таблица => Справочник.Номенклатура
![[Pasted image 20240825013829.png]]
3. Для реквизита "Товары", устанавливаем настройку списка: ЭтоГруппа = Ложь
![[Pasted image 20240825014327.png]]
4. Для колонки "Ссылка" реквизита "Товары" включаем настройку: Использовать всегда
![[Pasted image 20240825014602.png]]
4. Переносим колонки реквизита "Товары" на форму:
- Наименование
- Артикул
- СтавкаНДС
- ЕдиницаИзмерения
5. Отключаем Автозаполнение командной панели элемента "Товары"
![[Pasted image 20240825021450.png]]
###### Реквизит "Группы"
1. Создаем реквизит "Группы" с типом (ДинамическийСписок)
2. Свойства => Объект => Основная таблица => Справочник.Номенклатура
![[Pasted image 20240825015606.png]]
3. Свойства => Объект => НастройкаСписка => Отбор => ЭтоГруппа = Истина
![[Pasted image 20240825015758.png]]
4. Переносим колонку "Наименование" реквизита "Группы" на форму
5. Отключаем Автозаполнение командной панели элемента "Группы"
![[Pasted image 20240825021200.png]]
###### Реквизит "Корзина"
1. Создаем реквизит "Корзина" с типом "ТаблицаЗначений"
2. Создаем колонки для реквизита "Корзина":
- Номенклатура (СправочникСсылка.Номенклатура)
- Количество (Число, 10, 0)
- ЕдиницаИзмерения (СправочникСсылка.КлассификаторЕдиницИзмерения)
- СтавкаНДС (ПеречислениеСсылка.СтавкиНДС)
- Цена (Число, 10, 2)
- Сумма (Число, 10, 2)
- СуммаНДС (Число, 10, 2)
- СуммаВсего (Число, 10, 2)
3. Переносим реквизит "Корзина" в окно элементов формы, со всеми колонками
![[Pasted image 20240825020706.png]]
4. Отключаем Автозаполнение командной панели элемента "Корзины"
![[Pasted image 20240825021050.png]]
###### Команды формы
1. Создаем команду "ПеренестиВДокумент". Размещаем ее в командной панели формы
2. Создаем команду "ОчиститьКорзину". Размещаем ее в командной панели формы
![[Pasted image 20240825021840.png]]
###### События формы:
1. Создаем обработчик события формы "ПриСозданииНаСервере"
![[Pasted image 20240825022357.png]]
2. Создаем события "ПриИзменении" для элементов формы "КорзинаКоличество" и "КорзинаЦена"
![[Pasted image 20240825022712.png]]
3. Создаем событие "ПриАктивизацииСтроки" для элемента "Группы"
![[Pasted image 20240825022912.png]]
4. Создаем событие "Выбор" для элемента "Товары"
![[Pasted image 20240825023317.png]]
###### Модуль формы:
```bsl
//======================================================================
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Товары.Отображение = ОтображениеТаблицы.Список;
	Элементы.Группы.Отображение = ОтображениеТаблицы.Дерево;
	Элементы.Группы.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	
	Если Параметры.Свойство("ТоварыИзДокумента") Тогда
		Корзина.Загрузить(Параметры.ТоварыИзДокумента.Выгрузить());
	КонецЕсли;
	
	Если Параметры.Свойство("ВидЦены") Тогда
		ВидЦены = Параметры.ВидЦены;
	КонецЕсли;
	
	Если Параметры.Свойство("Дата") Тогда
		Дата = Параметры.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КорзинаКоличествоПриИзменении(Элемент)
	ДанныеСтроки = Элементы.Корзина.ТекущиеДанные;
	РассчитатьСуммыТЧ(ДанныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура КорзинаЦенаПриИзменении(Элемент)
	ДанныеСтроки = Элементы.Корзина.ТекущиеДанные;
	РассчитатьСуммыТЧ(ДанныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПриАктивизацииСтроки(Элемент)
	ТекущаяГруппа = Элементы.Группы.ТекущаяСтрока;                                                                    
	УстановитьЭлементОтбораДинамическогоСписка(Товары, "Родитель", ТекущаяГруппа, 
	ВидСравненияКомпоновкиДанных.Равно);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получаем данные добавляемого товара из "СписокТоваров"
	ДобавляемыйТовар = Элементы.Товары.ТекущиеДанные;

	// Ищем товар в списке "Корзина", который мы хотим добавить.
	СтруктураПоиска = Новый Структура("Номенклатура", ДобавляемыйТовар.Ссылка);
	ТоварыВКорзине = Корзина.НайтиСтроки(СтруктураПоиска);
	
	// Если в корзине еще нет такого товара, который мы хотим добавить
	Если ТоварыВКорзине.Количество() = 0 Тогда
		// Тогда добавляем наш товар в корзину
		СтрокаКорзины = Корзина.Добавить();
		СтрокаКорзины.Номенклатура			= ДобавляемыйТовар.Ссылка;
		СтрокаКорзины.СтавкаНДС				= ДобавляемыйТовар.СтавкаНДС;
		СтрокаКорзины.ЕдиницаИзмерения	= ДобавляемыйТовар.ЕдиницаИзмерения;
		СтрокаКорзины.Цена 						= ЦенаТовара(СтрокаКорзины.Номенклатура, ВидЦены, Дата);
		СтрокаКорзины.Количество				= 1;		
	Иначе
		// Иначе если такой товар есть, то просто увеличиваем его количество на 1 шт.
		СтрокаКорзины				= ТоварыВКорзине[0];
		СтрокаКорзины.Количество	= СтрокаКорзины.Количество + 1; 
	КонецЕсли;
	
	// Расчитываем суммы после добавления товара
	РассчитатьСуммыТЧ(СтрокаКорзины);
	
КонецПроцедуры

//======================================================================
// Обработчики действий команд

&НаКлиенте
Процедура ОчиститьКорзину(Команда)
	Корзина.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	ОповеститьОВыборе(Корзина);
КонецПроцедуры

//======================================================================
// Служебные процедуры и функции

&НаКлиенте
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ЗначениеПоля, ВидСравнения)
	
	Для Каждого ЭлементОтбора Из 
	ДинамическийСписок.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ОтборКомпоновкиДанных") Тогда
			
			ПользовательскийОтбор = ЭлементОтбора.Элементы;
			НовыйЭлементОтбора = Неопределено;
			
			Для Каждого СуществующийЭлементОтбора Из ПользовательскийОтбор Цикл
				
				Если СуществующийЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля) 
				Тогда
					НовыйЭлементОтбора = СуществующийЭлементОтбора;
					Прервать;
				КонецЕсли;	
					
			КонецЦикла;
			
			Если НовыйЭлементОтбора = Неопределено Тогда
				НовыйЭлементОтбора = 
				ПользовательскийОтбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
			КонецЕсли;
		
			НовыйЭлементОтбора.ВидСравнения = ВидСравнения;
			НовыйЭлементОтбора.Использование = ЗначениеЗаполнено(ЗначениеПоля);
			НовыйЭлементОтбора.РежимОтображения = 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
			НовыйЭлементОтбора.ПравоеЗначение = ЗначениеПоля;			
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммыТЧ(ДанныеСтроки)
	СтавкаНДСЧислом				= НДСЧислом(ДанныеСтроки.СтавкаНДС);
	ДанныеСтроки.Сумма			= ДанныеСтроки.Количество * ДанныеСтроки.Цена;
	ДанныеСтроки.СуммаНДС		= ДанныеСтроки.Сумма * СтавкаНДСЧислом / 100;
	ДанныеСтроки.СуммаВсего	= ДанныеСтроки.Сумма + ДанныеСтроки.СуммаНДС;
КонецПроцедуры

&НаКлиенте
Функция НДСЧислом(СтавкаНДС)
	СтавкиНДС = Новый Соответствие;
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"), 0);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"),    0);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10"), 10);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"), 18);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"), 20);
	Возврат СтавкиНДС.Получить(СтавкаНДС);
КонецФункции

&НаСервереБезКонтекста
Функция ЦенаТовара(Номенклатура, ВидЦены, Дата)
	Отбор = Новый Структура("Номенклатура, ВидЦен", Номенклатура, ВидЦены);
	Возврат РегистрыСведений.ЦеныНоменклатуры.ПолучитьПоследнее(Дата, Отбор).Цена;
КонецФункции
```


