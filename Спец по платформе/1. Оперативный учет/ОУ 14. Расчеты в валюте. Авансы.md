ЗАНЯТИЕ 28. РАСЧЕТЫ В ВАЛЮТЕ. АВАНСЫ
https://www.youtube.com/watch?v=ln_T5XhUKG0&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=28

ЗАНЯТИЕ 29. ЗАЧЕТ АВАНСОВ
https://www.youtube.com/watch?v=GcXL8DagfyM&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=29

---
### Задача:

Компания занимается оптовой торговлей. Отгрузка товаров покупателю отражается документом «*Расходная накладная*», причем каждая накладная может быть выставлена в своей *валюте*. Оплата от покупателя приходит в *рублях* и отражается документом «*Приход денег*». Одна оплата может относиться к нескольким накладным, в этом случае при проведении документа автоматически закрывается *долг* покупателя по отгрузке, начиная с самой ранней недоплаченной накладной и т.д. *Курс валюты* берется на дату оплаты.

В том случае, если сумма оплат превышает долг по отгрузке, то эта переплата засчитывается как *аванс*. При отгрузке товара необходимо проверять наличие аванса от покупателя. Если есть аванс, то он засчитывается как оплата накладной по курсу на дату накладной.

Складской учет товаров не ведется.

Необходимо создать отчеты о состоянии отгрузок товаров за период.

![[Pasted image 20241003143518.png]]

---
### Решение:

##### Регистр сведений: **КурсыВалют** 
Измерения: *Валюта*
Ресурсы: *Курс*

##### Регистр накопления: **ОстаткиНоменклатуры** (Остатки)
Измерения: *Номенклатура*
Ресурсы: *Количество*

##### Регистр накопления: **Взаиморасчеты** (Остатки)
Измерения: *Контрагент*, *Валюта*, *Накладная* (ДокументСсылка.РасходнаяНакладная)
Ресурсы: *Сумма*

##### Документ **РасходнаяНакладная**
Реквизиты: *Контрагент*, *Валюта*, *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Движение = Движения.Взаиморасчеты.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Накладная = Ссылка;
	Движение.Валюта = Валюта;
	Движение.Сумма = СуммаПоДокументу;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Накладная", Документы.РасходнаяНакладная.ПустаяСсылка());
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ВзаиморасчетыОстатки.СуммаОстаток КАК ОстатокАвансаВРублях,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	ВЫРАЗИТЬ(-ВзаиморасчетыОстатки.СуммаОстаток / ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК ЧИСЛО(15, 2)) КАК ОстатокАвансаВВалюте
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная = ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
		|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ОстатокАвансаВВалюте > 0 Тогда
		
		СуммаПоДокументуВВалюте = СуммаПоДокументу;
		СуммаПоДокументуВРублях = СуммаПоДокументуВВалюте * Выборка.Курс;
		
		Если Выборка.ОстатокАвансаВРублях >= СуммаПоДокументуВРублях Тогда
			СуммаСписанияАвансаВРублях = СуммаПоДокументуВРублях;
			СуммаЗачетаАвансаВВалюте   = СуммаПоДокументуВВалюте;
		Иначе
			СуммаСписанияАвансаВРублях = Выборка.ОстатокАвансаВРублях;
			СуммаЗачетаАвансаВВалюте   = Выборка.ОстатокАвансаВВалюте;
		КонецЕсли;
				
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.Сумма = СуммаСписанияАвансаВРублях;
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Ссылка;
		Движение.Валюта = Валюта;
		Движение.Сумма = СуммаЗачетаАвансаВВалюте;
		
	КонецЕсли;
			
КонецПроцедуры

```


##### Документ **ПриходДенег**
Реквизиты: *Контрагент*, *СуммаОплаты*
Табличная часть: *СписокНакладных*. Реквизит: *Накладная* (ДокументСсылка.РасходнаяНакладная)

Модуль объекта:
```bsl

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыОстатки.Накладная КАК Накладная,
		|	ВзаиморасчетыОстатки.Валюта КАК Валюта,
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ втДолгиПокупателей
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная <> ЗНАЧЕНИЕ(Документ.РасходнаяНакладная.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДолгиПокупателей.Контрагент КАК Контрагент,
		|	втДолгиПокупателей.Накладная КАК Накладная,
		|	втДолгиПокупателей.Валюта КАК Валюта,
		|	втДолгиПокупателей.СуммаОстаток КАК СуммаДолгаВВалюте,
		|	&Период КАК Период,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	втДолгиПокупателей.СуммаОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК СуммаДолгаВРублях
		|ИЗ
		|	втДолгиПокупателей КАК втДолгиПокупателей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						втДолгиПокупателей.Валюта КАК Валюта
		|					ИЗ
		|						втДолгиПокупателей КАК втДолгиПокупателей)) КАК КурсыВалютСрезПоследних
		|		ПО втДолгиПокупателей.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	втДолгиПокупателей.Накладная.МоментВремени";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СуммаКОплатеВРублях = СуммаОплаты;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() И СуммаКОплатеВРублях > 0 Цикл
		
		СуммаВРублях = Мин(СуммаКОплатеВРублях, Выборка.СуммаДолгаВРублях);
		
		Если СуммаВРублях = Выборка.СуммаДолгаВРублях Тогда
			СуммаВВалюте = Выборка.СуммаДолгаВВалюте;
		Иначе
			СуммаВВалюте = Окр(СуммаВРублях / Выборка.Курс, 2);
		КонецЕсли;
		
		Если СуммаВВалюте > 0 Тогда
			Движение = Движения.Взаиморасчеты.ДобавитьРасход();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Сумма = СуммаВВалюте;
			
			СуммаКОплатеВРублях = СуммаКОплатеВРублях - СуммаВРублях;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаКОплатеВРублях > 0 Тогда
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.Сумма = СуммаКОплатеВРублях;
		
	КонецЕсли;
	
КонецПроцедуры

```
