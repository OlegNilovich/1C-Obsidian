ЗАНЯТИЕ 25. ОТГРУЖЕННЫЕ И ПОЛУЧЕННЫЕ ПОКУПАТЕЛЕМ ТОВАРЫ
https://www.youtube.com/watch?v=WPHfP2R6IS4&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=25

ЗАНЯТИЕ 26. ВЗАИМОРАСЧЕТЫ
https://www.youtube.com/watch?v=H-uj_6W7lhE&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=26

---
### Задача:

Организация занимается торговлей скоропортящимися продуктами. Доставка товара покупателю отражается документом «*Расходная накладная*», оплата покупателя - документом «*Приход денег*». При получении товара покупатель по каждой накладной составляет акт о фактическом поступлении товара, за вычетом испортившегося при транспортировке. Данная информация также указывается в документе «Расходная накладная».

Оплата покупателя поступает общей суммой по нескольким накладным (накладные указываются в табличной части, сумма в шапке). При проведении документа оплаты должны автоматически распределяться по накладным, начиная с самой ранней недооплаченной, причем оплаты должны распределяться с учетом фактической (реально получено согласно акту) задолженности покупателя. Следует считать, что *переплат и авансов нет*.

Необходимо создать отчеты о состоянии отгрузок товаров за период.

![[Pasted image 20240930110114.png]]

---
### Решение:

##### Регистр накопления: **Взаиморасчеты** (Остатки)
Измерения: *Контрагент*, *Накладная* (ДокументСсылка.РасходнаяНакладная)
Ресурсы: *Сумма*

##### Регистр накопления: **ТоварыОтгруженные** (Обороты)
Измерения: *Номенклатура*
Ресурсы: *Количество*, *Сумма*

##### Регистр накопления: **ТоварыПолученные** (Обороты)
Измерения: *Номенклатура*
Ресурсы: *Количество*, *Сумма*

##### Документ **ПриходДенег**
Реквизиты: *Контрагент*, *СуммаОплаты*
Табличная часть: *СписокНакладных*. Реквизит: *Накладная* (ДокументСсылка.РасходнаяНакладная)

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.ИсточникДанных = СписокНакладных;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Накладная", "Накладная");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриходДенегСписокНакладных.Накладная КАК Накладная,
		|	&Контрагент КАК Контрагент
		|ПОМЕСТИТЬ втТЧНакладные
		|ИЗ
		|	Документ.ПриходДенег.СписокНакладных КАК ПриходДенегСписокНакладных
		|ГДЕ
		|	ПриходДенегСписокНакладных.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Контрагент,
		|	Накладная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧНакладные.Накладная КАК Накладная,
		|	втТЧНакладные.Накладная.Представление КАК НакладнаяПредставление,
		|	ЕСТЬNULL(ВзаиморасчетыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	втТЧНакладные.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ втДолгиПоНакладным
		|ИЗ
		|	втТЧНакладные КАК втТЧНакладные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Взаиморасчеты.Остатки(
		|				&МоментВремени,
		|				(Контрагент, Накладная) В
		|					(ВЫБРАТЬ
		|						втТЧНакладные.Контрагент,
		|						втТЧНакладные.Накладная
		|					ИЗ
		|						втТЧНакладные КАК втТЧНакладные)) КАК ВзаиморасчетыОстатки
		|		ПО втТЧНакладные.Накладная = ВзаиморасчетыОстатки.Накладная
		|			И втТЧНакладные.Контрагент = ВзаиморасчетыОстатки.Контрагент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДолгиПоНакладным.Накладная.Представление КАК НакладнаяПредставление
		|ИЗ
		|	втДолгиПоНакладным КАК втДолгиПоНакладным
		|ГДЕ
		|	втДолгиПоНакладным.СуммаОстаток = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДолгиПоНакладным.Накладная КАК Накладная,
		|	втДолгиПоНакладным.НакладнаяПредставление КАК НакладнаяПредставление,
		|	втДолгиПоНакладным.СуммаОстаток КАК СуммаОстаток,
		|	&Период КАК Период,
		|	втДолгиПоНакладным.Контрагент КАК Контрагент
		|ИЗ
		|	втДолгиПоНакладным КАК втДолгиПоНакладным
		|
		|УПОРЯДОЧИТЬ ПО
		|	втДолгиПоНакладным.Накладная.МоментВремени
		|ИТОГИ
		|	СУММА(СуммаОстаток)
		|ПО
		|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	РезультатПроверка = РезультатыЗапросов[2];
	Если Не РезультатПроверка.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатПроверка.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("По накладной %1 нет долга", Выборка.НакладнаяПредставление);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
		
	ВыборкаОбщийИтог = РезультатыЗапросов[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаОбщийИтог.Следующий();
	
	ДолгПоВсемНакладным = ВыборкаОбщийИтог.СуммаОстаток;
	
	Если СуммаОплаты > ДолгПоВсемНакладным Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Оплата не должна превышать суммарный долг по накладным (он равен %1)",
		ДолгПоВсемНакладным);
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	СуммаКОплате = СуммаОплаты;
	
	ВыборкаДетальныеЗаписи = ВыборкаОбщийИтог.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() И СуммаКОплате > 0 Цикл
		
		Сумма = Мин(СуммаКОплате, ВыборкаДетальныеЗаписи.СуммаОстаток);
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.Сумма = Сумма;
		
		СуммаКОплате = СуммаКОплате - Сумма;
		
	КонецЦикла;
	
КонецПроцедуры
```

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;		
	КонецЦикла;
	 
КонецПроцедуры

```

##### Документ **РасходнаяНакладная**
Реквизиты: *Контрагент*, *СуммаОтгружено*, *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*
- *КоличествоПолучено*
- *СуммаПолучено*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаОтгружено   = СписокНоменклатуры.Итог("Сумма");
	СуммаПоДокументу = СписокНоменклатуры.Итог("СуммаПолучено");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыОтгруженные.Записывать = Истина;
	Движения.ТоварыПолученные.Записывать  = Истина;
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление КАК НоменклатураПредставление,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.КоличествоПолучено) КАК КоличествоПолучено,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.СуммаПолучено) КАК СуммаПолучено,
		|	&Период КАК Период
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.КоличествоПолучено > Выборка.Количество Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Получено больше товара %1, чем было отгружено", 
			Выборка.НоменклатураПредставление);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.ТоварыОтгруженные.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Если Выборка.КоличествоПолучено > 0 Тогда
			Движение = Движения.ТоварыПолученные.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);		
			Движение.Количество = Выборка.КоличествоПолучено;
			Движение.Сумма = Выборка.СуммаПолучено;
		КонецЕсли;			
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СуммаПоДокументу > 0 Тогда		
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Ссылка;
		Движение.Сумма = СуммаПоДокументу;
	КонецЕсли;
	
КонецПроцедуры

```
