ЗАНЯТИЕ 19. ПРОДУКТЫ, НАБОРЫ И ГОТОВЫЕ БЛЮДА. ЧАСТЬ 1
https://www.youtube.com/watch?v=2k7MmioNfeU&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=19

ЗАНЯТИЕ 20. ПРОДУКТЫ, НАБОРЫ И ГОТОВЫЕ БЛЮДА. ЧАСТЬ 2
https://www.youtube.com/watch?v=vfD82KJ6H8M&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=20

---
### Задача:

Компания является заведением общественного питания. Возможна продажа как отдельных продуктов, так наборов этих продуктов и готовых блюд.

Набор представляет из себя перечень продуктов, хранящихся на складе. Например, можно продавать в виде набора пирожное и чай, а можно по отдельности - отдельно чай, отдельно пирожное.

В том случае, если из продуктов изготовлено блюдо, например из овощей сделан салат, то продаваться может только само блюдо, а входящие в его состав продукты проданы быть не могут.

Для каждой номенклатурной позиции (продукта, блюда) к ней добавляются любое количество характеристик. Набор характеристик является свойством. Например, Салат «огуречный, входит в комплексный обед, диетический» или томат «мелкий, красный, тепличный». «Огуречный», «входит в комплексный обед», «диетический» — это характеристики, а «Огуречный, входит в комплексный обед, диетический» - свойство.

Учет номенклатур, но не наборов, ведется в разрезе свойств. Механизм должен быть реализован с помощью плана видов характеристик. Одно и то же свойство может быть указано для различных номенклатурных позиций.

Закупка продуктов отражается документом «Приходная накладная», продажа - «Расходная накладная». Для отражения приготовления блюд служит документ «Комплектация». Документом комплектация пользователь делает движения в любой регистр накопления и правильность записей контролирует сам.

Учет номенклатуры в разрезе складов не ведется.

При продаже в одной табличной части указываются продукты, наборы и готовые блюда.

Со временем (не чаще, чем 1 раз в день) состав набора может изменяться. При продаже должен приниматься тот состав набора, который был актуальным на момент продажи.

![[Pasted image 20240930100013.png]]

Отчет строится по количеству для продуктов и блюд.

---
### Решение:

##### Регистр сведений: **СоставНаборовПродуктов**
Измерения: 
*НаборПродуктов* (СправочникСсылка.Номенклатура),
*Продукт* (СправочникСсылка.Номенклатура).
Ресурсы: *Количество*

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*
Ресурсы: *Количество*

##### Документ **Комплектация**
Реквизиты: *Блюдо* (СправочникСсылка.Номенклатура), *Количество* (Число)
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Продукт* (СправочникСсылка.Номенклатура)
- *Количество* (Число)

Модуль объекта:
```bsl

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомплектацияПродукты.Продукт КАК Продукт,
		|	СУММА(КомплектацияПродукты.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.Комплектация.Продукты КАК КомплектацияПродукты
		|ГДЕ
		|	КомплектацияПродукты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	КомплектацияПродукты.Продукт
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Продукт КАК Номенклатура,
		|	втТЧТовары.Количество КАК Количество,
		|	&Период КАК Период
		|ИЗ
		|	втТЧТовары КАК втТЧТовары";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение  = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Номенклатура = Блюдо;
	Движение.Количество = Количество;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Т.Продукт
		|				ИЗ
		|					втТЧТовары КАК Т)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();	
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно продукта %1 в количестве %2",
			Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

```

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;		
	КонецЦикла;
	 
КонецПроцедуры

```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Количество КАК Количество
		|ПОМЕСТИТЬ втНаборыПродуктов
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|ГДЕ
		|	втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставНаборовПродуктов.НаборПродуктов КАК НаборПродуктов,
		|	МАКСИМУМ(СоставНаборовПродуктов.Период) КАК Период
		|ПОМЕСТИТЬ втДатыПоследнихИзмененийНаборов
		|ИЗ
		|	РегистрСведений.СоставНаборовПродуктов КАК СоставНаборовПродуктов
		|ГДЕ
		|	СоставНаборовПродуктов.Период <= &Период
		|	И СоставНаборовПродуктов.НаборПродуктов В
		|			(ВЫБРАТЬ
		|				Т.Номенклатура
		|			ИЗ
		|				втНаборыПродуктов КАК Т)
		|
		|СГРУППИРОВАТЬ ПО
		|	СоставНаборовПродуктов.НаборПродуктов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	НаборПродуктов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставНаборовПродуктов.НаборПродуктов КАК НаборПродуктов,
		|	СоставНаборовПродуктов.Продукт КАК Продукт,
		|	СоставНаборовПродуктов.Количество КАК Количество
		|ПОМЕСТИТЬ втСоставНаборовПродуктов
		|ИЗ
		|	РегистрСведений.СоставНаборовПродуктов КАК СоставНаборовПродуктов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДатыПоследнихИзмененийНаборов КАК втДатыПоследнихИзмененийНаборов
		|		ПО СоставНаборовПродуктов.НаборПродуктов = втДатыПоследнихИзмененийНаборов.НаборПродуктов
		|			И СоставНаборовПродуктов.Период = втДатыПоследнихИзмененийНаборов.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НаборПродуктов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК НоменклатураПродажи,
		|	ЕСТЬNULL(втСоставНаборовПродуктов.Продукт, ВЫБОР
		|			КОГДА втТЧТовары.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
		|				ТОГДА втТЧТовары.Номенклатура
		|		КОНЕЦ) КАК Номенклатура,
		|	втТЧТовары.Количество * ЕСТЬNULL(втСоставНаборовПродуктов.Количество, ВЫБОР
		|			КОГДА втТЧТовары.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.НаборПродуктов)
		|				ТОГДА 1
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ втПродуктыИБлюда
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСоставНаборовПродуктов КАК втСоставНаборовПродуктов
		|		ПО втТЧТовары.Номенклатура = втСоставНаборовПродуктов.НаборПродуктов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПродуктыИБлюда.НоменклатураПродажи.Представление КАК НоменклатураПредставление
		|ИЗ
		|	втПродуктыИБлюда КАК втПродуктыИБлюда
		|ГДЕ
		|	втПродуктыИБлюда.Номенклатура ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПродуктыИБлюда.Номенклатура КАК Номенклатура,
		|	СУММА(втПродуктыИБлюда.Количество) КАК Количество,
		|	&Период КАК Период
		|ИЗ
		|	втПродуктыИБлюда КАК втПродуктыИБлюда
		|ГДЕ
		|	НЕ втПродуктыИБлюда.Номенклатура ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	втПродуктыИБлюда.Номенклатура";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	РезультатПроверка = РезультатыЗапросов[5];
	Если Не РезультатПроверка.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатПроверка.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не определен состав для набора " + Выборка.НоменклатураПредставление;
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Результат = РезультатыЗапросов[6];
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					Т.Номенклатура
		|				ИЗ
		|					втПродуктыИБлюда КАК Т)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда	
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Если Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Продукт Тогда
				Сообщение.Текст = СтрШаблон("Мало продукта %1 в количестве %2 (может быть в составе набора)",
				Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
			ИначеЕсли Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Блюдо Тогда
				Сообщение.Текст = СтрШаблон("Недостаточно блюда %1 в количестве %2",
				Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
			Иначе
				Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
				Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);				
			КонецЕсли;
			Сообщение.Сообщить();	
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

```