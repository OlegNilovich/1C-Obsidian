#УТ

регистр сведений *РаспределениеЗапасов*

регистр накопления *ЗапасыИПотребности*
регистр накопления *ТоварыКПоступлению*
регистр накопления *ТоварыКОткгрузке*

Отчет *Остатки и доступность товаров*

[Получить свободные остатки товаров в УТ 11.5](https://forum.mista.ru/topic/884941)
[Удален регистр накопления СвободныеОстатки. Что вместо него?](https://tnsoft.ru/blog/udalen-registr-nakopleniya-svobodnyeostatki-chto-vmesto-nego/)

В программе предусмотрена работа с резервами в документах:
- Заказ клиента
- Заказ на перемещение
- Заказ на внутреннее потребление
- Заказ на сборку (разборку).

Обозначение статусов:

- **Отгрузить** – данный статус обозначает, что товар помечен к отгрузке. В данном статусе товар резервируется на складе и может быть отгружен в любой момент.
- **Резервировать** – товар будет зарезервирован, но недоступен к отгрузке.
- **Резервировать по мере поступления** – товар будет зарезервирован, как только придет на склад.
- **К обеспечению** – товар в данном статусе не резервируется товары, но формирует потребность и доступен к заказу у поставщика.
- **Не обеспечивать** – товар невозможно отгрузить, и он не формирует потребность.
### Задача

Есть Организация «ОРГ1» и «ОРГ2». Есть склад «СКЛ1». По каждой организации в складе «СКЛ1» есть остаток номенклатуры «НМК» по 10000 штук. Это мы получаем из регистра "ТоварыОрганизаций" Свободный остаток, где нет среза по организациям, показывает что доступно в целом 5000 штук(РегистрСведений.РаспределениеЗапасов), но не возможно определить, в какой организации этот свободный остаток. То есть свободный остаток может быть и в одной, и в другой организации. Нужен не остаток в организации, которую получаем из регистра ТоварыОрганизаций, а именно свободный остаток в организации. В данной задаче получается остаток 20000 штук, в каждой организации по 10000 штук, из всего этого 15000 в резерве, доступно 5000, но определить организацию не получается.

### Решение

Минимальный из остатка организации и на складе свободный.

РН *ЗапасыИПотребности* там есть заказ в заказе есть *организация*, по нему можно получить остатки резервов и резервов по мере поступления, по товарам Организаций можно получить остаток товаров. Проблема будет в том, что в *ЗапасыИПотребности* есть *резерв* и *резерв по мере поступления* и надо будет итоговый резерв высчитывать исходя из остатка и т.д.

По идее, можно посмотреть более ранние периоды, чтобы установить соответствие по "СКЛ1": ТОВАР1 -> "ОРГ1" и ТОВАР2  -> "ОРГ2". Соответствие может быть неявным, а через какую-нибудь аналитику, если  " ТОВАРi" неоднозначно соответствует организации

```bsl
ВЫБРАТЬ
	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
	СУММА(РаспределениеЗапасов.ВНаличии) КАК ВНаличии,
	СУММА(РаспределениеЗапасов.Свободно) КАК СвободноНаСкладе
ИЗ
	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
ГДЕ
	РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
	И РаспределениеЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	И РаспределениеЗапасов.Склад = &Склад

СГРУППИРОВАТЬ ПО
	РаспределениеЗапасов.Номенклатура
```

```bsl
ВЫБРАТЬ
	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	ВЫРАЗИТЬ(ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК Справочник.Склады) КАК Склад,
	ТоварыОрганизацийОстатки.Организация КАК Организация,
	ТоварыОрганизацийОстатки.КоличествоОстаток КАК ОстатокОрганизации
ПОМЕСТИТЬ ВТ_ОстаткиОрганизаций
ИЗ
	РегистрНакопления.ТоварыОрганизаций.Остатки(, Организация = &Организация) КАК ТоварыОрганизацийОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	РезервыТоваровОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	РезервыТоваровОрганизацийОстатки.Организация КАК Организация,
	РезервыТоваровОрганизацийОстатки.КоличествоОстаток КАК РезервОрганизации
ПОМЕСТИТЬ ВТ_РезервыОрганизаций
ИЗ
	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(, Организация = &Организация) КАК РезервыТоваровОрганизацийОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	ВТ_ОстаткиОрганизаций.Номенклатура КАК Номенклатура,
	ВТ_ОстаткиОрганизаций.Склад КАК Склад,
	ВТ_ОстаткиОрганизаций.Организация КАК Организация,
	ВТ_ОстаткиОрганизаций.ОстатокОрганизации КАК ОстатокОрганизации,
	ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) КАК РезервОрганизации,
	ВТ_ОстаткиОрганизаций.ОстатокОрганизации - ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) КАК СвободныйОстатокОрганизации
ПОМЕСТИТЬ ВТ_ОстаткиИРезервыОрганизаций
ИЗ
	ВТ_ОстаткиОрганизаций КАК ВТ_ОстаткиОрганизаций
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезервыОрганизаций КАК ВТ_РезервыОрганизаций
		ПО ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры = ВТ_РезервыОрганизаций.АналитикаУчетаНоменклатуры
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
	СУММА(РаспределениеЗапасов.ВНаличии) КАК ОстатокНаСкладе,
	СУММА(РаспределениеЗапасов.Свободно) КАК СвободныйОстатокНаСкладе
ПОМЕСТИТЬ ВТ_СвободныеОстаткиИРезервыНаСкладе
ИЗ
	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
ГДЕ
	РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
	И РаспределениеЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	И (ВЫРАЗИТЬ(РаспределениеЗапасов.Склад КАК Справочник.Склады)) = &Склад

СГРУППИРОВАТЬ ПО
	РаспределениеЗапасов.Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ОстаткиИРезервыОрганизаций.Номенклатура КАК Номенклатура,
	ВТ_ОстаткиИРезервыОрганизаций.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	ВТ_ОстаткиИРезервыОрганизаций.Склад КАК Склад,
	ВТ_ОстаткиИРезервыОрганизаций.Организация КАК Организация,
	ВТ_ОстаткиИРезервыОрганизаций.РезервОрганизации КАК РезервОрганизации,
	ВТ_ОстаткиИРезервыОрганизаций.ОстатокОрганизации КАК ОстатокОрганизации,
	ВТ_ОстаткиИРезервыОрганизаций.СвободныйОстатокОрганизации КАК СвободныйОстатокОрганизации,
	МИНИМУМ(ВТ_СвободныеОстаткиИРезервыНаСкладе.ОстатокНаСкладе) КАК ОстатокНаСкладе,
	МИНИМУМ(ВТ_СвободныеОстаткиИРезервыНаСкладе.СвободныйОстатокНаСкладе) КАК СвободныйОстатокНаСкладе
ИЗ
	ВТ_ОстаткиИРезервыОрганизаций КАК ВТ_ОстаткиИРезервыОрганизаций,
	ВТ_СвободныеОстаткиИРезервыНаСкладе КАК ВТ_СвободныеОстаткиИРезервыНаСкладе

СГРУППИРОВАТЬ ПО
	ВТ_ОстаткиИРезервыОрганизаций.Номенклатура,
	ВТ_ОстаткиИРезервыОрганизаций.Склад,
	ВТ_ОстаткиИРезервыОрганизаций.Организация,
	ВТ_ОстаткиИРезервыОрганизаций.РезервОрганизации,
	ВТ_ОстаткиИРезервыОрганизаций.ОстатокОрганизации,
	ВТ_ОстаткиИРезервыОрганизаций.СвободныйОстатокОрганизации,
	ВТ_ОстаткиИРезервыОрганизаций.Номенклатура.ЕдиницаИзмерения
```

```bsl
ВЫБРАТЬ
	ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
	ЗапасыИПотребностиОстатки.Склад КАК Склад,
	ЗапасыИПотребностиОстатки.Заказ.Организация КАК Организация,
	СУММА(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток) КАК РезервироватьНаСкладе,
	СУММА(ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток) КАК РезервироватьПоМереПоступления,
	СУММА(ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток, 0)) + СУММА(ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток, 0)) КАК ИтоговыйРезерв
ПОМЕСТИТЬ ВТ_РезервыТоваровОрганизации
ИЗ
	РегистрНакопления.ЗапасыИПотребности.Остатки(
			,
			(ВЫРАЗИТЬ(Склад КАК Справочник.Склады)) = &Склад
				И Заказ.Организация = &Организация) КАК ЗапасыИПотребностиОстатки

СГРУППИРОВАТЬ ПО
	ЗапасыИПотребностиОстатки.Номенклатура,
	ЗапасыИПотребностиОстатки.Склад,
	ЗапасыИПотребностиОстатки.Заказ.Организация
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	ВЫРАЗИТЬ(ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК Справочник.Склады) КАК Склад,
	ТоварыОрганизацийОстатки.Организация КАК Организация,
	ТоварыОрганизацийОстатки.КоличествоОстаток КАК ОстатокОрганизации
ПОМЕСТИТЬ ВТ_ОстаткиТоваровОрганизации
ИЗ
	РегистрНакопления.ТоварыОрганизаций.Остатки(
			,
			Организация = &Организация
				И (ВЫРАЗИТЬ(АналитикаУчетаНоменклатуры.МестоХранения КАК Справочник.Склады)) = &Склад) КАК ТоварыОрганизацийОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ОстаткиТоваровОрганизации.Номенклатура КАК Номенклатура,
	ВТ_ОстаткиТоваровОрганизации.Организация КАК Организация,
	ВТ_ОстаткиТоваровОрганизации.Склад КАК Склад,
	ВТ_ОстаткиТоваровОрганизации.ОстатокОрганизации КАК Остаток,
	ЕСТЬNULL(ВТ_РезервыТоваровОрганизации.ИтоговыйРезерв, 0) КАК ИтоговыйРезерв,
	ВТ_ОстаткиТоваровОрганизации.ОстатокОрганизации - ЕСТЬNULL(ВТ_РезервыТоваровОрганизации.ИтоговыйРезерв, 0) КАК СвободныйОстаток
ИЗ
	ВТ_ОстаткиТоваровОрганизации КАК ВТ_ОстаткиТоваровОрганизации
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезервыТоваровОрганизации КАК ВТ_РезервыТоваровОрганизации
		ПО (ВТ_ОстаткиТоваровОрганизации.Номенклатура = ВТ_РезервыТоваровОрганизации.Номенклатура)
```

Пример из ТГ чата
```bsl
ВЫБРАТЬ
	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	ВложенныйЗапрос.Склад КАК Склад,
	ВложенныйЗапрос.Серия КАК Серия,
	СУММА(ВложенныйЗапрос.Количество) КАК Количество
ПОМЕСТИТЬ Остатки
ИЗ
	(ВЫБРАТЬ
		Т.Номенклатура КАК Номенклатура,
		Т.Характеристика КАК Характеристика,
		Т.Назначение КАК Назначение,
		Т.Склад КАК Склад,
		Т.Серия КАК Серия,
		-Т.ВРезервеОстаток - Т.КОтгрузкеОстаток КАК Количество
	ИЗ
		РегистрНакопления.ТоварыКОтгрузке.Остатки(&ДатаОстатков {(&ДатаОстатков)}, склад В ИЕРАРХИИ (&склады)) КАК Т
	
	ОБЪЕДИНИТЬ ВСЕ
	
	ВЫБРАТЬ
		Т.Номенклатура,
		Т.Характеристика,
		Т.Назначение,
		Т.Склад,
		Т.Серия,
		Т.ВНаличииОстаток
	ИЗ
		РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаОстатков {(&ДатаОстатков)}, склад В ИЕРАРХИИ (&склады)) КАК Т) КАК ВложенныйЗапрос

СГРУППИРОВАТЬ ПО
	ВложенныйЗапрос.Номенклатура,
	ВложенныйЗапрос.Склад,
	ВложенныйЗапрос.Серия
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Закупки.Контрагент КАК Поставщик,
	Закупки.Договор КАК Контракт,
	Закупки.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	Закупки.Регистратор КАК ДокументОприходования,
	Закупки.Склад КАК Склад
ПОМЕСТИТЬ Приход
ИЗ
	РегистрНакопления.Закупки КАК Закупки
ГДЕ
	Закупки.Склад В ИЕРАРХИИ(&склады)

СГРУППИРОВАТЬ ПО
	Закупки.Контрагент,
	Закупки.АналитикаУчетаНоменклатуры.Серия,
	Закупки.Договор,
	Закупки.Регистратор,
	Закупки.Склад
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Остатки.Номенклатура.гуЕСКЛП.ДействующееВеществоМНН КАК МНН,
	Остатки.Номенклатура КАК Номенклатура,
	Остатки.Серия КАК СерияНоменклатуры,
	СУММА(Остатки.Количество) КАК Количество,
	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) * СУММА(Остатки.Количество) КАК ЧИСЛО(15, 2)) КАК Сумма,
	Приход.Поставщик КАК Поставщик,
	Приход.Контракт КАК Контракт,
	Приход.ДокументОприходования КАК ДокументОприходования,
	Остатки.Склад КАК Склад
ИЗ
	Остатки КАК Остатки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаОстатков {(&ДатаОстатков)}, ВидЦены = &ВыбВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		ПО Остатки.Серия = ЦеныНоменклатурыСрезПоследних.СерияНоменклатуры
		ЛЕВОЕ СОЕДИНЕНИЕ Приход КАК Приход
		ПО Остатки.Серия = Приход.Серия
ГДЕ
	Остатки.Количество <> 0

СГРУППИРОВАТЬ ПО
	Остатки.Номенклатура,
	Остатки.Серия,
	ЦеныНоменклатурыСрезПоследних.Цена,
	Остатки.Номенклатура.гуЕСКЛП.ДействующееВеществоМНН,
	Приход.Поставщик,
	Приход.Контракт,
	Приход.ДокументОприходования,
	Остатки.Склад
```

Финальный запрос (рабочий)
```bsl
ВЫБРАТЬ
	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	ТоварыОрганизацийОстатки.Организация КАК Организация,
	ТоварыОрганизацийОстатки.КоличествоОстаток КАК Остаток,
	КлючиАналитикиУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	КлючиАналитикиУчетаНоменклатуры.МестоХранения КАК МестоХранения
ПОМЕСТИТЬ ВТ_ОстаткиОрганизаций
ИЗ
	РегистрНакопления.ТоварыОрганизаций.Остатки(, Организация = &Организация) КАК ТоварыОрганизацийОстатки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
		ПО ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
ГДЕ
	(ВЫРАЗИТЬ(КлючиАналитикиУчетаНоменклатуры.МестоХранения КАК Справочник.Склады)) = &Склад
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	РезервыТоваровОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	РезервыТоваровОрганизацийОстатки.КоличествоОстаток КАК РезервОрганизации
ПОМЕСТИТЬ ВТ_РезервыОрганизаций
ИЗ
	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(, Организация = &Организация) КАК РезервыТоваровОрганизацийОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЗапасыИПотребностиОстатки.ПоступитОстаток КАК Поступит,
	ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладе,
	ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступления,
	ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура
ПОМЕСТИТЬ ВТ_ПоступитИРезервы
ИЗ
	РегистрНакопления.ЗапасыИПотребности.Остатки(
			,
			(ВЫРАЗИТЬ(Склад КАК Справочник.Склады)) = &Склад
				И Заказ.Организация = &Организация) КАК ЗапасыИПотребностиОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	ВТ_ОстаткиОрганизаций.МестоХранения КАК МестоХранения,
	ВТ_ОстаткиОрганизаций.Номенклатура КАК Номенклатура,
	ВТ_ОстаткиОрганизаций.Организация КАК Организация,
	ВТ_ОстаткиОрганизаций.Остаток КАК Остаток,
	ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) КАК РезервОрганизации,
	ЕСТЬNULL(ВТ_ПоступитИРезервы.Поступит, 0) КАК Поступит,
	ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьНаСкладе, 0) КАК РезервироватьНаСкладе,
	ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
	ВТ_ОстаткиОрганизаций.Остаток - ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.Поступит, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьНаСкладе, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьПоМереПоступления, 0) КАК СвободныйОстаток
ИЗ
	ВТ_ОстаткиОрганизаций КАК ВТ_ОстаткиОрганизаций
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезервыОрганизаций КАК ВТ_РезервыОрганизаций
		ПО ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры = ВТ_РезервыОрганизаций.АналитикаУчетаНоменклатуры
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоступитИРезервы КАК ВТ_ПоступитИРезервы
		ПО ВТ_ОстаткиОрганизаций.Номенклатура = ВТ_ПоступитИРезервы.Номенклатура
```