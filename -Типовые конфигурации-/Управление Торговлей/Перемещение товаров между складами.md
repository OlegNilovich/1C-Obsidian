#TODO #УТ

![[Pasted image 20241201221754.png]]

```bsl

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗаполнитьПоОстаткамНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СкладОтправительПриИзменении(Элемент)
	ЗаполнитьПоОстаткамНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	ЗаполнитьПоОстаткамНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументПеремещения(Команда)
	
	Если Объект.СкладОтправитель = Объект.СкладПолучатель Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Склад отправителя должен отличаться от склада получателя");
		Возврат;
	КонецЕсли;
	
	Если СписокНоменклатуры.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Список номенклатуры пуст!");
		Возврат;
	КонецЕсли;
	
	ДокПеремещения = СоздатьДокументПеремещенияНаСервере();
	
	Текст		= "Документ успешно создан";
	Нажатие	= ПолучитьНавигационнуюСсылку(ДокПеремещения);
	Пояснение = ДокПеремещения;
	Картинка	= БиблиотекаКартинок.Оповещения;
	Статус		= СтатусОповещенияПользователя.Важное;
	ПоказатьОповещениеПользователя(Текст, Нажатие, Пояснение, Картинка, Статус, Текст);
	
	ПараметрыФормы = Новый Структура("Ключ", ДокПеремещения);
	ОткрытьФорму("Документ.ПеремещениеТоваров.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументПеремещенияНаСервере()
	
	ДокПеремещения = Документы.ПеремещениеТоваров.СоздатьДокумент();
	ДокПеремещения.Дата						= ТекущаяДата();
	ДокПеремещения.Организация				= Объект.Организация;
	ДокПеремещения.СкладОтправитель			= Объект.СкладОтправитель;
	ДокПеремещения.СкладПолучатель			= Объект.СкладПолучатель;
	ДокПеремещения.Ответственный        		= Пользователи.ТекущийПользователь();
	ДокПеремещения.Автор                       		= Пользователи.ТекущийПользователь();
	ДокПеремещения.Статус						= Перечисления.СтатусыПеремещенийТоваров.Принято;
	ДокПеремещения.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
	ДокПеремещения.ВариантПриемкиТоваров	= Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным;
	
	Для Каждого ТекСтрока Из СписокНоменклатуры Цикл
		НоваяСтрока = ДокПеремещения.Товары.Добавить();
		НоваяСтрока.Номенклатура			= ТекСтрока.Номенклатура;
		НоваяСтрока.Упаковка				= ТекСтрока.ЕдиницаИзмерения;
		НоваяСтрока.Количество				= ТекСтрока.СвободныйОстаток;
		НоваяСтрока.КоличествоУпаковок	= ТекСтрока.СвободныйОстаток;
	КонецЦикла;
	
	ДокПеремещения.Записать();
	
	Возврат ДокПеремещения.Ссылка;
	
КонецФункции

#КонецОбласти


&НаСервере
Процедура ЗаполнитьПоОстаткамНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Склад", Объект.СкладОтправитель);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	               |	ТоварыОрганизацийОстатки.Организация КАК Организация,
	               |	ТоварыОрганизацийОстатки.КоличествоОстаток КАК Остаток,
	               |	КлючиАналитикиУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	               |	КлючиАналитикиУчетаНоменклатуры.МестоХранения КАК МестоХранения
	               |ПОМЕСТИТЬ ВТ_ОстаткиОрганизаций
	               |ИЗ
	               |	РегистрНакопления.ТоварыОрганизаций.Остатки(, Организация = &Организация) КАК ТоварыОрганизацийОстатки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	               |		ПО ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
	               |ГДЕ
	               |	(ВЫРАЗИТЬ(КлючиАналитикиУчетаНоменклатуры.МестоХранения КАК Справочник.Склады)) = &Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервыТоваровОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	               |	РезервыТоваровОрганизацийОстатки.КоличествоОстаток КАК РезервОрганизации
	               |ПОМЕСТИТЬ ВТ_РезервыОрганизаций
	               |ИЗ
	               |	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(, Организация = &Организация) КАК РезервыТоваровОрганизацийОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗапасыИПотребностиОстатки.ПоступитОстаток КАК Поступит,
	               |	ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток КАК РезервироватьНаСкладе,
	               |	ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступления,
	               |	ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура
	               |ПОМЕСТИТЬ ВТ_ПоступитИРезервы
	               |ИЗ
	               |	РегистрНакопления.ЗапасыИПотребности.Остатки(
	               |			,
	               |			(ВЫРАЗИТЬ(Склад КАК Справочник.Склады)) = &Склад
	               |				И Заказ.Организация = &Организация) КАК ЗапасыИПотребностиОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	               |	ВТ_ОстаткиОрганизаций.МестоХранения КАК МестоХранения,
	               |	ВТ_ОстаткиОрганизаций.Номенклатура КАК Номенклатура,
	               |	ВТ_ОстаткиОрганизаций.Организация КАК Организация,
	               |	ВТ_ОстаткиОрганизаций.Остаток КАК Остаток,
	               |	ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) КАК РезервОрганизации,
	               |	ЕСТЬNULL(ВТ_ПоступитИРезервы.Поступит, 0) КАК Поступит,
	               |	ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьНаСкладе, 0) КАК РезервироватьНаСкладе,
	               |	ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
	               |	ВТ_ОстаткиОрганизаций.Остаток - ЕСТЬNULL(ВТ_РезервыОрганизаций.РезервОрганизации, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.Поступит, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьНаСкладе, 0) - ЕСТЬNULL(ВТ_ПоступитИРезервы.РезервироватьПоМереПоступления, 0) КАК СвободныйОстаток
	               |ИЗ
	               |	ВТ_ОстаткиОрганизаций КАК ВТ_ОстаткиОрганизаций
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезервыОрганизаций КАК ВТ_РезервыОрганизаций
	               |		ПО ВТ_ОстаткиОрганизаций.АналитикаУчетаНоменклатуры = ВТ_РезервыОрганизаций.АналитикаУчетаНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоступитИРезервы КАК ВТ_ПоступитИРезервы
	               |		ПО ВТ_ОстаткиОрганизаций.Номенклатура = ВТ_ПоступитИРезервы.Номенклатура";
	СписокНоменклатуры.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = СписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры
```