
Выгружаем результат работы отчета *ВедомостьПоТоварамОрганизаций* в таблицу значений, предварительно наложив отбор по *Организации* и *Складу*

```bsl
&НаСервере
Процедура ОтчетНаСервере()

	Отчет = Отчеты.ВедомостьПоТоварамОрганизаций.Создать();
	СКД = Отчет.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиКомпоновки = СКД.НастройкиПоУмолчанию;
	
	// Отбор по организации
	ВыбранноеПолеКомпоновкиДанных = НастройкиКомпоновки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПолеКомпоновкиДанных.Использование = Истина;
	ВыбранноеПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("Организация");
	ЭлементОтбораКомпоновкиДанных = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораКомпоновкиДанных.Использование = Истина;
	ЭлементОтбораКомпоновкиДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	ЭлементОтбораКомпоновкиДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораКомпоновкиДанных.ПравоеЗначение = Организация;
	
	// Отбор по складу
	ВыбранноеПолеКомпоновкиДанных = НастройкиКомпоновки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПолеКомпоновкиДанных.Использование = Истина;
	ВыбранноеПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("Склад");
	ЭлементОтбораКомпоновкиДанных = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораКомпоновкиДанных.Использование = Истина;
	ЭлементОтбораКомпоновкиДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
	ЭлементОтбораКомпоновкиДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораКомпоновкиДанных.ПравоеЗначение = СкладОтправитель;	

	ДанныеРасшифровки=Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, НастройкиКомпоновки, 
	ДанныеРасшифровки,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ТЗ = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТЗ);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	СписокНоменклатуры.Очистить();
	Для Каждого ТекСтрока Из ТЗ Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Номенклатура) ИЛИ 
				НЕ ЗначениеЗаполнено(ТекСтрока.НоменклатураЕдиницаИзмерения)
					ИЛИ ТекСтрока.КоличествоКонечныйОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = СписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		
	КонецЦикла;

КонецПроцедуры
```