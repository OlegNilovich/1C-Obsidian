#УНФ

Шаблон: [[СозданиеНаОсновании]]
### Задача:
Требуется на основании документа "Заказ покупателя", создавать документ "Счет на оплату" с разделением на товары и услуги. Если в заказе одновременно присутствуют и товары и услуги, тогда нужно создавать два счета: один для товаров, другой для услуг.

### Решение:

#### Конфигуратор:

1. Копируем шаблон [[СозданиеНаОсновании]] и называем его *ПР_СозданиеСчетовСРазделением*
2. Модуль объекта обработки:
```bsl
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиСозданиеСвязанныхОбъектов();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид					= Вид;
	ПараметрыРегистрации.Версия				= "1.0.0.1";
	ПараметрыРегистрации.Наименование		= Синоним;
	ПараметрыРегистрации.БезопасныйРежим	= Ложь;
	ПараметрыРегистрации.Информация			= "Создание на основании";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПокупателя");
	
	Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовКлиентскогоМетода();
	Команда							= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление			= Синоним;
	Команда.Идентификатор			= ИмяОбработки;
	Команда.Использование			= Использование;
	Команда.ПоказыватьОповещение	= Истина;
	
	Возврат ПараметрыРегистрации; 
	
КонецФункции

#КонецОбласти
```

3. Модуль формы обработки
```bsl

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив, СозданныеОбъекты) Экспорт
	
	Если ОбъектыНазначенияМассив.Количество() > 0 Тогда
		Заказ = ОбъектыНазначенияМассив[0];
		СоздатьСчетаНаОплату(Заказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьСчетаНаОплату(СсылкаНаОбъект)

	МассивНоменклатуры = СсылкаНаОбъект.Запасы.ВыгрузитьКолонку("Номенклатура");
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивНоменклатуры, 
	"ТипНоменклатуры");
	
	#Область СчетТовары
	СчетТовары = Документы.СчетНаОплату.СоздатьДокумент();
	СчетТовары.Заполнить(СсылкаНаОбъект);
	СчетТовары.Дата = ТекущаяДата();
	
	НеТовары = Новый Массив;
	Для Каждого ТекСтрока Из СчетТовары.Запасы Цикл
		ТипНоменклатуры = РеквизитыНоменклатуры[ТекСтрока.Номенклатура];
		Если ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
			НеТовары.Добавить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого НеТовар Из НеТовары Цикл
		СчетТовары.Запасы.Удалить(НеТовар);
	КонецЦикла;
	
	Если СчетТовары.Запасы.Количество() > 0 Тогда
		Попытка
			СчетТовары.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю("Создан счет на товары: " + СчетТовары.Ссылка);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось записать счет для товаров");
		КонецПопытки;
	КонецЕсли;
	#КонецОбласти
	
	
	#Область СчетУслуги
	СчетУслуги = Документы.СчетНаОплату.СоздатьДокумент();
	СчетУслуги.Заполнить(СсылкаНаОбъект);
	СчетУслуги.Дата = ТекущаяДата();
	
	НеУслуги = Новый Массив;
	Для Каждого ТекСтрока Из СчетУслуги.Запасы Цикл
		ТипНоменклатуры = РеквизитыНоменклатуры[ТекСтрока.Номенклатура];
		Если ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
			НеУслуги.Добавить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого НеУслуга Из НеУслуги Цикл
		СчетУслуги.Запасы.Удалить(НеУслуга);
	КонецЦикла;
	
	Если СчетУслуги.Запасы.Количество() > 0 Тогда
		Попытка
			СчетУслуги.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю("Создан счет на услуги: " + СчетУслуги.Ссылка);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось записать счет для услуг");
		КонецПопытки;
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти
```