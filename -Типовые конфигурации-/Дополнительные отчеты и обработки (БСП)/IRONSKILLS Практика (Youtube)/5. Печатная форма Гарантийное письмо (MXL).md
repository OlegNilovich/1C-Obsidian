#УНФ

Шаблон: [[-Типовые конфигурации-/Дополнительные отчеты и обработки (БСП)/Шаблоны внешних отчетов и обработок/Назначаемые/Печатная форма/Печатная форма]]
### Задача:

Для документа "Заказ покупателя", требуется добавить печатную форму "Гарантийное письмо"
![[Pasted image 20241115162145.png]]
![[Pasted image 20241115162112.png]]

### Решение:

#### Конфигуратор:

1. Копируем шаблон [[-Типовые конфигурации-/Дополнительные отчеты и обработки (БСП)/Шаблоны внешних отчетов и обработок/Назначаемые/Печатная форма/Печатная форма]] и называем его *ПР_ЗаказКлиента_ГарантийноеПисьмо*
2. Макет *ПФ_MXL_ГарантийноеПисьмо*:
![[Pasted image 20241115161906.png]]
3. Модуль объекта обработки:
```bsl
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним), Синоним, ИмяОбработки);
	
	Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид = Вид;
	ПараметрыРегистрации.Версия = "1.0.0.1";
	ПараметрыРегистрации.Наименование = Синоним;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	ПараметрыРегистрации.Информация = "Внешняя печатная форма """ + Синоним + """";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ЗаказПокупателя");
	
	Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = Синоним;
	Команда.Идентификатор = ИмяОбработки;
	Команда.Использование = Использование;
	Команда.Модификатор = "ПечатьMXL";
	Команда.ЗаменяемыеКоманды = ""; // если нужно заменить текущую команду печати, укажите её идентификатор
	Команда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации; 
	
КонецФункции

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(	
	КоллекцияПечатныхФорм,
	ЭтотОбъект.Метаданные().Имя,
	НСтр("ru = 'Тест'"),
	СформироватьПечатнуюФорму_ГарантийноеПисьмо(МассивОбъектов));
	
	//ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры 

#КонецОбласти

#Область ФункцииФормированияПечатныхФорм

Функция СформироватьПечатнуюФорму_ГарантийноеПисьмо(МассивОбъектов)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("ПФ_MXL_ГарантийноеПисьмо");
	Область = Макет.ПолучитьОбласть("Основная");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Номер КАК Номер,
		|	ЗаказПокупателя.Дата КАК Дата,
		|	ЗаказПокупателя.ДатаОтгрузки КАК СрокОплаты,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПокупателя.Организация) КАК Организация,
		|	ПРЕДСТАВЛЕНИЕ(ЗаказПокупателя.Контрагент) КАК Контрагент
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.Ссылка В(&МассивОбъектов)";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Область.Параметры.Заполнить(Выборка);
		Область.Параметры.Дата				= Формат(Выборка.Дата, "ДЛФ=D");
		Область.Параметры.СрокОплаты	= Формат(Выборка.СрокОплаты, "ДЛФ=D");
		ТабДок.Вывести(Область);
		
	КонецЦикла;
	
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
	
КонецФункции	

#КонецОбласти
```