```bsl
// Глобальная обработка по расписанию (Загрузка курсов валют)
// Копируем шаблон "ВнешняяОбработкаПоРасписанию" с названием "ЗагрузкаКурсовВалют"
// Открываем копию шаблона в конфигураторе
// Открываем справочник "Дополнительные отчеты и обработки" В 1С:Предприятии
// Нажимаем кнопку создать и указываем путь до нашей обработки
// Во вкладке "Команды" ставим флажок напротив нашей обработки в колонке с часами
// Указываем расписание. После записи будет создано регламентное задание
// Переходим в раздел "НСИ и Администрирование" => "Обслуживание" =>
// => "Регламентные операции" => "Регламентные и фоновые задания"


// Модуль объекта обработки:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    
    ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
    РегистрационныеДанные = Новый Структура;
    РегистрационныеДанные.Вставить("Вид","ДополнительнаяОбработка");
    РегистрационныеДанные.Вставить("Наименование", Синоним);
    РегистрационныеДанные.Вставить("Версия", "1.0");
    РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
    РегистрационныеДанные.Вставить("Информация", "Обработка """ + Синоним + """");
    
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовСерверногоМетода", Истина, );
        
    РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
    
    Возврат РегистрационныеДанные;
                                       
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
    
    Команды = Новый ТаблицаЗначений;
    Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
    Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
    Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
    Возврат Команды;
   
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, 
						  Представление, 
						  Идентификатор, 
						  Использование = "ОткрытиеФормы", 
						  ПоказыватьОповещение = Ложь, 
						  Модификатор = "ПечатьMXL")
    
    НоваяКоманда = ТаблицаКоманд.Добавить();
    НоваяКоманда.Представление = Представление;
    НоваяКоманда.Идентификатор = Идентификатор;
    НоваяКоманда.Использование = Использование;
    НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
    НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры 

#КонецОбласти

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт 
	         
	ЗагрузкаКурсовВалютРБ();
	
КонецПроцедуры    

Процедура ЗагрузкаКурсовВалютРБ() Экспорт
	
	ИмяСобытияЖурнала = "IRONSKILLS_ЗагрузкаКурсовВалют";
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Информация, , , "старт: " + ТекущаяДата());
	
	ДатаКурса = НачалоДня(ТекущаяДата());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Валюты.Наименование КАК СимвольныйКод,
		|	Валюты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Валюты КАК Валюты
		|ГДЕ
		|	Валюты.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СоответствиеВалют = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеВалют.Вставить(Выборка.СимвольныйКод, Выборка.Ссылка);
	КонецЦикла;
	
	HTTPСоединение = Новый HTTPСоединение("www.nbrb.by", ,,,, 20, Новый ЗащищенноеСоединениеOpenSSL); 
	
	HTTPЗапрос = Новый HTTPЗапрос("/api/exrates/rates?ondate=" + Формат(ДатаКурса, "ДФ=yyyy-MM-dd") + "&periodicity=0");
	
	Попытка
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
	Исключение
		ТекстОшибки = "Не удалось выполнить HTTP-запрос, адрес ресурса: " + "www.nbrb.by" + "/api/exrates/rates?ondate=" + Формат(ДатаКурса, "ДФ=yyyy-MM-dd") + "&periodicity=0";
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;
	
	Если HTTPОтвет.КодСостояния = 200 Тогда
		
		ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
		
		МассивКурсов = ПрочитатьJSON(ЧтениеJSON);
		
		Для каждого СтруктураКурса Из МассивКурсов Цикл
			
			ВалютаСсылка = СоответствиеВалют.Получить(СтруктураКурса.Cur_Abbreviation);
			
			Если ВалютаСсылка <> Неопределено Тогда
				НоваяЗапись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
				НоваяЗапись.Период		= ДатаКурса;
				НоваяЗапись.Валюта		= ВалютаСсылка;
				НоваяЗапись.Курс		= СтруктураКурса.Cur_OfficialRate;
				НоваяЗапись.Кратность	= СтруктураКурса.Cur_Scale;
				НоваяЗапись.Записать();
			Иначе
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе                                                                                                         
		
		ОтветСервера = HTTPОтвет.ПолучитьТелоКакСтроку();
		ТекстОшибки = "Ошибка выполнения запроса: код состояния "  + HTTPОтвет.КодСостояния + ", тело ответа: " + ОтветСервера;
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
		
	КонецЕсли;      
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала, УровеньЖурналаРегистрации.Информация, , , "финиш: " + ТекущаяДата());
	
КонецПроцедуры


```