```bsl
// Назначаемая обработка (Заполнение объекта: Команда)

// Копируем шаблон "ЗаполнениеОбъекта" с названием "ЗаполнениеПересчетаТоваров"
// Открываем обработку-печатную форму в конфигураторе
// Открываем модуль объекта обработки => Функция СведенияОВнешнейОбработке()
// Указываем назначение в строчке: Назначение.Добавить("Документ.ПересчетТоваров");
// Открываем справочник "Дополнительные отчеты и обработки" В 1С:Предприятии
// Нажимаем кнопку создать и указываем путь до нашей обработки

//============================================================================

// Модуль объекта обработки:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	Назначения = Новый Массив ;
	Назначения.Добавить("Документ.ПересчетТоваров") ;
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Вид","ЗаполнениеОбъекта");
	РегистрационныеДанные.Вставить("Наименование", Синоним);
	РегистрационныеДанные.Вставить("Версия", "1.0");
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	РегистрационныеДанные.Вставить("Информация", "Заполнение объекта");
	РегистрационныеДанные.Вставить("Назначение",Назначения);
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовКлиентскогоМетода", Истина, );
	
	РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(	ТаблицаКоманд,
							Представление, 
							Идентификатор, 
							Использование = "ОткрытиеФормы", 
							ПоказыватьОповещение = Ложь, 
							Модификатор = "ПечатьMXL")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

#КонецОбласти

//============================================================================

// Модуль формы:
&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	
	//Для Каждого СсылкаНаНаОбъект Из ОбъектыНазначенияМассив Цикл
	//	ДобавитьСтрокуВТовары(СсылкаНаНаОбъект);
	//КонецЦикла;	
	//
	//ВладелецФормы.Прочитать();
	
	ЧтоДелатьПосле = Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект);
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Проводник.Фильтр = "Текстовый документ | *.txt";
	Проводник.Показать(ЧтоДелатьПосле);
	
КонецПроцедуры         

&НаКлиенте
Процедура ПослеВыбораФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
	//1. прочитать файл и сформировать массив штрихкодов
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать(ПутьКФайлу);
	
	КоличествоСтрок = Текст.КоличествоСтрок();
	
	МассивШтрихкодов = Новый Массив;
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		Штрихкод = Текст.ПолучитьСтроку(НомерСтроки);
		МассивШтрихкодов.Добавить(Штрихкод);
	КонецЦикла;
	
	//2. Найти номенклатуру по штрихкодам (соответствие Штрихкод - ссылка на номенклатуру
	СоответствиеШтрихкодНоменклатура = ПолучитьДанные(МассивШтрихкодов);
	
	//3. Обойти массив штрихкодов (всех)
	Для каждого Штрихкод Из МассивШтрихкодов Цикл
		
		//3.1. Определить номенклатуру по штрихкоду
		Номенклатура = СоответствиеШтрихкодНоменклатура.Получить(Штрихкод);
		
		Если Номенклатура <> Неопределено Тогда
			
			//3.2. Найти в документе строку с этой номенклатурой
			СтруктураПоиска = Новый Структура("Номенклатура", Номенклатура);
			НайденныеСтроки = ВладелецФормы.Объект.Товары.НайтиСтроки(СтруктураПоиска);
			
			Если НайденныеСтроки.Количество() <> 0 Тогда
				//3.3. Увеличить количество факт на 1
				СтрокаТЧ = НайденныеСтроки[0];
				СтрокаТЧ.КоличествоФакт = СтрокаТЧ.КоличествоФакт + 1;
				СтрокаТЧ.КоличествоУпаковокФакт = СтрокаТЧ.КоличествоУпаковокФакт + 1;
			КонецЕсли;
			
		Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Неизвестный штрихкод: " + Штрихкод;
			Сообщение.Сообщить();			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ПослеВыбораФайла()

&НаСервере
Функция ПолучитьДанные(МассивШтрихкодов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод В(&МассивШтрихкодов)";
	
	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Данные = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Данные.Вставить(Выборка.Штрихкод, Выборка.Номенклатура);
	КонецЦикла;

	Возврат Данные;

КонецФункции // ПолучитьДанные()


//&НаСервере
//Процедура ДобавитьСтрокуВТовары(СсылкаНаНаОбъект)
//	ДокОбъект = СсылкаНаНаОбъект.ПолучитьОбъект();
//	НовСтр = ДокОбъект.Товары.Добавить();
//	НовСтр.Цена = 100;
//	ДокОбъект.Записать();
//КонецПроцедуры	

```