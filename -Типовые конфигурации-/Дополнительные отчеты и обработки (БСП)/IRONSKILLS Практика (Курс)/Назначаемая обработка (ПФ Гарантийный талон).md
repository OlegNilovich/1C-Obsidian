```bsl
// Назначаемая обработка (ПФ Гарантийный талон)

// Копируем шаблона "ВнешняяПечатнаяФорма" с названием "ПФ_ЗаказКлиентаГарантийныйТалон"
// Открываем обработку-печатную форму в конфигураторе
// Открываем модуль объекта обработки => Функция СведенияОВнешнейОбработке()
// Указываем назначение в строчке: Назначение.Добавить("Документ.ЗаказКлиента");
// Открываем справочник "Дополнительные отчеты и обработки" В 1С:Предприятии
// Нажимаем кнопку создать и указываем путь до нашей обработки
// Во вкладке "Команды" указываем раздел для нашей обработки, например "Продажи"
// В той же вкладке настраиваем быстрый доступ для всех пользователей нашей обработки
// Наша обработка находится по пути: "Продажи" => "Сервис" => "Дополнительные обработки"

// Модуль объекта обработки-печатной формы:
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОбработки = ЭтотОбъект.Метаданные().Имя; 
	Синоним = ЭтотОбъект.Метаданные().Синоним; 
	Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОбработки);
	
	Назначение = Новый Массив;
	Назначение.Добавить("Документ.ЗаказКлиента");
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Вид","ПечатнаяФорма");
	РегистрационныеДанные.Вставить("Наименование", Синоним);
	РегистрационныеДанные.Вставить("Версия", "1.0");
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	РегистрационныеДанные.Вставить("Информация", "Внешняя печатная форма """ + Синоним + """");
	РегистрационныеДанные.Вставить("Назначение", Назначение);
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, Синоним, ИмяОбработки , "ВызовСерверногоМетода", Истина, "ПечатьMXL");
	
	РегистрационныеДанные.Вставить("Команды", ТаблицаКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(	ТаблицаКоманд, 
	Представление, 
	Идентификатор, 
	Использование = "ОткрытиеФормы", 
	ПоказыватьОповещение = Ложь, 
	Модификатор = "ПечатьMXL")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

#КонецОбласти

//============================================================================

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	КоллекцияПечатныхФорм,
	"ЗаказКлиента_ГарантийныйТалон",    //должен совпадать с именем обработки
	НСтр("ru = 'Гарантийный талон (ironskills)'"),
	СформироватьПечатнуюФормуТест(МассивОбъектов),
	,
	);
	//ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
КонецПроцедуры 

Функция СформироватьПечатнуюФормуТест(МассивОбъектов)
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("ПФ_MXL_ГарантийныйТалон");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.Организация.Наименование КАК НаименованиеОрганизации,
	|	ЗаказКлиента.Организация КАК Организация,
	|	ЗаказКлиента.Номер КАК Номер,
	|	ЗаказКлиента.Дата КАК Дата,
	|	ЗаказКлиента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказКлиентаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказКлиентаТовары.Номенклатура.Наименование КАК Номенклатура,
	|	ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдИзм,
	|	ЗаказКлиентаТовары.Количество КАК Количество,
	|	ЗаказКлиентаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатШапка = МассивРезультатов[0];
	РезультатТовары = МассивРезультатов[1];
	
	ДанныеШапки = РезультатШапка.Выбрать();
	ДанныеПоТоварам = РезультатТовары.Выбрать();
	
	Пока ДанныеШапки.Следующий() Цикл
		
		ВидКИ_ЭлПочта = Справочники.ВидыКонтактнойИнформации.EmailОрганизации;
		ВидКИ_Телефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
		
		ЭлПочта = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ДанныеШапки.Организация, ВидКИ_ЭлПочта);
		Телефон = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ДанныеШапки.Организация, ВидКИ_Телефон);
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.НазваниеОрганизации = ДанныеШапки.НаименованиеОрганизации;
		Область.Параметры.ЭлПочтаОрганизации = ЭлПочта;
		Область.Параметры.ТелефонОрганизации = Телефон;
		ТабДок.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабДок.Вывести(Область);                     
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеШапки.Ссылка);
		ДанныеПоТоварам.Сбросить();
		Пока ДанныеПоТоварам.НайтиСледующий(СтруктураПоиска) Цикл
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ДанныеПоТоварам);
			ТабДок.Вывести(Область);
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("ТекстГарантии");
		ТабДок.Вывести(Область);
		
	КонецЦикла;	
	
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабДок;
	
КонецФункции
```