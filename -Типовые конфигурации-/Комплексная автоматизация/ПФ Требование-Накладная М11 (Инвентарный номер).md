#БП 

Шаблон: [[Печатная форма]]
YouTube: [ВПФ Комплексная автоматизация на основе М-11](https://www.youtube.com/watch?v=z1WNMVluZpY)

---
### Задача

Для документа **ВнутреннееПотребление** требуется создать внешнюю печатную форму, на основе печатной формы *Требование-накладная М11* вместо колонки *Порядковый номер по складской картотеке* добавить колонку *Инвентарный номер*

До:
![[Pasted image 20241124135438.png]]

После:
![[Pasted image 20241125212653.png]]
### Решение

1. Копируем шаблон обработки [[Печатная форма]] и называем его *М11СИнвентарнымНомером*
2. Меняем тип реквизита *Документ* на *ДокументСсылка.ВнутреннееПотребление*
![[Pasted image 20241124154741.png]]

3. Создаем форму обработки, для удобства отладки. Для запоминания выбранного документа после закрытия формы, ставим флажок *Сохранение* напротив реквизита *Документ*
![[Pasted image 20241125212336.png]]

4. Модуль формы
```bsl

&НаКлиенте
Процедура Печать(Команда)
	
	Массив = ПечатьНаСервере();
	
	Для каждого ТабДок Из Массив Цикл
		ТабДок.Показать();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Документ);
	
	КоллекцияПечатныхФорм = УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм(ОбработкаОбъект.ИмяМакетаВПФ);
	ОбъектыПечати = Новый СписокЗначений;
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("ДоступнаПечатьПоКомплектно", Ложь);
	ПараметрыВывода.Вставить("ЗаголовокФормы", "");
	ПараметрыВывода.Вставить("КодЯзыка", "ru");
	ПараметрыВывода.Вставить("ПараметрыОтправки", Новый Структура); 
	
	ПараметрыВывода = Новый Структура;
	
	ОбработкаОбъект.Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	Массив = Новый Массив;
	
	Для каждого СтрокаТЧ Из КоллекцияПечатныхФорм Цикл
		Массив.Добавить(СтрокаТЧ.ТабличныйДокумент);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции
```

5. Находим обработку *ПечатьМ11* и копируем макет *ПФ_MXL_M11* в нашу обработку
![[Pasted image 20241124154621.png]]

6. В скопированном макете печатной формы, в области *Заголовка таблицы* => Переименовываем последнюю колонку на *Инвентарный номер* и в области *Строка* создаем параметр *ИнвентарныйНомер*
![[Pasted image 20241124155148.png]]

7. Открываем модуль менеджера *Нашей обработки* => Заполняем функцию *СведенияОВнешнейОбработке()*. Создаем и заполняем две глобальных переменных *ПредставлениеВПФ* и *ИмяМакетаВПФ*
```bsl

Перем ПредставлениеВПФ Экспорт;
Перем ИмяМакетаВПФ Экспорт;

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид					= "ПечатнаяФорма";
	ПараметрыРегистрации.Версия				= "1.0.0.1";
	ПараметрыРегистрации.Наименование		= ПредставлениеВПФ;
	ПараметрыРегистрации.БезопасныйРежим	= Истина;
	ПараметрыРегистрации.Информация			= "Внешняя печатная форма """ + ПредставлениеВПФ + """";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ВнутреннееПотребление");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление			= ПредставлениеВПФ;
	Команда.Идентификатор			= ИмяМакетаВПФ;
	Команда.Использование		 	= "ВызовСерверногоМетода";
	Команда.Модификатор			= "ПечатьMXL";
	Команда.ЗаменяемыеКоманды		= ""; // если нужно заменить существующую команду печати, укажите её идентификатор
	Команда.ПоказыватьОповещение = Истина;

	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

ПредставлениеВПФ = Метаданные().Представление();
ИмяМакетаВПФ = "ПФ_MXL_М11";
```

8. Открываем модуль менеджера обработки *ПечатьМ11*
![[Pasted image 20241124155357.png]]

9. Копируем код области *Печать* и переносим его в модуль объекта *нашей обработки*:
![[Pasted image 20241125192435.png]]

10. Вносим изменения в процедуру *Печать*:
```bsl
//++ GON 25.11.2024
// Убрал параметр "ПараметрыПечати"
//Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
//-- GON 25.11.2024

	//++ GON 25.11.2024
	//Создал структуру "ПараметрыПечати" для функции "СформироватьПечатнуюФормуМ11()"
	ПараметрыПечати = Новый Структура;
	//-- GON 25.11.2024

	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	//++ GON 25.11.2024
	//Заменил второй параметр "ИмяМакета" на переменную "ИмяМакетаВПФ"
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М11") Тогда
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакетаВПФ) Тогда
	//-- GON 25.11.2024
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			
			//++ GON 25.11.2024
			//Изменил имя и синоним макета
			//"М11",
			//НСтр("ru='Требование-накладная (М-11)'"),
			ИмяМакетаВПФ,
			ПредставлениеВПФ,
			//-- GON 25.11.2024
			
			СформироватьПечатнуюФормуМ11(
				СтруктураТипов,
				ОбъектыПечати,
				ПараметрыПечати));
	КонецЕсли;
КонецПроцедуры // Печать()
```

8. Открываем тело функции *СформироватьПечатнуюФормуМ11*, внутри нее нас интересует функция *ПолучитьДанныеДляПечатнойФормыМ11()*. Выделяем ее двойным кликом ЛКМ и нажимаем комбинацию клавиш ==Shift + Ctrl + F== для использования глобального поиска.
![[Pasted image 20241124155731.png]]

8. Поскольку наша печатная форма предназначена для документа **ВнутреннееПотреблениеЛокализация**, нас будет интересовать общий модуль *ВнутреннееПотреблениеЛокализация*, в котором находится искомая функция *ПолучитьДанныеДляПечатнойФормыМ11()* получающая данные для нашей печатной формы
![[Pasted image 20241125202238.png]]

9. Нам нужны обе функции: *ПолучитьДанныеДляПечатнойФормыМ11()* и *ПолучитьЗапросПолученияДанныхДляПечатнойФормыМ11()* - последняя, получает данные запросом в базу. Копируем обе функции и переносим их 
![[Pasted image 20241125202844.png]]

10. Вносим изменения в функцию *СформироватьПечатнуюФормуМ11()*, теперь *ДанныеДляПечати* мы получаем напрямую из функции *ПолучитьДанныеДляПечатнойФормыМ11()*, которую мы скопировали из общего модуля **ВнутреннееПотреблениеЛокализация**
```bsl
Функция СформироватьПечатнуюФормуМ11(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_М11";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		//++ GON 25.11.2024
		//
		//Вызываем скопированную функцию ПолучитьДанныеДляПечатнойФормыМ11() напрямую
		//
		//МенеджерОбъекта = ОбщегоНазначенияУТ.ПолучитьМодульЛокализации(СтруктураОбъектов.Ключ);
		//Если МенеджерОбъекта = Неопределено Тогда
		//	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		//КонецЕсли;
		//ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыМ11(
		//	ПараметрыПечати,
		//	СтруктураОбъектов.Значение);
		//
		ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыМ11(ПараметрыПечати, СтруктураОбъектов.Значение);
		//
		//-- GON 25.11.2024
		
		ЗаполнитьТабличныйДокументМ11(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФормуСчетНаОплату()
```

11. Вносим изменения в процедуру *ЗаполнитьТабличныйДокументМ11()*. Макет мы получаем из нашей обработки
![[Pasted image 20241125204834.png]]

12. Вносим изменения в функцию *ПолучитьЗапросПолученияДанныхДляПечатнойФормыМ11()*:
- Открываем текст запроса с помощью *Конструктора запроса*
![[Pasted image 20241125205343.png]]

- В первом пакете запроса временной таблицы *Товары* вытаскиваем *ИнвентарныйНомер*
![[Pasted image 20241125210035.png]]

- В восьмой пакет запроса, добавляем поле *ИнвентарныйНомер* из временной таблицы *Товары*
![[Pasted image 20241125210227.png]]

13. Модуль формы:
```bsl
&НаКлиенте
Процедура Печать(Команда)
	
	Массив = ПечатьНаСервере();
	
	Для каждого СтрокаТЧ Из Массив Цикл
		СтрокаТЧ.Показать();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Документ);
	
	КоллекцияПечатныхФорм = 
	УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм(ОбработкаОбъект.ИмяОбработки);
	ОбъектыПечати = Новый СписокЗначений;
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("ДоступнаПечатьПоКомплектно", Ложь);
	ПараметрыВывода.Вставить("ЗаголовокФормы", "");
	ПараметрыВывода.Вставить("КодЯзыка", "ru");
	ПараметрыВывода.Вставить("ПараметрыОтправки", Новый Структура); 
	
	ПараметрыВывода = Новый Структура;
	
	ОбработкаОбъект.Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	Массив = Новый Массив;
	
	Для каждого СтрокаТЧ Из КоллекцияПечатныхФорм Цикл
		Массив.Добавить(СтрокаТЧ.ТабличныйДокумент);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции
```

