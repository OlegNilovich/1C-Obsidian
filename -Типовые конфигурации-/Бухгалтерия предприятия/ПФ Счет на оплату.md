#БП #TODO 

Шаблон: [[Печатная форма]]
YouTube: https://www.youtube.com/watch?v=iL9ujYVsbxY&list=PLslT4jG57pLWji5ILkd-KOtk5sa48CNWI&index=7

---
### Задача

Требуется создать внешнюю печатную форму, на основе печатной формы *Требование-накладная М11* вместо колонки *Порядковый номер по складской картотеке* добавить колонку *Инвентарный номер*
![[Pasted image 20241124135438.png]]
### Решение

1. Копируем шаблон обработки [[Печатная форма]] и называем его *М11СИнвентарнымНомером*
2. Меняем тип реквизита *Документ* на *ДокументСсылка.ВнутреннееПотребление*
![[Pasted image 20241124154741.png]]

3. Находим обработку *ПечатьМ11* и копируем макет *ПФ_MXL_M11*
![[Pasted image 20241124154621.png]]

4. В макете печатной формы в области *Заголовка таблицы* => Переименовываем последнюю колонку на *Инвентарный номер* и в области *Строка* создаем параметр *ИнвентарныйНомер*
![[Pasted image 20241124155148.png]]

5. Открываем модуль менеджера обработки *ПечатьМ11*
![[Pasted image 20241124155357.png]]

6. Копируем код и переносим его в модуль объекта *нашей обработки*:
- Процедура *Печать()*
- Функция *СформироватьПечатнуюФормуМ11()*
- Процедура *ЗаполнитьТабличныйДокументМ11()*
![[Pasted image 20241124164309.png]]

7. Открываем функцию *СформироватьПечатнуюФормуМ11*, нас интересует функция *ПолучитьДанныеДляПечатнойФормыМ11()* - ищем ее место определения через глобальный поиск
![[Pasted image 20241124155731.png]]

8. Два раза кликаем по результату поиска и мы попадем в общий модуль *ВнутреннееПотреблениеЛокализация* в функцию *ПолучитьДанныеДляПечатнойФормыМ11()*
![[Pasted image 20241124160113.png]]

8. Ставим курсор на функцию *ПолучитьДанныеДляПечатнойФормыМ11()* => Нажимаем клавишу *F12*
![[Pasted image 20241124160339.png]]

9. Переходим к определению функции, которая формирует данные запросом для нашей печатной формы
![[Pasted image 20241124160505.png]]

10. Мы нашли нужные нам функции для формирования данных для печатной формы. Копируем обе функции в *Нашу обработку*
![[Pasted image 20241124164945.png]]

11. Модуль формы:
```bsl
&НаКлиенте
Процедура Печать(Команда)
	
	Массив = ПечатьНаСервере();
	
	Для каждого СтрокаТЧ Из Массив Цикл
		СтрокаТЧ.Показать();
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Документ);
	
	КоллекцияПечатныхФорм = 
	УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм(ОбработкаОбъект.ИмяОбработки);
	ОбъектыПечати = Новый СписокЗначений;
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("ДоступнаПечатьПоКомплектно", Ложь);
	ПараметрыВывода.Вставить("ЗаголовокФормы", "");
	ПараметрыВывода.Вставить("КодЯзыка", "ru");
	ПараметрыВывода.Вставить("ПараметрыОтправки", Новый Структура); 
	
	ПараметрыВывода = Новый Структура;
	
	ОбработкаОбъект.Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	Массив = Новый Массив;
	
	Для каждого СтрокаТЧ Из КоллекцияПечатныхФорм Цикл
		Массив.Добавить(СтрокаТЧ.ТабличныйДокумент);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции
```

12. Модуль объекта обработки:
```bsl

Перем ИмяОбработки Экспорт;
Перем СинонимОбработки Экспорт;

#Область ПрограммныйИнтерфейс

// 0. Сведения о внешней обработке
Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид					= "ПечатнаяФорма";
	ПараметрыРегистрации.Версия				= "1.0.0.1";
	ПараметрыРегистрации.Наименование		= СинонимОбработки;
	ПараметрыРегистрации.БезопасныйРежим	= Истина;
	ПараметрыРегистрации.Информация			= "Внешняя печатная форма """ + СинонимОбработки + """";
	
	ПараметрыРегистрации.Назначение.Добавить("Документ.ВнутреннееПотребление");
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление			= СинонимОбработки;
	Команда.Идентификатор			= ИмяОбработки;
	Команда.Использование		 	= "ВызовСерверногоМетода";
	Команда.Модификатор			= "ПечатьMXL";
	Команда.ЗаменяемыеКоманды		= ""; // если нужно заменить существующую команду печати, укажите её идентификатор
	Команда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации; 
	
КонецФункции

// 1. Печать
Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыПечати = Новый Структура;
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяОбработки) Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
									КоллекцияПечатныхФорм,
									ИмяОбработки,
									СинонимОбработки,
									СформироватьПечатнуюФормуМ11(
									СтруктураТипов,
									ОбъектыПечати,
									ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры // Печать()

#КонецОбласти

#Область ФункцииФормированияПечатныхФорм

// 2. Формирование печатной формы
Функция СформироватьПечатнуюФормуМ11(СтруктураТипов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_М11";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыМ11(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		ЗаполнитьТабличныйДокументМ11(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПечатнуюФормуСчетНаОплату()

// 3. Получение данных для печатной формы
Функция ПолучитьДанныеДляПечатнойФормыМ11(ПараметрыПечати, МассивДокументов)
	
	Запрос = ПолучитьЗапросПолученияДанныхДляПечатнойФормыМ11();
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Товары.Упаковка",
			"Товары.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Цены.Упаковка",
			"Товары.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"Товары.Упаковка",
			"Товары.Номенклатура"));
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПакетаЗапросов[РезультатПакетаЗапросов.ВГраница() - 1],
		РезультатПакетаЗапросов[РезультатПакетаЗапросов.ВГраница()]);
	
КонецФункции

// 4. Запрос для получения данных печатной формы
Функция ПолучитьЗапросПолученияДанныхДляПечатнойФормыМ11()

	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();
	Если ИспользуетсяЦенообразование25 Тогда
		ДатаПереходаНаЦенообразование25 = ЦенообразованиеВызовСервера.ДатаПереходаНаЦенообразованиеВерсии25();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаПереходаНаЦенообразование25", ДатаПереходаНаЦенообразование25);
	Запрос.УстановитьПараметр("ИспользуетсяЦенообразование25", ИспользуетсяЦенообразование25);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|			ИНАЧЕ Товары.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоСерии = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)
	|			ИНАЧЕ Товары.Серия.СерияНоменклатурыДляЦенообразования
	|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК СерияЦО,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоУпаковке = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|		ИНАЧЕ Товары.Упаковка
	|	КОНЕЦ КАК УпаковкаЦО,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Товары.Ссылка.ВидЦены КАК ВидЦены,
	|	Товары.Ссылка.ВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Товары.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ВнутреннееПотребление.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = Товары.Номенклатура.ВидНоменклатуры)
	|			И (&ИспользуетсяЦенообразование25)
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|	И НЕ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.Характеристика КАК Характеристика,
	|	NULL КАК ХарактеристикаЦО,
	|	NULL КАК СерияЦО,
	|	NULL КАК УпаковкаЦО,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыЦенНоменклатуры
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И Товары.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|			И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура,
	|	ЦеныНоменклатуры.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены КАК ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	NULL КАК Поле1,
	|	ЦеныНоменклатуры.ХарактеристикаЦО КАК ХарактеристикаЦО,
	|	ЦеныНоменклатуры.СерияЦО КАК СерияЦО,
	|	ЦеныНоменклатуры.УпаковкаЦО КАК УпаковкаЦО,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыЦенНоменклатуры25
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
	|		ПО Товары.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И Товары.ХарактеристикаЦО = ЦеныНоменклатуры.ХарактеристикаЦО
	|			И Товары.СерияЦО = ЦеныНоменклатуры.СерияЦО
	|			И Товары.УпаковкаЦО = ЦеныНоменклатуры.УпаковкаЦО
	|			И Товары.ДатаПолученияЦены >= ЦеныНоменклатуры.Период
	|			И Товары.ВалютаЦены = ЦеныНоменклатуры.Валюта
	|ГДЕ
	|	Товары.ДатаПолученияЦены >= &ДатаПереходаНаЦенообразование25
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.НомерСтроки,
	|	ЦеныНоменклатуры.ВидЦены,
	|	ЦеныНоменклатуры.Номенклатура,
	|	ЦеныНоменклатуры.ХарактеристикаЦО,
	|	ЦеныНоменклатуры.СерияЦО,
	|	ЦеныНоменклатуры.УпаковкаЦО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.ВалютаРегламентированногоУчета КАК БазоваяВалюта,
	|	МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|	КурсыВалют.Валюта КАК Валюта
	|ПОМЕСТИТЬ ПериодыКурсовВалютПоДокументам
	|ИЗ
	|	Документ.ВнутреннееПотребление КАК Документы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО Документы.ВидЦены.ВалютаЦены = КурсыВалют.Валюта
	|			И Документы.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|			И Документы.Дата >= КурсыВалют.Период
	|ГДЕ
	|	Документы.Ссылка В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Документы.Ссылка,
	|	Документы.Организация.ВалютаРегламентированногоУчета,
	|	КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка КАК Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатуры.Цена КАК Цена,
	|	ЦеныНоменклатуры.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ Цены
	|ИЗ
	|	ПериодыЦенНоменклатуры КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|			И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ПериодыЦенНоменклатуры.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И (НЕ &ИспользуетсяЦенообразование25)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПериодыЦенНоменклатуры.Ссылка,
	|	ПериодыЦенНоменклатуры.НомерСтроки,
	|	ЦеныНоменклатуры.Цена,
	|	ЦеныНоменклатуры.Упаковка
	|ИЗ
	|	ПериодыЦенНоменклатуры25 КАК ПериодыЦенНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
	|		ПО ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|			И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ПериодыЦенНоменклатуры.ХарактеристикаЦО = ЦеныНоменклатуры.ХарактеристикаЦО
	|			И ПериодыЦенНоменклатуры.СерияЦО = ЦеныНоменклатуры.СерияЦО
	|			И ПериодыЦенНоменклатуры.УпаковкаЦО = ЦеныНоменклатуры.УпаковкаЦО
	|			И (&ИспользуетсяЦенообразование25)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка КАК Ссылка,
	|	КурсыВалют.Валюта КАК Валюта,
	|	КурсыВалют.КурсЧислитель КАК КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	ПериодыКурсовВалютПоДокументам КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|			И ПериодыКурсовВалютПоДокументам.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(Документы.ИсправляемыйДокумент.Номер, Документы.Номер) КАК Номер,
	|	ЕСТЬNULL(Документы.ИсправляемыйДокумент.Дата, Документы.Дата) КАК ДатаСоставления,
	|	ЕСТЬNULL(Документы.ИсправляемыйДокумент.Дата, Документы.Дата) КАК ДатаДокумента,
	|	Документы.Организация КАК Организация,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Подразделение КАК Подразделение,
	|	Документы.ВидЦены КАК УчетныйВидЦены,
	|	Документы.ВидЦены.ВалютаЦены КАК УчетныйВидЦеныВалютаЦены
	|ИЗ
	|	Документ.ВнутреннееПотребление КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Ссылка.Склад КАК Склад,
	|	ВЫРАЗИТЬ(Товары.Ссылка.Склад КАК Справочник.Склады).ТекущийОтветственный КАК КладовщикОтправитель,
	|	ВЫРАЗИТЬ(Товары.Ссылка.Склад КАК Справочник.Склады).ТекущаяДолжностьОтветственного КАК ДолжностьКладовщикаОтправителя,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	Товары.Номенклатура.Код КАК НоменклатурныйНомерКод,
	|	Товары.Номенклатура.Артикул КАК НоменклатурныйНомерАртикул,
	|	Товары.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Товары.Упаковка.Наименование
	|	КОНЕЦ КАК Упаковка,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	Товары.КоличествоУпаковок КАК Количество,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31, 2))) * Товары.КоличествоУпаковок КАК Сумма,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(Цены.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31, 2)) КАК Цена,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО Товары.Ссылка = КурсыВалют.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Цены КАК Цены
	|		ПО Товары.Ссылка = Цены.Ссылка
	|			И Товары.НомерСтроки = Цены.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(КладовщикОтправитель),
	|	МАКСИМУМ(ДолжностьКладовщикаОтправителя)
	|ПО
	|	Ссылка,
	|	Склад";
	
	Возврат Запрос;
	
КонецФункции

// 5. Заполнение табличного документа
Процедура ЗаполнитьТабличныйДокументМ11(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)

	ШаблонОшибкиТовары = НСтр("ru = 'В документе %1 отсутствуют товары. Печать М-11 не требуется'");

	ТоварКод = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(ТоварКод) Тогда
		ТоварКод = "Код";
	КонецЕсли;

	//Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьМ11.ПФ_MXL_М11");
	Макет = ЭтотОбъект.ПолучитьМакет("ПФ_MXL_М11");

	ВыводитьНДИГК = Ложь;
	//++ НЕ УТ
	ВыводитьНДИГК = Константы.ВыводитьДопКолонкиНаправлениеДеятельностиИГК.Получить()
		И ДанныеДляПечати.РезультатПоТабличнойЧасти.Колонки.Найти("НДИГКПолучателя") <> Неопределено;
	//-- НЕ УТ

	ПервыйДокумент = Истина;
	ВыборкаДокументы = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ВыборкаПоТабличнымЧастям = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СтруктураОтбора = Новый Структура("Ссылка");

	Пока ВыборкаДокументы.Следующий() Цикл

		СтруктураОтбора.Ссылка = ВыборкаДокументы.Ссылка;
		ВыборкаПоТабличнымЧастям.Сбросить();
		Если Не ВыборкаПоТабличнымЧастям.НайтиСледующий(СтруктураОтбора) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибкиТовары, ВыборкаДокументы.Ссылка),
				ВыборкаДокументы.Ссылка);
			Продолжить;
		КонецЕсли;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ВыборкаПоСкладам = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПоСкладам.Следующий() Цикл

			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;

			ПервыйДокумент = Ложь;
			
			// Вывод шапки.
			ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьШапка,
				ВыборкаДокументы.Ссылка);
			ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'ТРЕБОВАНИЕ-НАКЛАДНАЯ №'")
				+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументы.Номер, Ложь, Истина);
			ОбластьШапка.Параметры.Заполнить(ВыборкаДокументы);
			ОбластьШапка.Параметры.Заполнить(ВыборкаПоСкладам);

			СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокументы.Организация,
				ВыборкаДокументы.ДатаДокумента);
			ОбластьШапка.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
				СведенияОбОрганизации);
			ОбластьШапка.Параметры.КодОКПО = СведенияОбОрганизации.КодПоОКПО;
			ТабличныйДокумент.Вывести(ОбластьШапка);

			Если ВыводитьНДИГК Тогда

				ВыборкаПоНДИГКОтправителя = ВыборкаПоСкладам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				НомерСтраницы   = 1;
				
				ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
				ТабличныйДокумент.Вывести(ОбластьПодпись);

				Пока ВыборкаПоНДИГКОтправителя.Следующий() Цикл

					ВыборкаПоНДИГКПолучателя = ВыборкаПоНДИГКОтправителя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

					Пока ВыборкаПоНДИГКПолучателя.Следующий() Цикл	
						
						// Создаем массив для проверки вывода
						МассивВыводимыхОбластей = Новый Массив;

						ШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицыНДИГК");
						ШапкаТаблицы.Параметры.Заполнить(ВыборкаДокументы);
						ШапкаТаблицы.Параметры.Заполнить(ВыборкаПоНДИГКПолучателя);
						ШапкаТаблицы.Параметры.ПредставлениеПодразделения = ВыборкаДокументы.Подразделение;

						ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
						
						// Области многострочной части документа
						ОбластьМакета	= Макет.ПолучитьОбласть("Строка");
						ОбластьПодвал	= Макет.ПолучитьОбласть("Подвал");

						МассивВыводимыхОбластей.Добавить(ОбластьПодпись);
						МассивВыводимыхОбластей.Добавить(ШапкаТаблицы);
						МассивВыводимыхОбластей.Добавить(ЗаголовокТаблицы);
						МассивВыводимыхОбластей.Добавить(ОбластьМакета);
						МассивВыводимыхОбластей.Добавить(ОбластьПодвал);

						Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
							НомерСтраницы = НомерСтраницы + 1;
							ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
							ШапкаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтраницыШапка", "Страница "
								+ НомерСтраницы));
							ТабличныйДокумент.Вывести(ШапкаТаблицы);
							ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
						Иначе
							ТабличныйДокумент.Вывести(ШапкаТаблицы);
							ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
						КонецЕсли;

						НомерСтроки = 0;

						ВыборкаПоСтрокам = ВыборкаПоНДИГКПолучателя.Выбрать();
						КоличествоСтрок = ВыборкаПоСтрокам.Количество();
						Пока ВыборкаПоСтрокам.Следующий() Цикл

							Область = Макет.ПолучитьОбласть("Строка");
							Область.Параметры.Заполнить(ВыборкаПоСтрокам);

							НомерСтроки = НомерСтроки + 1;

							МассивВыводимыхОбластей.Очистить();
							МассивВыводимыхОбластей.Добавить(Область);

							Если НомерСтроки = КоличествоСтрок Тогда
								МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
							КонецЕсли;

							Если ТоварКод = "Артикул" Тогда
								Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерАртикул;
							Иначе
								Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерКод;
							КонецЕсли;

							ДопПараметрыПредставлениеНоменклатуры = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
							ДопПараметрыПредставлениеНоменклатуры.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();

							Область.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
							СокрЛП(ВыборкаПоСтрокам.НоменклатураНаименование), СокрЛП(ВыборкаПоСтрокам.Характеристика), , ,
								ДопПараметрыПредставлениеНоменклатуры);

							Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
								НомерСтраницы = НомерСтраницы + 1;
								ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
								СтруктураПараметров = Новый Структура;
								СтруктураПараметров.Вставить("НомерСтраницы", "Страница " + НомерСтраницы);
								ЗаголовокТаблицы.Параметры.Заполнить(СтруктураПараметров);
								ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
							КонецЕсли;

							ТабличныйДокумент.Вывести(Область);

						КонецЦикла;

					КонецЦикла;

				КонецЦикла;
				
				// Вывод подвала.
				ОбластьПодвал.Параметры.ДолжностьОтправителя = ВыборкаПоСкладам.ДолжностьКладовщикаОтправителя;
				ОбластьПодвал.Параметры.ФИООтправителя = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(
					ВыборкаПоСкладам.КладовщикОтправитель, ВыборкаДокументы.ДатаДокумента);
				ТабличныйДокумент.Вывести(ОбластьПодвал);

			Иначе

				НомерСтраницы   = 1;
				НомерСтроки = 0;
				
				// Создаем массив для проверки вывода
				МассивВыводимыхОбластей = Новый Массив;

				ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");

				ШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
				ШапкаТаблицы.Параметры.Заполнить(ВыборкаДокументы);
				ШапкаТаблицы.Параметры.Заполнить(ВыборкаПоСкладам);
				ШапкаТаблицы.Параметры.ПредставлениеПодразделения = ВыборкаДокументы.Подразделение;
				ТабличныйДокумент.Вывести(ШапкаТаблицы);

				ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
				ТабличныйДокумент.Вывести(ОбластьПодпись);

				// Выводим заголовок таблицы
				ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
				ТабличныйДокумент.Вывести(ЗаголовокТаблицы);

				ВыборкаПоСтрокам = ВыборкаПоСкладам.Выбрать();
				КоличествоСтрок = ВыборкаПоСтрокам.Количество();
				Пока ВыборкаПоСтрокам.Следующий() Цикл

					Область = Макет.ПолучитьОбласть("Строка");
					Область.Параметры.Заполнить(ВыборкаПоСтрокам);

					НомерСтроки = НомерСтроки + 1;

					МассивВыводимыхОбластей.Очистить();
					МассивВыводимыхОбластей.Добавить(Область);

					Если НомерСтроки = КоличествоСтрок Тогда
						МассивВыводимыхОбластей.Добавить(ОбластьПодвал);
					КонецЕсли;

					Если ТоварКод = "Артикул" Тогда
						Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерАртикул;
					Иначе
						Область.Параметры.НоменклатурныйНомер = ВыборкаПоСтрокам.НоменклатурныйНомерКод;
					КонецЕсли;

					ДопПараметрыПредставлениеНоменклатуры = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
					ДопПараметрыПредставлениеНоменклатуры.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();

					Область.Параметры.МатериалНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					СокрЛП(ВыборкаПоСтрокам.НоменклатураНаименование), СокрЛП(ВыборкаПоСтрокам.Характеристика), , ,
						ДопПараметрыПредставлениеНоменклатуры);

					Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
						НомерСтраницы = НомерСтраницы + 1;
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						СтруктураПараметров = Новый Структура;
						СтруктураПараметров.Вставить("НомерСтраницы", "Страница " + НомерСтраницы);
						ЗаголовокТаблицы.Параметры.Заполнить(СтруктураПараметров);
						ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
					КонецЕсли;

					ТабличныйДокумент.Вывести(Область);

				КонецЦикла;
				
				// Вывод подвала.
				ОбластьПодвал.Параметры.ДолжностьОтправителя = ВыборкаПоСкладам.ДолжностьКладовщикаОтправителя;
				ОбластьПодвал.Параметры.ФИООтправителя = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(
					ВыборкаПоСкладам.КладовщикОтправитель, ВыборкаДокументы.ДатаДокумента);
				ТабличныйДокумент.Вывести(ОбластьПодвал);

			КонецЕсли;

		КонецЦикла;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ВыборкаДокументы.Ссылка);

	КонецЦикла;

КонецПроцедуры


#КонецОбласти

ИмяОбработки			= ЭтотОбъект.Метаданные().Имя; 
СинонимОбработки	= ЭтотОбъект.Метаданные().Синоним; 
СинонимОбработки	= ?(ЗначениеЗаполнено(СинонимОбработки), СинонимОбработки, ИмяОбработки);
```