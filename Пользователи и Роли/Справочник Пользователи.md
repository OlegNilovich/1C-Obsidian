### Задача:
Требуется создать справочник "Пользователи", который позволит создавать пользователей и назначать им права в режиме 1С:Предприятие
### Решение:
1. Создаем справочник **Пользователи**.
2. Создаем реквизиты:
- *ПолноеИмя*						(Строка, 100)		
- *Пароль*							(Строка, 20)
- *ПоказыватьВСпискеВыбора*		(Булево). Свойства => Представление => Значение заполнения => Истина
- *ИдентификаторПользователяИБ*	(УникальныйИдентификатор)
- *Сотрудник*						(СправочникСсылка.Сотрудники)
3. Создаем табличную часть *СписокРолей*
4. Создаем реквизиты табличной части:
- *Установлена*	(Булево)
- *Имя*			(Строка, 100)
- *Синоним*		(Строка, 100)

1. Создаем форму элемента справочника
2. Элементы => Список ролей => Командная панель => Свойства => Автозаполнение => *Отключить*
3. Для элемента *СписокРолейУстановлена* создаем событие *ПриИзменении* на клиенте
4. Создаем событие формы *ПриСозданииНаСервере*
5. Создаем событие формы *ПередЗаписьюНаСервере*
6. Модуль формы элемента справочника:
```bsl

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда // Если ссылка пустая - значит пользователь новый
		
		// Заполняем список ролей всеми доступными ролями конфигурации
		Для каждого ТекущаяРоль Из Метаданные.Роли Цикл
			СтрокаРоли = Объект.СписокРолей.Добавить();
			СтрокаРоли.Установлена = Ложь;
			СтрокаРоли.Имя = ТекущаяРоль.Имя;
			СтрокаРоли.Синоним = ТекущаяРоль.Синоним;
		КонецЦикла;
		
	Иначе //Ищем пользователя информационной базы по уникальному идентификатору
		
		ID = Объект.ИдентификаторПользователяИБ;
		Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ID);
		
		//Пользователь найден. Заполняем список ролей и отмечаем роли, которые пренадлежат пользователю
		Если Пользователь <> Неопределено Тогда
			Объект.СписокРолей.Очистить();
			Для каждого Роль Из Метаданные.Роли Цикл
				СтрокаРоли = Объект.СписокРолей.Добавить();
				СтрокаРоли.Установлена = Пользователь.Роли.Содержит(Роль);
				СтрокаРоли.Имя = Роль.Имя;
				СтрокаРоли.Синоним = Роль.Синоним;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолейУстановленаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

```

##### Модуль объекта справочника:
```bsl

Процедура ПередУдалением(Отказ)

	Пользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ);
	Если Пользователь <> Неопределено Тогда
		Пользователь.Удалить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоНовый() Тогда
		
		НайденныйПользователь = ПользователиИнформационнойБазы.НайтиПоИмени(Наименование);
		Если НайденныйПользователь = Неопределено Тогда
			
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
			ОбновитьДанныеПользователяИБ(ПользовательИБ);
			ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
			
		Иначе
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "В списке уже есть пользователь с таким именем!";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;           
		
	Иначе  
		
		ПользовательИБ = 
		ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ);
		Если ПользовательИБ <> Неопределено Тогда
			ОбновитьДанныеПользователяИБ(ПользовательИБ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбновитьДанныеПользователяИБ(ПользовательИБ)
	
	ПользовательИБ.Имя = Наименование;
	ПользовательИБ.ПолноеИмя = ПолноеИмя; 
	ПользовательИБ.Пароль = Пароль;
	ПользовательИБ.ПоказыватьВСпискеВыбора = ПоказыватьВСпискеВыбора;
	
	ПользовательИБ.Роли.Очистить();
	
	Если СписокРолей.Количество() > 0 Тогда
		Для каждого Роль Из СписокРолей Цикл      
			Если Роль.Установлена Тогда
				ПользовательИБ.Роли.Добавить(Метаданные.Роли[Роль.Имя]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПользовательИБ.Записать();       
	
КонецПроцедуры

```

