#ЗАПРОСЫ 
##### Движение по регистру **ОстаткиНоменклатуры**
![[Pasted image 20240925082606.png]]
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК			Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК	Количество
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Номенклатура.Представление КАК			НоменклатураПредставление,
	|	Документ.Номенклатура КАК									Номенклатура,
	|	Документ.Количество КАК										Количество,
	|	Регистр.Партия КАК												Партия,
	|	ЕСТЬNULL(Регистр.КоличествоОстаток, 0) КАК		КоличествоОстаток,
	|	ЕСТЬNULL(Регистр.СтоимостьОстаток, 0) КАК			СтоимостьОстаток
	|ИЗ
	|	ВТ_Товары КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТ_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ВТ_Товары КАК ВТ_Товары)) КАК Регистр
	|		ПО Документ.Номенклатура = Регистр.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистр.Партия.МоментВремени ВОЗР
	|ИТОГИ
	|	МАКСИМУМ(КоличествоСписания),
	|	СУММА(КоличествоОстаток),
	|	СУММА(СтоимостьОстаток)
	|ПО
	|	Номенклатура";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписания;
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МоментВремени ВОЗР", "МоментВремени УБЫВ");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Текст = "Не хватает товара %1 в количестве %2 ед.";
			Сообщить(СтрШаблон(Текст, ВыборкаНоменклатура.НоменклатураПредставление,
			ВыборкаНоменклатура.КоличествоСписания - ВыборкаНоменклатура.КоличествоОстаток));
			Отказ = Истина;
			Продолжить;
		КонецЕсли;
		
		Если НЕ Отказ Тогда                         
			
			ОсталосьСписать = ВыборкаНоменклатура.Количество;
			
			Выборка = ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
				
				Списать = МИН(ОсталосьСписать, Выборка.КоличествоОстаток);
				Стоимость = Списать / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток;
				
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период			= Дата;
				Движение.Партия			= Выборка.Партия;
				Движение.Номенклатура	= Выборка.Номенклатура;
				Движение.Количество		= Списать;
				Движение.Стоимость		= Стоимость;
				
				ОсталосьСписать = ОсталосьСписать - Списать;
				
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры
```

---
##### Движение по регистрам: *ОстаткиНоменклатуры* и **ПродажиТоваров**
![[Pasted image 20240925082433.png]]

Если **ВидНоменклатуры** = *Товар*, тогда формируем движения по регистрам *Остатки* и *Продажи*
Если **ВидНоменклатуры** = *Услуга*, тогда формируем движение только по регистру *Продажи*

```bsl
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ПродажиТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК								Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК	ВидНоменклатуры,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК						Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК								Сумма
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Номенклатура.Представление КАК			НоменклатураПредставление,
	|	Документ.ВидНоменклатуры КАК							ВидНоменклатуры,
	|	Документ.Номенклатура КАК									Номенклатура,
	|	Документ.Количество КАК										Количество,
	|	Документ.Сумма КАК												Выручка,
	|	Регистр.Партия КАК												Партия,
	|	ЕСТЬNULL(Регистр.КоличествоОстаток, 0) КАК		КоличествоОстаток,
	|	ЕСТЬNULL(Регистр.СтоимостьОстаток, 0) КАК			СтоимостьОстаток
	|ИЗ
	|	ВТ_Товары КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТ_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ВТ_Товары КАК ВТ_Товары)) КАК Регистр
	|		ПО Документ.Номенклатура = Регистр.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистр.Партия.МоментВремени
	|ИТОГИ
	|	МАКСИМУМ(ВидНоменклатуры),
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(Выручка),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписания;
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МоментВремени ВОЗР", "МоментВремени УБЫВ");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовар.Следующий() Цикл
		
		Если ВыборкаТовар.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			
			Если ВыборкаТовар.Количество > ВыборкаТовар.КоличествоОстаток Тогда
				Текст = "Не хватает товара %1 в количестве %2 ед.";
				Сообщить(СтрШаблон(Текст, ВыборкаТовар.НоменклатураПредставление,
				ВыборкаТовар.Количество - ВыборкаТовар.КоличествоОстаток));
				Отказ = Истина;
				Продолжить;
			КонецЕсли;		
			
			Если НЕ Отказ Тогда                         
				
				ОсталосьСписать  = ВыборкаТовар.Количество;
				СтоимостьПродаж = 0;
				
				Выборка = ВыборкаТовар.Выбрать();
				Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
					
					Списать = МИН(ОсталосьСписать, Выборка.КоличествоОстаток);
					СтоимостьПартия = Списать / Выборка.КоличествоОстаток * Выборка.СтоимостьОстаток;
					СтоимостьПродаж = СтоимостьПродаж + СтоимостьПартия;
					
					// Регистр ОстаткиНоменклатуры Расход
					Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
					Движение.Период				= Дата;
					Движение.Партия				= Выборка.Партия;
					Движение.Номенклатура	= Выборка.Номенклатура;
					Движение.Количество		= Списать;
					Движение.Стоимость			= СтоимостьПартия;
					
					ОсталосьСписать = ОсталосьСписать - Списать;
					
				КонецЦикла;
				
			КонецЕсли;	
			
		КонецЕсли;
		
		// Регистр ПродажиТоваров 
		Движение = Движения.ПродажиТоваров.Добавить();
		Движение.Период			= Дата; 
		Движение.Номенклатура	= ВыборкаТовар.Номенклатура;
		Движение.Количество		= ВыборкаТовар.Количество;
		Движение.Стоимость		= СтоимостьПродаж;
		Движение.Выручка			= ВыборкаТовар.Выручка;	
		
	КонецЦикла;
	
КонецПроцедуры
```