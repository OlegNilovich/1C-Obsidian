#ЗАПРОСЫ 

```bsl
// 1. Очистка старых движений регистра, чтобы они не учитывались при учете остатков
Движения.ТоварыНаСкладах.Записать();

// 2. Получение остатков из регистра (на момент времени документа)
Запрос = Новый Запрос;
Запрос.Текст = 
"ВЫБРАТЬ
|	РасходнаяНакладнаяТовары.Номенклатура КАК				Номенклатура,
|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК	Количество
|ПОМЕСТИТЬ ВТ_ТоварыДокумента
|ИЗ
|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
|ГДЕ
|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
|
|СГРУППИРОВАТЬ ПО
|	РасходнаяНакладнаяТовары.Номенклатура
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ТоварыНаСкладахОстатки.Номенклатура КАК			Номенклатура,
|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК	КоличествоОстаток
|ПОМЕСТИТЬ ВТ_Остатки
|ИЗ
|	РегистрНакопления.ТоварыНаСкладах.Остатки(
|			&МоментВремени,
|			(Склад, Организация, Номенклатура) В
|				(ВЫБРАТЬ
|					&Склад,
|					&Организация,
|					ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура
|				ИЗ
|					ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ТоварыНаСкладахОстатки
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_ТоварыДокумента.Номенклатура. Представление КАК		НоменклатураПредставление,
|	ВТ_ТоварыДокумента.Номенклатура КАК								Номенклатура,
|	ВТ_ТоварыДокумента.Количество КАК									КоличествоДокумент,
|	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК					КоличествоОстаток
|ИЗ
|	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента
|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
|		ПО ВТ_ТоварыДокумента.Номенклатура = ВТ_Остатки.Номенклатура";
Запрос.УстановитьПараметр("Организация", Организация);
Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
Запрос.УстановитьПараметр("Склад", Склад);
Запрос.УстановитьПараметр("Ссылка", Ссылка);

// 3. Установка явной управляемой блокировки
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
ЭлементБлокировки.ИсточникДанных = Товары;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать();

// 4. Проверка остатков
Выборка = Запрос.Выполнить().Выбрать();
Пока Выборка.Следующий() Цикл
	
	Если Выборка.КоличествоДокумент > Выборка.КоличествоОстаток Тогда
		Отказ = Истина;
		Текст = "Недостаточно товара %1 в количестве %2 шт";
		Дефицит = Выборка.КоличествоДокумент - Выборка.КоличествоОстаток;
		Сообщить(СтрШаблон(Текст, Выборка.НоменклатураПредставление, Дефицит));	
	КонецЕсли;
	
КонецЦикла;

// 5. Формирование движений, только если контроль остатков пройден
Если НЕ  Тогда

	Движения.ТоварыНаСкладах.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
		Движение.Период			= Дата;
		Движение.Склад				= Склад;
		Движение.Организация	= Организация;
		Движение.Номенклатура	= ТекСтрокаТовары.Номенклатура;
		Движение.Количество		= ТекСтрокаТовары.Количество;
	КонецЦикла;
	
КонецЕсли;
```