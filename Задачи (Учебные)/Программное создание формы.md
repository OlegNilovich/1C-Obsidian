#ФОРМЫ 

Полезная статья: https://infostart.ru/1c/articles/1118319/#_Toc18589857
### Задача:
Требуется программным путем создать реквизиты и элементы для формы элемента справочника "Контрагенты". Окончательный вид формы:
![[Pasted image 20240822023003.png]]

### Решение:
Создаем обработку и две формы обработки:
![[Pasted image 20240822023150.png]]
##### Форма списка контрагентов
1. Открываем редактор формы "ФормаСпискаКонтрагентов"
2. Создаем реквизит формы "Список", с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Справочники => Контрагенты
![[Pasted image 20240822023408.png]]
4. Для элемента формы "Список", создаем событие "Выбор"
![[Pasted image 20240822023710.png]]
5. При двойном клике на элемент списка, будет открываться форма выбранного контрагента
```bsl
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ", ВыбраннаяСтрока);
	ОткрытьФорму("Обработка.ПрограммноеСозданиеФормы.Форма.ФормаКонтрагента", ПараметрыФормы);
	
КонецПроцедуры
```

##### Форма контрагента
1. Открываем редактор формы "ФормаСпискаКонтрагентов"
2. Выбираем тип основного реквизита: Справочник.Объект.Контрагенты
![[Pasted image 20240822024812.png]]
3. Создаем событие формы "ПриСозданииНаСервере"
![[Pasted image 20240822024631.png]]
4. Модуль формы:
```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Область Наименование_НаименованиеПолное_ВидКонтрагента
	Если Элементы.Найти("Наименование") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Наименование", Тип("ПолеФормы"));
		НовыйЭлемент.ПутьКДанным = "Объект.Наименование";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если Элементы.Найти("НаименованиеПолное") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("НаименованиеПолное", Тип("ПолеФормы"));
		НовыйЭлемент.ПутьКДанным = "Объект.НаименованиеПолное"; 
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если Элементы.Найти("ВидКонтрагента") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ВидКонтрагента", Тип("ПолеФормы"));
		НовыйЭлемент.ПутьКДанным = "Объект.ВидКонтрагента"; 
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеПереключателя;
		НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыКонтрагентов.ЮридическоеЛицо);
		НовыйЭлемент.СписокВыбора.Добавить(Перечисления.ВидыКонтрагентов.ФизическоеЛицо);
		НовыйЭлемент.ВидПереключателя = ВидПереключателя.Тумблер;
	КонецЕсли;
	#КонецОбласти
	
//==================================================================
	
	#Область ИНН_Категория_ЗаполнитьДанныеКонтрагента
	Если Элементы.Найти("Группа_ИНН_Категория") = Неопределено Тогда
		Группа_ИНН_Категория = ЭтаФорма.Элементы.Добавить("Группа_ИНН_Категория", Тип("ГруппаФормы"), 
		ЭтаФорма);
		Группа_ИНН_Категория.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа_ИНН_Категория.Отображение = ОтображениеОбычнойГруппы.Нет;
		Группа_ИНН_Категория.ОтображатьЗаголовок = ЛОЖЬ; 
		Группа_ИНН_Категория.Группировка = 
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	Если Элементы.Найти("ИНН") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ИНН", Тип("ПолеФормы"), Группа_ИНН_Категория);
		НовыйЭлемент.ПутьКДанным = "Объект.ИНН"; 
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если Элементы.Найти("ЗаполнитьДанныеКонтрагента") = Неопределено Тогда	
		НоваяКоманда = Команды.Добавить("ЗаполнитьДанныеКонтрагента");
		НоваяКоманда.Действие = "ЗаполнитьДанныеКонтрагента";
		НоваяКнопка = Элементы.Добавить("ЗаполнитьДанныеКонтрагента", Тип("КнопкаФормы"), Группа_ИНН_Категория);
		НоваяКнопка.ИмяКоманды  = "ЗаполнитьДанныеКонтрагента";
		НоваяКнопка.Картинка	  = БиблиотекаКартинок.ОформлениеФлажок;
		НоваяКнопка.Отображение = ОтображениеКнопки.Картинка;
	КонецЕсли;	
	
	Если Элементы.Найти("Категория") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Категория", Тип("ПолеФормы"), Группа_ИНН_Категория);
		НовыйЭлемент.ПутьКДанным = "Объект.Категория";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеПереключателя;
		НовыйЭлемент.ВидПереключателя = ВидПереключателя.Тумблер;
		НовыйЭлемент.СписокВыбора.Добавить(Перечисления.КатегорииКонтрагентов.Важный);
		НовыйЭлемент.СписокВыбора.Добавить(Перечисления.КатегорииКонтрагентов.Обычный);
		НовыйЭлемент.СписокВыбора.Добавить(Перечисления.КатегорииКонтрагентов.Ключевой);
	КонецЕсли;
	#КонецОбласти
	
//==================================================================
	
	#Область СтранаРегистрации_КПП
	Если Элементы.Найти("Группа_Страна_КПП") = Неопределено Тогда
		Группа_Страна_КПП = ЭтаФорма.Элементы.Добавить("Группа_Страна_КПП", Тип("ГруппаФормы"), 
		ЭтаФорма);
		Группа_Страна_КПП.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа_Страна_КПП.Отображение = ОтображениеОбычнойГруппы.Нет;
		Группа_Страна_КПП.ОтображатьЗаголовок = ЛОЖЬ; 
		Группа_Страна_КПП.Группировка = 
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	Если Элементы.Найти("СтранаРегистрации") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("СтранаРегистрации", Тип("ПолеФормы"), Группа_Страна_КПП);
		НовыйЭлемент.ПутьКДанным = "Объект.СтранаРегистрации"; 
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если Элементы.Найти("КПП") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("КПП", Тип("ПолеФормы"), Группа_Страна_КПП);
		НовыйЭлемент.ПутьКДанным = "Объект.КПП"; 
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	#КонецОбласти
	
//===================================================================
	
	#Область Договор
	// Создаем реквизит формы "ОсновнойДоговор"
	РеквизитОсновнойДоговор = Новый РеквизитФормы("ОсновнойДоговор", Новый 
	ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(РеквизитОсновнойДоговор);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	Если Элементы.Найти("ОсновнойДоговор") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ОсновнойДоговор", Тип("ПолеФормы"));
		НовыйЭлемент.ПутьКДанным = "ОсновнойДоговор";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.ЦветТекста = Новый Цвет(0, 0, 255);
	КонецЕсли;
	
	// Заполняем реквизит "ОсновнойДоговор", если таковой имеется у контрагента
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.ОсновнойДоговор.Видимость = Ложь;
	Иначе
		ЭтотОбъект.ОсновнойДоговор = 
		Справочники.ДоговорыКонтрагентов.НайтиПоРеквизиту("Основной", Истина,, Объект.Ссылка);
	КонецЕсли;
	#КонецОбласти
	
//===================================================================
	
	#Область Покупатель_Поставщик_Прочее
	Если Элементы.Найти("Покупатель_Поставщик_Прочее") = Неопределено Тогда
		Покупатель_Поставщик_Прочее = ЭтаФорма.Элементы.Добавить("Покупатель_Поставщик_Прочее", 
		Тип("ГруппаФормы"), ЭтаФорма);
		Покупатель_Поставщик_Прочее.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Покупатель_Поставщик_Прочее.Отображение = ОтображениеОбычнойГруппы.Нет;
		Покупатель_Поставщик_Прочее.ЦветФона = Новый Цвет(250, 240, 160);
		Покупатель_Поставщик_Прочее.ОтображатьЗаголовок = ЛОЖЬ; 
		Покупатель_Поставщик_Прочее.Группировка = 
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;

	Если Элементы.Найти("Покупатель") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Покупатель", Тип("ПолеФормы"), Покупатель_Поставщик_Прочее);
		НовыйЭлемент.ПутьКДанным = "Объект.Покупатель";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	КонецЕсли;
	
	Если Элементы.Найти("Поставщик") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("Поставщик", Тип("ПолеФормы"), Покупатель_Поставщик_Прочее);
		НовыйЭлемент.ПутьКДанным = "Объект.Поставщик";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	КонецЕсли;
	
	Если Элементы.Найти("ПрочиеОтношения") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ПрочиеОтношения", Тип("ПолеФормы"), 
		Покупатель_Поставщик_Прочее);
		НовыйЭлемент.ПутьКДанным = "Объект.ПрочиеОтношения";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	КонецЕсли;
	#КонецОбласти
	
//==================================================================
	
	#Область КонтактнаяИнформация_ДополнительныеРеквизиты
	Если Элементы.Найти("ГруппаСтраницы") = Неопределено Тогда
		ГруппаСтраниц = ЭтаФорма.Элементы.Добавить("ГруппаСтраницы", Тип("ГруппаФормы"), ЭтаФорма);
		ГруппаСтраниц.Вид = ВидГруппыФормы.Страницы;
		ГруппаСтраниц.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		ГруппаСтраниц.РастягиватьПоГоризонтали = ИСТИНА;
	КонецЕсли;
	
	// Контактная информация
	Если Элементы.Найти("СтраницаКонтактнаяИнформация") = Неопределено Тогда
		СтраницаКонтактнаяИнформация = ЭтаФорма.Элементы.Добавить("СтраницаКонтактнаяИнформация", 
		Тип("ГруппаФормы"), ГруппаСтраниц);
		СтраницаКонтактнаяИнформация.Вид = ВидГруппыФормы.Страница;
		СтраницаКонтактнаяИнформация.Заголовок = "Контактная информация";
	КонецЕсли;

	Если Элементы.Найти("ТаблицаКонтактнаяИнформация") = Неопределено Тогда
		ТаблицаКонтактнаяИнформация = Элементы.Добавить("ТаблицаКонтактнаяИнформация", 
		Тип("ТаблицаФормы"), СтраницаКонтактнаяИнформация);
		ТаблицаКонтактнаяИнформация.ПутьКДанным = "Объект.КонтактнаяИнформация";
		
		КолонкаТип = Элементы.Добавить("Тип", Тип("ПолеФормы"), ТаблицаКонтактнаяИнформация);
		КолонкаТип.ПутьКДанным = "Объект.КонтактнаяИнформация.Тип";
		КолонкаТип.Вид = ВидПоляФормы.ПолеВвода;
		
		КолонкаВид = Элементы.Добавить("Вид", Тип("ПолеФормы"), ТаблицаКонтактнаяИнформация);
		КолонкаВид.ПутьКДанным = "Объект.КонтактнаяИнформация.Вид";
		КолонкаВид.Вид = ВидПоляФормы.ПолеВвода;
		
		КолонкаЗначение = Элементы.Добавить("Значение", Тип("ПолеФормы"), 
		ТаблицаКонтактнаяИнформация);
		КолонкаЗначение.ПутьКДанным = "Объект.КонтактнаяИнформация.Значение";
		КолонкаЗначение.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	// ДополнительныеРеквизиты
	Если Элементы.Найти("СтраницаДополнительныеРеквизиты") = Неопределено Тогда
		СтраницаДополнительныеРеквизиты = 
		ЭтаФорма.Элементы.Добавить("СтраницаДополнительныеРеквизиты", Тип("ГруппаФормы"), 
		ГруппаСтраниц);
		СтраницаДополнительныеРеквизиты.Вид = ВидГруппыФормы.Страница;
		СтраницаДополнительныеРеквизиты.Заголовок = "Дополнительные реквизиты";
	КонецЕсли;

	Если Элементы.Найти("ТаблицаДополнительныеРеквизиты") = Неопределено Тогда
		ТаблицаДополнительныеРеквизиты = Элементы.Добавить("ТаблицаДополнительныеРеквизиты", 
		Тип("ТаблицаФормы"), СтраницаДополнительныеРеквизиты);
		ТаблицаДополнительныеРеквизиты.ПутьКДанным = "Объект.ДополнительныеРеквизиты";
		
		КолонкаДопРеквизит = Элементы.Добавить("ДопРеквизит", Тип("ПолеФормы"), 
		ТаблицаДополнительныеРеквизиты);
		КолонкаДопРеквизит.ПутьКДанным = "Объект.ДополнительныеРеквизиты.ДопРеквизит";
		КолонкаДопРеквизит.Вид = ВидПоляФормы.ПолеВвода;
		
		КолонкаЗначениеДопРеквизита = Элементы.Добавить("ЗначениеДопРеквизита", Тип("ПолеФормы"), 
		ТаблицаДополнительныеРеквизиты);
		КолонкаЗначениеДопРеквизита.ПутьКДанным = "Объект.ДополнительныеРеквизиты.Значение";
		КолонкаЗначениеДопРеквизита.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеКонтрагента()
	Сообщить("Эмуляция заполнения данных контрагента из данных сайта");
КонецПроцедуры

```