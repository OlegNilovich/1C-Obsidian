#ЗАПРОСЫ

```bsl
// 1. Получить данные документа
МенеджерВТ = Новый МенеджерВременныхТаблиц;
Запрос = Новый Запрос;
Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяТовары.Номенклатура КАК				Номенклатура,
	|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК	Количество
	|ПОМЕСТИТЬ ВТ_ТоварыДокумента
	|ИЗ
	|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
	|ГДЕ
	|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыДокумента.Номенклатура КАК		Номенклатура,
	|	ВТ_ТоварыДокумента.Количество КАК			Количество
	|ИЗ
	|	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента";
Запрос.УстановитьПараметр("Ссылка", Ссылка);

// 2. Сформировать движения для записи регистр
Выборка = Запрос.Выполнить().Выбрать();
Пока Выборка.Следующий() Цикл
	Движение	= Движения.ТоварыНаСкладах.ДобавитьРасход();
	Движение.Период			= Дата;
	Движение.Склад				= Склад;
	Движение.Организация	= Организация;
	Движение.Номенклатура	= Выборка.Номенклатура;
	Движение.Количество		= Выборка.Количество;
КонецЦикла;  

// 3. Установить блокировку отключением разделения итогов и записать движения в регистр
Движения.СвободныеОстатки.БлокироватьДляИзменения = Истина;
Движения.ТоварыНаСкладах.Записать(); 

// 4. Прочитать остатки и проверить не ушли ли мы в минус
Запрос = Новый Запрос;
Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК							Номенклатура,
	|	-ТоварыНаСкладахОстатки.КоличествоОстаток КАК					Дефицит,
	|	ТоварыНаСкладахОстатки.Номенклатура.Представление КАК		НоменклатураПредставление
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&Граница,
	|			(Склад, Организация, Номенклатура) В
	|				(ВЫБРАТЬ
	|					&Склад,
	|					&Организация,
	|					ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура
	|				ИЗ
	|					ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.КоличествоОстаток < 0";
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
Запрос.УстановитьПараметр("Организация", Организация);
Запрос.УстановитьПараметр("Склад", Склад);

РезультатЗапроса = Запрос.Выполнить();

// 5. Сообщить об отрицательных остатках пользователю
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Текст = "Не хватает товара %1 в количестве %2 шт ";
		Сообщить(СтрШаблон(Текст, Выборка.НоменклатураПредставление, Выборка.Дефицит));	
	КонецЦикла;
КонецЕсли;

// 6. Если контроль остатков не пройден - Выходим из процедуры проведения
Если Отказ = Истина Тогда
	Возврат;
КонецЕсли;
```