#ФОРМЫ 
### Задача:
В документе "РасходнаяНакладная", требуется создать команду "Подбор". Которая будет открывать форму подбора товаров и после окончания подбора будет переносить выбранные товары в табличную часть документа
![[Pasted image 20240823081445.png]]

---
### Решение:
1. Создаем обработку "ФормаПодбораТоваров"
2. Создаем первую и основную форму "СписокРасходныхНакладных"
3. Создаем вторую форму "РасходнаяНакладная"
4. Создаем третью форму "ФормаПодбора"
![[Pasted image 20240823081616.png]]
##### Форма "СписокРасходныхНакладных"
1. Открываем редактор формы "СписокРасходныхНакладных"
2. Создаем реквизит "Список" с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Документ.РасходнаяНакладная
4. Переносим реквизит "Список" на форму
![[Pasted image 20240823072253.png]]
5. Для элемента формы "Список", создаем событие "Выбор"
![[Pasted image 20240823072758.png]]
6. Модуль формы:
```bsl
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураПараметров = Новый Структура("Ключ", ВыбраннаяСтрока);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Обработка.ФормаПодбораТоваров.Форма.РасходнаяНакладная", 
	СтруктураПараметров,,,,,,Режим);
	
КонецПроцедуры
```
##### Форма "РасходнаяНакладная"
1. Открываем редактор формы "РасходнаяНакладная"
2. Создаем реквизит "Объект" с типом (ДокументОбъект.РасходнаяНакладная)
3. Переносим необходимые реквизиты из шапки документа на форму
![[Pasted image 20240823073130.png]]
4. Переносим табличную часть "Товары" с необходимыми реквизитами
![[Pasted image 20240823073306.png]]
5. Создаем обработчик события формы "ОбработкаОповещения"
![[Pasted image 20240823073720.png]]
6. Создаем команду "Подбор" и обработчик команды
![[Pasted image 20240823073536.png]]
7. Модуль формы:
```bsl
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ТоварыПодобраны" Тогда
		
		ТоварыПодбора = Параметр;

		Для каждого ТоварПодбора из ТоварыПодбора Цикл
			
			ПараметрыОтбора = Новый Структура("Номенклатура", ТоварПодбора.Номенклатура);
			НайденныеСтроки = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				ТоварДокумента = НайденныеСтроки[0];	
				ТоварДокумента.Количество = ТоварДокумента.Количество + 
				ТоварДокумента.Количество;
			Иначе
				ТоварДокумента = Объект.Товары.Добавить();
				ТоварДокумента.Номенклатура = ТоварПодбора.Номенклатура;
				ТоварДокумента.Количество = ТоварПодбора.Количество;
				ТоварДокумента.Цена = 
				ЦенаТовара(ТоварДокумента.Номенклатура, Объект.ВидЦен, Объект.Дата);
				ТоварДокумента.Сумма = ТоварДокумента.Количество * ТоварДокумента.Цена;
			КонецЕсли
			
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ЦенаТовара(Номенклатура, ВидЦены, Дата)
	Отбор = Новый Структура("Номенклатура, ВидЦен", Номенклатура, ВидЦены);
	Возврат РегистрыСведений.ЦеныНоменклатуры.ПолучитьПоследнее(Дата, Отбор).Цена;
КонецФункции


&НаКлиенте
Процедура Подбор(Команда)
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Обработка.ФормаПодбораТоваров.Форма.ФормаПодбора",,,,,,, Режим);
КонецПроцедуры
```
##### Форма "ФормаПодбора"
1. Открываем редактор формы "ФормаПодбора"
2. Создаем реквизит "Товары" с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Справочник.Номенклатура
4. Переносим реквизит на форму
![[Pasted image 20240823074957.png]]
5. Создаем реквизит "Корзина" с типом (ТаблицаЗначений)
6. Добавляем колонку "Номенклатура" с типом (СправочникСсылка.Номенклатура)
7. Добавляем колонку "Количество" с типом (Число, 10, 0)
![[Pasted image 20240823075245.png]]
8. Для элемента формы "Товары", создаем обработчик события "Выбор"
![[Pasted image 20240823075511.png]]
9. Для элемента формы "Корзина", создаем обработчик события "Перетаскивание"
![[Pasted image 20240823080442.png]]
10. Создаем команду "ПеренестиВДокумент" и обработчик действия команды
![[Pasted image 20240823075644.png]]
10. Модуль формы:
```bsl
&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	Оповестить("ТоварыПодобраны", Корзина);
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если НЕ Элементы.Товары.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		Номенклатура = Элементы.Товары.ТекущиеДанные.Ссылка;
		ДобавитьНоменклатуру(Номенклатура);
	КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура КорзинаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если НЕ Элементы.Товары.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		Номенклатура = ПараметрыПеретаскивания.Значение[0];
		ДобавитьНоменклатуру(Номенклатура);
	КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНоменклатуру(Номенклатура)
		ПараметрыОтбора = Новый Структура("Номенклатура", Номенклатура);
		НайденныеСтроки = Корзина.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТоварВКорзине = НайденныеСтроки[0];	
			ТоварВКорзине.Количество = ТоварВКорзине.Количество + 1;
		Иначе
			ТоварВКорзине = Корзина.Добавить();
			ТоварВКорзине.Номенклатура = Номенклатура;
			ТоварВКорзине.Количество = 1;
		КонецЕсли
КонецПроцедуры
```