ЗАНЯТИЕ 6. РАСЧЕТ СЕБЕСТОИМОСТИ ПО КОМПАНИИ В ЦЕЛОМ
https://www.youtube.com/watch?v=hEKNAUiZr4w&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=6

ЗАНЯТИЕ 7-9. ОТЧЕТ ПО ОСТАТКАМ ТОВАРОВ
https://www.youtube.com/watch?v=fJvlJFY7DII&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=7
https://www.youtube.com/watch?v=ydq78W6p9W4&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=8
https://www.youtube.com/watch?v=ZE3m7pEAFqU&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=9

---
### Задача:

Компания занимается оптовой торговлей. Поступление товара отражается документом «*Приходная накладная*», продажа – «*Расходная накладная*». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Учет товаров ведется в разрезе *складов*. Поступление и продажа осуществляется с указанием склада (в шапке документа).

При проведении документа «Расходная накладная» необходимо производить списание товара со склада. В том случае, когда товара не хватает, документ проводиться не должен.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от метода списания *(FIFO или LIFO*), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения.

Для расчета себестоимости при списании товара необходимо учитывать только момент поступления товара в компанию, вне зависимости от того, на какой склад он пришел. Предположим, для метода списания FIFO первое поступление портсигара произошло на склад «Основной» документом «Приходная накладная №1», а потом на склад «Транзитный» документом «Приходная накладная №2». В этом случае при продаже товара со склада «Транзитный» в первую очередь должна быть списана себестоимость портсигара по документу «Приходная накладная №1», так как она пришла раньше.

Необходимо построить отчет по остаткам товара на складах на указанную дату.
![[Pasted image 20240927110043.png]]

---
### Решение:
##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *Склад*
Ресурсы: *Количество*

##### Регистр накопления: **СебестоимостьТоваров**
Измерения: *Номенклатура*, *Партия*
Ресурсы: *Количество*, *Стоимость*

##### Регистр сведений: **УчетнаяПолитика**
Измерения: 
Ресурсы: *МетодСписанияСебестоимости*

##### Документ **ПриходнаяНакладная**
Реквизиты: *Склад*, *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.СебестоимостьТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	&Склад КАК Склад,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка КАК Партия,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
		|	&Период КАК Период
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Движение = Движения.СебестоимостьТоваров.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);		
	КонецЦикла;
	
КонецПроцедуры
```

##### Документ **РасходнаяНакладная**
Реквизиты: *Склад*, *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Результат = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если Результат.МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ПустаяСсылка() Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан метод списания себестоимости";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	МетодСписанияСебестоимости = Результат.МетодСписанияСебестоимости;
	
	// регистр ОстаткиНоменклатуры
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = 
		|  ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Количество КАК Количество,
		|	&Период КАК Период,
		|	&Склад КАК Склад,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
		|ИЗ
		|	втТЧТовары КАК втТЧТовары";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Склад",  Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Нехватка
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						Т.Номенклатура
		|					ИЗ
		|						втТЧТовары КАК Т)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Склад",   Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать();	
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			                  Выборка.НоменклатураПредставление, Выборка.Нехватка);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;


	// регистр СебестоимостьТоваров
	Движения.СебестоимостьТоваров.Записать();
	Движения.СебестоимостьТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СебестоимостьТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	СебестоимостьТоваровОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(СебестоимостьТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(СебестоимостьТоваровОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
		|	СебестоимостьТоваровОстатки.Партия.Представление КАК ПартияПредставление
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК СебестоимостьТоваровОстатки
		|		ПО втТЧТовары.Номенклатура = СебестоимостьТоваровОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СебестоимостьТоваровОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Если МетодСписанияСебестоимости = Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СебестоимостьТоваровОстатки.Партия.МоментВремени",
		                                         "СебестоимостьТоваровОстатки.Партия.МоментВремени УБЫВ");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			                  ВыборкаНоменклатура.НоменклатураПредставление,
							  ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоСписать > 0 Цикл
			
			Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			
			Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			Иначе
				Если ВыборкаДетальныеЗаписи.КоличествоОстаток = 0 Тогда
					Отказ = Истина;
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтрШаблон("Обнаружен нулевой остаток по партии %1", 
					ВыборкаДетальныеЗаписи.ПартияПредставление);
					Сообщение.Сообщить();
					Прервать;
				КонецЕсли;
					
				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток / 
				ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
			КонецЕсли;
			
			Движение = Движения.СебестоимостьТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = Количество;
			Движение.Стоимость = Себестоимость;
			
			КоличествоСписать = КоличествоСписать - Количество;
					
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
```
