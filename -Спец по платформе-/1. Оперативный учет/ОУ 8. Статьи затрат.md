#TODO 

ЗАНЯТИЕ ???

---
### Задача:

Компания занимается оптовой торговлей. Все операции отражаются документами «Приходная накладная», «Расходная накладная», «Приход денег» и «Расход денег», причем для каждого из них может быть указана своя статья затрат. Таким образом, документ «Расход денег» может отражать операции, например, по оплате поставщику или возврата от покупателя в зависимости от выбранной статьи затрат.

Структурно компания состоит из нескольких подразделений. В целях ведения управленческого учета для каждого отдела заводится бюджет (документ «Бюджет») на  произвольный ~~месяц~~ период (указывается в шапке документа) с указанием всех предполагаемых статей затрат, сумм по ним и возможного превышения этих сумм в суммовом выражении. Следует считать, что каждое подразделение может вводить несколько документов «Бюджет», данные которых должны суммироваться для формирования итогового бюджета.

В целях контроля над исполнением бюджета при проведении любого документа должен происходить анализ фактических затрат и выводится соответствующее предупреждение, в случае превышения над бюджетом. Например, если по статье «оплата поставщикам» запланировано 100 000 и 20 000, то при суммарной оплате более 100 000 и более 120 000 должны быть выданы соответствующие предупреждения.

Можно считать, что документы задним числом не вводятся, но существующие документы могут быть перепроведены.

Необходимо создать отчет по исполнению бюджета за период, кратный месяцу:
![[Pasted image 20241003150437.png]]

«% исп.» рассчитывается как процент фактических затрат относительно плановых.

---
### Решение:

##### Справочник: **Валюты** 
Реквизиты: *КраткоеНаименование*

##### Справочник: **Проекты** 
Реквизиты: *Валюта* (СправочникСсылка.Валюты), *ДатаОплаты* (Дата)

##### Регистр сведений: **КурсыВалют** 
Измерения: *Валюта*
Ресурсы: *Курс*

##### Регистр накопления: **ОстаткиНоменклатуры** (Остатки)
Измерения: *Номенклатура*
Ресурсы: *Количество*

##### Регистр накопления: **Взаиморасчеты** (Остатки)
Измерения: *Контрагент*, *Валюта*, *Проект* (СправочникСсылка.Проекты)
Ресурсы: *Сумма*

##### Общий модуль: **ОбщегоНазначения** (Сервер)

```bsl
Функция ВалютаПроекта(Проект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Проекты.Валюта КАК Валюта
		|ИЗ
		|	Справочник.Проекты КАК Проекты
		|ГДЕ
		|	Проекты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Валюта;	
	КонецЕсли;	
	
	Возврат Неопределено;
		
КонецФункции
```

##### Документ **РасходнаяНакладная**
Реквизиты: *Контрагент*, *Проект*, *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Движение = Движения.Взаиморасчеты.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Проект = Проект;
	
	ВалютаПроекта = ОбщегоНазначения.ВалютаПроекта(Проект);
	Движение.Валюта = ВалютаПроекта;
	
	Движение.СуммаВВалюте = СуммаПоДокументу;
	
	Курс = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ВалютаПроекта)).Курс;
	Курс = ?(Курс = 0, 1, Курс);
	
	Движение.СуммаВРублях = СуммаПоДокументу * Курс;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Проект", Справочники.Проекты.ПустаяСсылка());
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ВзаиморасчетыОстатки.СуммаВРубляхОстаток КАК ОстатокАвансаВРублях,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	ВЫРАЗИТЬ(-ВзаиморасчетыОстатки.СуммаВРубляхОстаток / ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК ЧИСЛО(15, 2)) КАК ОстатокАвансаВВалюте
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект = ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
		|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("Валюта", ВалютаПроекта);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ОстатокАвансаВВалюте > 0 Тогда
		
		СуммаПоДокументуВВалюте = СуммаПоДокументу;
		СуммаПоДокументуВРублях = СуммаПоДокументу * Выборка.Курс;
		
		Если Выборка.ОстатокАвансаВРублях >= СуммаПоДокументуВРублях Тогда
			СуммаСписанияАвансаВРублях = СуммаПоДокументуВРублях;
			СуммаСписанияАвансаВВалюте = СуммаПоДокументуВВалюте;
		Иначе
			СуммаСписанияАвансаВРублях = Выборка.ОстатокАвансаВРублях;
			СуммаСписанияАвансаВВалюте = Выборка.ОстатокАвансаВВалюте;
		КонецЕсли;
		
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Справочники.Проекты.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.СуммаВВалюте = СуммаСписанияАвансаВРублях;
		Движение.СуммаВРублях = СуммаСписанияАвансаВРублях;
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Проект;
		Движение.Валюта = ВалютаПроекта;
		Движение.СуммаВВалюте = СуммаСписанияАвансаВВалюте;
		Движение.СуммаВРублях = СуммаСписанияАвансаВРублях;
		
	КонецЕсли;
	
КонецПроцедуры

```

##### Документ **ПриходДенег**
Реквизиты: *Контрагент*, *СуммаПлатежа*

Модуль объекта:
```bsl

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Движения.Взаиморасчеты.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Контрагент КАК Контрагент,
		|	ВзаиморасчетыОстатки.Проект КАК Проект,
		|	ВзаиморасчетыОстатки.Валюта КАК Валюта,
		|	ВзаиморасчетыОстатки.СуммаВРубляхОстаток КАК СуммаВРубляхОстаток,
		|	ВзаиморасчетыОстатки.СуммаВВалютеОстаток КАК СуммаВВалютеОстаток
		|ПОМЕСТИТЬ втДолгиПокупателей
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК ВзаиморасчетыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДолгиПокупателей.Контрагент КАК Контрагент,
		|	втДолгиПокупателей.Проект КАК Проект,
		|	втДолгиПокупателей.Валюта КАК Валюта,
		|	втДолгиПокупателей.СуммаВРубляхОстаток КАК СуммаВРубляхОстаток,
		|	втДолгиПокупателей.СуммаВВалютеОстаток КАК СуммаВВалютеОстаток,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	&Период КАК Период,
		|	втДолгиПокупателей.Проект.ДатаОплаты КАК ДатаОплаты
		|ИЗ
		|	втДолгиПокупателей КАК втДолгиПокупателей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						втДолгиПокупателей.Валюта КАК Валюта
		|					ИЗ
		|						втДолгиПокупателей КАК втДолгиПокупателей)) КАК КурсыВалютСрезПоследних
		|		ПО втДолгиПокупателей.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОплаты";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);
	
	СуммаКПогашениюВРублях = СуммаПлатежа;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() И СуммаКПогашениюВРублях > 0 Цикл
			
			СуммаВРублях = Мин(СуммаКПогашениюВРублях, Выборка.СуммаВРубляхОстаток);
			
			Если СуммаВРублях = Выборка.СуммаВРубляхОстаток Тогда
				СуммаВВалюте = Выборка.СуммаВВалютеОстаток;
			Иначе
				СуммаВВалюте = Окр(СуммаВРублях / Выборка.Курс, 2);
				Если СуммаВВалюте = Выборка.СуммаВВалютеОстаток Тогда
					СуммаВВалюте = СуммаВВалюте - 0.01;
				КонецЕсли;
			КонецЕсли;
			
			Если СуммаВВалюте > 0 Тогда
				Движение = Движения.Взаиморасчеты.ДобавитьРасход();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				Движение.СуммаВРублях = СуммаВРублях;
				Движение.СуммаВВалюте = СуммаВВалюте;
				
				СуммаКПогашениюВРублях = СуммаКПогашениюВРублях - СуммаВРублях;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// авансы
	Если СуммаКПогашениюВРублях > 0 Тогда
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Справочники.Проекты.ПустаяСсылка();
		Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
		Движение.СуммаВВалюте = СуммаКПогашениюВРублях;
		Движение.СуммаВРублях = СуммаКПогашениюВРублях;
	КонецЕсли;
		
КонецПроцедуры

```
