ЗАНЯТИЕ 22. ПОСТУПЛЕНИЕ И ОТГРУЗКА ПО ЗАКАЗАМ
https://www.youtube.com/watch?v=pExwyNKcDTQ&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=22

ЗАНЯТИЕ 23. ОТЧЕТ ПО НЕДООТГРУЖЕННЫМ ЗАКАЗАМ
https://www.youtube.com/watch?v=dOx1kJRh-lo&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=23

---
### Задача:

Компания занимается торговлей специализированного оборудования. Принят следующий порядок работы: поступает заказ покупателя (документ «Заказ покупателя»). На основании заказов покупателей производится закупка оборудования у поставщика (документ «Приходная накладная»), причем можно приобрести только то оборудование, которое заказано покупателем. После того, как оборудование поступило, оно может быть отгружено покупателю (документ «Расходная накладная»).

В документе «Приходная накладная» для каждой номенклатурной позиции указывается заказ покупателя, для которого приобретено это оборудование. Оборудование для одного заказа покупателя может поставляться несколькими документами. Закупать можно только заказанное покупателем оборудование.

Отгрузка оборудования по одному заказу покупателя может происходить несколькими документами «Расходная накладная». Следует считать, что оборудование по заказу будет отгружено полностью и отгрузка может происходить только на основании заказа. Себестоимость оборудования рассчитывается как средняя в рамках закупок под заказ покупателя.

Используя планы видов характеристик, необходимо предоставить возможность пользователю добавить произвольное количество дополнительных характеристик к каждому заказу. Например, для одного заказа могут быть указаны регион и срочность, а для другого заказа может быть указан регион и необходимость в дополнительном контроле поставки.

Необходимо создать отчет по анализу недоотгруженных заказов покупателей на выбранную дату в разрезе выбранной характеристики.

![[Pasted image 20240930103221.png]]

---
### Решение:

##### План видов характеристик: **ХарактеристикиОбъектов**

##### Справочник: **ДопЗначенияХарактеристик**
Владелец: *ПланВидовХарактеристик.ХарактеристикиОбъектов*

##### Регистр Сведений: **ЗначенияХарактеристикЗаказов**
Измерения: 
- *ЗаказКлиента* (ДокументСсылка.ЗаказКлиента)
- *Характеристика* (ПланВидовХарактеристикСсылка.ХарактеристикиОбъектов)
Ресурсы: 
- *Значение* (Характеристика.ХарактеристикиОбъектов)

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *ЗаказКлиента* (ДокументСсылка.ЗаказКлиента)
Ресурсы: *Количество*, *Стоимость*

##### Регистр накопления: **ТоварыКЗакупке**
Измерения: *Номенклатура*, *ЗаказКлиента* (ДокументСсылка.ЗаказКлиента)
Ресурсы: *Количество*

##### Документ **ЗаказКлиента**
Реквизиты: 
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*

Модуль объекта:
```bsl

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыКЗакупке.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ЗаказКлиентаСписокНоменклатуры.Количество) КАК Количество,
		|	&Ссылка КАК ЗаказКлиента,
		|	&Период КАК Период
		|ИЗ
		|	Документ.ЗаказКлиента.СписокНоменклатуры КАК ЗаказКлиентаСписокНоменклатуры
		|ГДЕ
		|	ЗаказКлиентаСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыКЗакупке.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
КонецПроцедуры

```

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*
- *ЗаказКлиента* (ДокументСсылка.ЗаказКлиента)

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказКлиента КАК ЗаказКлиента,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.ЗаказКлиента,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ЗаказКлиента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.ЗаказКлиента КАК ЗаказКлиента,
		|	втТЧТовары.Количество КАК Количество,
		|	&Период КАК Период,
		|	втТЧТовары.Сумма КАК Стоимость
		|ИЗ
		|	втТЧТовары КАК втТЧТовары";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ТоварыКЗакупке.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ТоварыКЗакупке.Записывать = Истина;
	Движения.ТоварыКЗакупке.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыКЗакупкеОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ТоварыКЗакупкеОстатки.ЗаказКлиента.Представление КАК ЗаказКлиентаПредставление,
		|	-ТоварыКЗакупкеОстатки.КоличествоОстаток КАК КоличествоНехватки
		|ИЗ
		|	РегистрНакопления.ТоварыКЗакупке.Остатки(
		|			&Граница,
		|			(Номенклатура, ЗаказКлиента) В
		|				(ВЫБРАТЬ
		|					Т.Номенклатура,
		|					Т.ЗаказКлиента
		|				ИЗ
		|					втТЧТовары КАК Т)) КАК ТоварыКЗакупкеОстатки
		|ГДЕ
		|	ТоварыКЗакупкеОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по заказу %2 в количестве %3",
			Выборка.НоменклатураПредставление, Выборка.ЗаказКлиентаПредставление, Выборка.КоличествоНехватки);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;

КонецПроцедуры

```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*, *ЗаказКлиента* (ДокументСсылка.ЗаказКлиента)
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		// Заполнение шапки
		ЗаказКлиента = ДанныеЗаполнения;
		Для Каждого ТекСтрокаСписокНоменклатуры Из ДанныеЗаполнения.СписокНоменклатуры Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
			НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;			
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// удаление движения
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	// взвели флаг
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ЗаказКлиента", ЗаказКлиента);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	// контроль остатков
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				ЗаказКлиента = &ЗаказКлиента
		|					И Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧТовары.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара " + Выборка.НоменклатураПредставление 
			+ " в количестве " + (Выборка.Количество - Выборка.КоличествоОстаток);
			Сообщение.Сообщить();	
			
			Отказ = Истина;
			
		КонецЕсли;	
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СтоимостьОстаток;
		Иначе
			Себестоимость = Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
		КонецЕсли;
						
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.ЗаказКлиента = ЗаказКлиента;
		Движение.Количество = Выборка.Количество;
		Движение.Стоимость = Себестоимость;
		
	КонецЦикла;
	
КонецПроцедуры

```