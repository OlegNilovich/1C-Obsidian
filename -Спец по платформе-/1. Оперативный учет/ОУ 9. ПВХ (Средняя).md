ЗАНЯТИЕ 21. ХАРАКТЕРИСТИКИ И СВОЙСТВА
https://www.youtube.com/watch?v=1GbWPVLtlaQ&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=21

---
### Задача:

Организация занимается оптовой торговлей. Для описания товаров существуют различные характеристики, набор которых составляет свойство. Учет товаров ведется в разрезе свойств этих товаров, причем для каждого товара набор этих свойств может быть произвольный. Например, «ботинки черные 42 размера», «ботинки коричневые 42 размера» и «ботинки коричневые 44 размера утепленные» это один товар «ботинки», но с разными свойствами. «Черные 42 размера» будет являться свойством, а цвет «черный» и размер «42» являются характеристиками.

Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». И в документе поступления, и в документе списания для каждого товара указывается его свойство.

Механизм свойств и характеристик товаров должен быть реализован с использованием плана видов характеристик.

Складской учет товаров не ведется.

Списание себестоимости товаров должно быть организовано в разрезе свойств.

Необходимо построить отчет о движениях товаров за период по выбранной характеристике в количественном и суммовом выражении.

![[Pasted image 20240930101755.png]]

---
### Решение:

##### План видов характеристик: **ХарактеристикиОбъектов**

##### Справочник: **ДополнительныеЗначенияХарактеристик**
Владелец: *ПланВидовХарактеристик.ХарактеристикиОбъектов*

##### Справочник: **СвойстваНоменклатуры**
Владелец: *Справочник.Номенклатура*
Табличная часть: *НаборХарактеристик*. Реквизиты:
- *Характеристика* (ПланВидовХарактеристикСсылка.ХарактеристикиОбъектов)
- *Значение* (Характеристика.ХарактеристикиОбъектов)

##### Регистр Сведений: **ЗначенияХарактеристикСвойств**
Измерения: 
- *Свойство* (СправочникСсылка.СвойстваНоменклатуры)
- *Характеристика* (ПланВидовХарактеристикСсылка.ХарактеристикиОбъектов)
Ресурсы: 
- *Значение* (Характеристика.ХарактеристикиОбъектов)

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *Свойство* (СправочникСсылка.СвойстваНоменклатуры)
Ресурсы: *Количество*, *Стоимость*

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Свойство* (СправочникСсылка.СвойстваНоменклатуры)
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Свойство";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Свойство = Выборка.Свойство;
		Движение.Количество = Выборка.Количество;
		Движение.Стоимость = Выборка.Сумма;
	КонецЦикла;
	 
КонецПроцедуры

```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Свойство* (СправочникСсылка.СвойстваНоменклатуры)*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// удаление движения
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	// взвели флаг
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Свойство", "Свойство");
	Блокировка.Заблокировать();
	
	// контроль остатков
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Свойство КАК Свойство,
		|	втТЧТовары.Свойство.Представление КАК СвойствоПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, Свойство) В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура,
		|						втТЧТовары.Свойство КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И втТЧТовары.Свойство = ОстаткиНоменклатурыОстатки.Свойство";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара " + Выборка.НоменклатураПредставление + 
			" со свойством " + Выборка.СвойствоПредставление +
			" в количестве " + (Выборка.Количество - Выборка.КоличествоОстаток);
			Сообщение.Сообщить();	
			
			Отказ = Истина;
			
		КонецЕсли;	
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СтоимостьОстаток;
		Иначе
			Себестоимость = Выборка.СтоимостьОстаток / Выборка.КоличествоОстаток * Выборка.Количество;
		КонецЕсли;
						
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Свойство = Выборка.Свойство;
		Движение.Количество = Выборка.Количество;
		Движение.Стоимость = Себестоимость;
		
	КонецЦикла;
	
КонецПроцедуры

```