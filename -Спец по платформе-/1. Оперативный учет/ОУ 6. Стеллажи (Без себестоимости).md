ЗАНЯТИЕ 17. СТЕЛЛАЖИ И КОМПЛЕКТУЮЩИЕ
https://www.youtube.com/watch?v=51E-Hc80gp0&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=17

---
### Задача:

Компания занимается оптовой торговлей складских стеллажей и их комплектующих. Закупка комплектующих отражается документом «Приходная Накладная», продажа – «Расходная накладная».

 Каждый стеллаж представляет собой некоторый фиксированный набор комплектующих (например, 4 стойки, 5 полок и 20 болтов). Необходимо обеспечить уникальность деталей, т.е. одна и та же деталь не может относиться к разным стеллажам.

 Учет остатков ведется в разрезе складов. В документах «приходная накладная» и «Расходная накладная» склад только один (склад – реквизит шапки).

Возможна продажа как отдельных комплектующих, так и целых стеллажей, причем и стеллажи и их комплектующие указываются в одной табличной части. В случае продажи стеллажа осуществляется списание со склада соответствующего количества комплектующих. В том случае, если каких-либо комплектующих на складе не хватает, документ проводиться не должен. Учет себестоимости деталей вести не требуется.

Используя планы видов характеристик, необходимо предоставить возможность пользователю добавить произвольное количество характеристик к каждому стеллажу (материал, страна).

Создать отчет, который в разрезе складов будет показывать количество стеллажей. Например, если стеллаж состоит из 4 стоек, 5 полок и 20 болтов, а на складе есть 8 стоек, 15 полок и 30 болтов, то целый стеллаж только один. Также в отчете вывести информацию о стране стеллажа.

![[Pasted image 20240930093531.png]]

---
### Решение:

##### Регистр сведений: **СоставСтеллажей**
Измерения: *Комплектующая* (СправочникСсылка.Номенклатура)
Ресурсы: *Стеллаж* (СправочникСсылка.Номенклатура), *Количество*

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *Склад*
Ресурсы: *Количество*

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*, *Склад*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.Количество;		
	КонецЦикла;
	 
КонецПроцедуры

```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*, *Склад*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Запрос = Новый Запрос;
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК НоменклатураПродажи,
		|	ЕСТЬNULL(СоставСтеллажей.Комплектующая, ВЫБОР
		|			КОГДА втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Комплектующая)
		|				ТОГДА втТЧТовары.Номенклатура
		|		КОНЕЦ) КАК Номенклатура,
		|	ЕСТЬNULL(втТЧТовары.Количество * СоставСтеллажей.Количество, ВЫБОР
		|			КОГДА втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Комплектующая)
		|				ТОГДА втТЧТовары.Количество
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ втКомплектующие
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		|		ПО втТЧТовары.Номенклатура = СоставСтеллажей.Стеллаж
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКомплектующие.НоменклатураПродажи.Представление КАК НоменклатураПредставление
		|ИЗ
		|	втКомплектующие КАК втКомплектующие
		|ГДЕ
		|	втКомплектующие.Номенклатура ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКомплектующие.Номенклатура КАК Номенклатура,
		|	СУММА(втКомплектующие.Количество) КАК Количество,
		|	&Период КАК Период,
		|	&Склад КАК Склад
		|ИЗ
		|	втКомплектующие КАК втКомплектующие
		|
		|СГРУППИРОВАТЬ ПО
		|	втКомплектующие.Номенклатура";
		
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Склад",  Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	РезультатПроверка = РезультатыЗапросов[2];
	Если Не РезультатПроверка.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатПроверка.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не определен состав стеллажа " + Выборка.НоменклатураПредставление;
			Сообщение.Сообщить();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
	Результат = РезультатыЗапросов[3];
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК КоличествоНехватки,
		|	СоставСтеллажей.Стеллаж.Представление КАК СтеллажПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&Граница,
		|			Склад = &Склад
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						Т.Номенклатура
		|					ИЗ
		|						втКомплектующие КАК Т)) КАК ОстаткиНоменклатурыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		|		ПО ОстаткиНоменклатурыОстатки.Номенклатура = СоставСтеллажей.Комплектующая
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();	
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			
			Если Выборка.СтеллажПредставление = NULL Тогда
				Сообщение.Текст = СтрШаблон("Недостаточно комплектующей %1 в количестве %2",
				Выборка.НоменклатураПредставление, Выборка.КоличествоНехватки);
			Иначе	
				Сообщение.Текст = СтрШаблон("Недостаточно комплектующей %1 (входит в состав %2) в кол-ве %3",
				Выборка.НоменклатураПредставление, Выборка.СтеллажПредставление, Выборка.КоличествоНехватки);	
			КонецЕсли;
			
			Сообщение.Сообщить();	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

```