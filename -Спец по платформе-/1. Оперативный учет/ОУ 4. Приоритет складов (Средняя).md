ЗАНЯТИЕ 14. СПИСАНИЕ С УЧЕТОМ ПРИОРИТЕТОВ СКЛАДОВ
https://www.youtube.com/watch?v=BXKjInJGKuQ&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=14

---
### Задача:

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Учет товаров ведется в разрезе складов. Каждый из складов имеет свой приоритет, который может меняться не чаще чем 1 раз в месяц.

При проведении расходной накладной необходимо в первую очередь контролировать хватает ли товара вообще. Если нет - выдавать соответствующее предупреждение с указанием количества нехватки и не позволять проводить документ.

Списание себестоимости товаров должно быть организовано по складам, в зависимости от текущего значения их приоритета и выбранного в документе склада. В первую очередь товар списывается со склада, указанного в шапке документа, далее со склада с минимальным приоритетом, потом со следующего склада с большим приоритетом и т.д. пока не спишется все необходимое количество.

*Себестоимость* товаров рассчитывается как *средняя* по складу.

Поступление товара происходит на один выбранный пользователем в документе «Приходная накладная» склад.

Необходимо построить отчет по движениям товаров за период по количеству и сумме.
![[Pasted image 20240927112604.png]]

---
### Решение:

##### Регистр сведений: **ПриоритетыСкладов**
Измерения: *Склад*
Ресурсы: *Приоритет*

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *Склад*
Ресурсы: *Количество*, *Себестоимость*

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*, *Склад*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Себестоимость = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;

КонецПроцедуры
```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*, *Склад*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
		
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = 
		|	ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура.Представление КАК												НоменклатураПредставление,
		|	втТЧТовары.Номенклатура КАК																		Номенклатура,
		|	втТЧТовары.Количество КАК																			Количество,
		|	ОстаткиНоменклатурыОстатки.Склад КАК														Склад,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК				КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СебестоимостьОстаток, 0) КАК		СебестоимостьОстаток,
		|	ВЫБОР
		|		КОГДА ОстаткиНоменклатурыОстатки.Склад = &Склад
		|			ТОГДА -1
		|		ИНАЧЕ ЕСТЬNULL(ПриоритетыСкладовСрезПоследних.Приоритет, 100)
		|	КОНЕЦ КАК																									Приоритет
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары)) КАК ОстаткиНоменклатурыОстатки
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетыСкладов.СрезПоследних(&МоментВремени, ) КАК 
		|			ПриоритетыСкладовСрезПоследних
		|			ПО ОстаткиНоменклатурыОстатки.Склад = ПриоритетыСкладовСрезПоследних.Склад
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Мало товара %1", ВыборкаНоменклатура.НоменклатураПредставление);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаПартия = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаПартия.Следующий() И ОсталосьСписать > 0 Цикл
			
			Количество = Мин(ОсталосьСписать, ВыборкаПартия.КоличествоОстаток);
			
			Если Количество = ВыборкаПартия.КоличествоОстаток Тогда
				Себестоимость = ВыборкаПартия.СебестоимостьОстаток;
			Иначе
				Себестоимость = Количество  / ВыборкаПартия.КоличествоОстаток
				 * ВыборкаПартия.СебестоимостьОстаток ;
			КонецЕсли;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаПартия);
			Движение.Период = Дата;
			Движение.Количество = Количество;
			Движение.Себестоимость = Себестоимость;
			
			ОсталосьСписать = ОсталосьСписать - Количество;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
```