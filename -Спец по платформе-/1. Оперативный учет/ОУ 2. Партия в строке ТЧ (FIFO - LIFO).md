ЗАНЯТИЕ 10. КОНТРОЛЬ ОСТАТКОВ В РАЗРЕЗЕ ПАРТИЙ
https://www.youtube.com/watch?v=MyoJeDH63Zo&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=10

ЗАНЯТИЕ 11. ОТЧЕТ ПО ПРОДАЖАМ
https://www.youtube.com/watch?v=ftDwCsulOi4&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=11

---
### Задача:

Компания занимается оптовой торговлей. Поступление товаров отражается документом «*Приходная накладная*», продажа - «*Расходная накладная*». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Складской учет товаров не ведется.

В документе «Расходная накладная», в табличной части для каждого товара пользователь указывает *партию*, которую необходимо списать. В том случае, если товара по указанной партии не хватает, документ не проводится и выводится соответствующее сообщение о нехватке.

Необходимо построить отчет по анализу продаж товаров за период.
![[Pasted image 20240927112251.png]]

**Прибыль** рассчитывается как: *Сумма продаж* - *Себестоимость*

**Интервал** - расчетный показатель средний интервал отгрузок (в днях). 
Рассчитывается как: (*Дата первой отгрузки* - *Дата последней отгрузки*) / *Количество отгрузок*
В том случае, когда отгрузка была только одна, то в колонке Интервал выводится *разовая*.

**Срок** - расчетный показатель срок последней отгрузки (в днях), определяющий, как давно прошла последняя отгрузка. 
Рассчитывается как: *Конец периода отчета* - *Дата последнего документа отгрузки*

---
### Решение:
##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *Партия*
Ресурсы: *Количество*, *Стоимость*

##### Регистр накопления: **Продажи**
Измерения: *Номенклатура*
Ресурсы: *Количество*, *СуммаПродажи*, *Себестоимость*

##### Регистр сведений: **УчетнаяПолитика**
Измерения: 
Ресурсы: *МетодСписанияСебестоимости*

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		Движение = Движения.ОстаткиНоменклатуры.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Партия = Ссылка;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Стоимость = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;

КонецПроцедуры
```

##### Документ **РасходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура*
- *Количество*
- *Цена*
- *Сумма*
- *Партия*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия",       "Партия");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Партия,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Партия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.ВидНоменклатуры КАК ВидНоменклатуры,
		|	втТЧТовары.Партия КАК Партия,
		|	втТЧТовары.Партия.Представление КАК ПартияПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	втТЧТовары.Сумма КАК СуммаПродажи,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, Партия) В
		|					(ВЫБРАТЬ
		|						втТЧТовары.Номенклатура КАК Номенклатура,
		|						втТЧТовары.Партия КАК Партия
		|					ИЗ
		|						втТЧТовары КАК втТЧТовары
		|					ГДЕ
		|						втТЧТовары.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар))) КАК 
		|						ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И втТЧТовары.Партия = ОстаткиНоменклатурыОстатки.Партия
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(СуммаПродажи)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		
		ИтогоСебестоимость = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Товар Тогда
			
				Если ВыборкаДетальныеЗаписи.Количество > ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
					Отказ = Истина;
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по партии %2 в количестве %3",
					ВыборкаДетальныеЗаписи.НоменклатураПредставление, 
					ВыборкаДетальныеЗаписи.ПартияПредставление,
					ВыборкаДетальныеЗаписи.Количество - ВыборкаДетальныеЗаписи.КоличествоОстаток);
					Сообщение.Сообщить();
				КонецЕсли;
				
				Если Отказ Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВыборкаДетальныеЗаписи.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
					Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
				Иначе
					Себестоимость = ВыборкаДетальныеЗаписи.Количество / 
					ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьОстаток;
				КонецЕсли;
				
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
				Движение.Стоимость = Себестоимость;
				
				ИтогоСебестоимость = ИтогоСебестоимость + Движение.Стоимость;
			КонецЕсли;
		КонецЦикла;
		
		Движение = Движения.Продажи.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
		Движение.Период = Дата;
		Движение.Себестоимость = ИтогоСебестоимость;
		
	КонецЦикла;
	
КонецПроцедуры
```