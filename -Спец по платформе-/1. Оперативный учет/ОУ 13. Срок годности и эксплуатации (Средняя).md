ЗАНЯТИЕ 24. УЧЕТ СРОКОВ ГОДНОСТИ И ЭКСПЛУАТАЦИИ
https://www.youtube.com/watch?v=L7nwSDBFQq8&list=PLh28ogpgRJUPQDnx1uV9p19jLbpyBa3ua&index=24

---
### Задача:

В компании используется оборудование, имеющее ограничения к эксплуатации. Для каждого устройства устанавливается срок годности и срок использования. Срок годности считается с момента приобретения оборудования и указывается в документе поступления, а срок эксплуатации с момента его ввода в эксплуатацию и указывается непосредственно для оборудования и не меняется. Поступление оборудования отражается документом «Приходная накладная». Через некоторое время оборудование вводится в эксплуатацию документом «Ввод в эксплуатацию».

При передаче оборудования в эксплуатацию в первую очередь передастся оборудование, у которого минимальный срок годности. В документе «Ввод в эксплуатацию» указывается перечень и количество передаваемого оборудования. При проведении должна происходить проверка на наличие оборудования и его срок годности. В том случае, если срок годности истек или оборудования недостаточно, документ не проводится и выдается соответствующее сообщение.

Раз в месяц формируется регламентный документ «Выбытие оборудования», который при проведении проверяет эксплуатируемое оборудование и, если оно негодно, то списывает его. При проверке определяется, вышел ли срок эксплуатации устройства с момента его ввода в эксплуатацию. Если срок эксплуатации истек или истек срок годности, то оборудование должно быть списано. Кроме того, этот же документ должен списывать еще не введенное в эксплуатацию оборудование, но срок годности, которого уже истек.

Учет оборудования в разрезе складов не ведется. Себестоимость оборудования рассчитывается как средняя.

Необходимо создать отчет о состоянии эксплуатируемого оборудования на выбранную дату.

![[Pasted image 20240930104536.png]]

---
### Решение:

##### Регистр накопления: **ОстаткиНоменклатуры**
Измерения: *Номенклатура*, *СрокГодности* (Дата)
Ресурсы: *Количество*, *Стоимость*

##### Регистр накопления: **ОборудованиеВЭксплуатации**
Измерения: 
- *Номенклатура*
- *СрокГодности* (Дата)
- *СрокЭксплуатации* (Дата)
Ресурсы: *Количество*, *Стоимость*

##### Документ **ПриходнаяНакладная**
Реквизиты: *СуммаПоДокументу*
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*
- *Цена*
- *Сумма*
- *СрокГодностиДней* (Число)

Модуль объекта:
```bsl

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Стоимость,
		|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней) 
		|	КАК СрокГодности,
		|	&Период КАК Период
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ПриходнаяНакладнаяСписокНоменклатуры.СрокГодностиДней)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	 
КонецПроцедуры

```

##### Документ **ВводВЭксплуатацию**
Реквизиты: 
Табличная часть: *СписокНоменклатуры*. Реквизиты:
- *Номенклатура* (СправочникСсылка.Номенклатура)
- *Количество*

Модуль объекта:
```bsl
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
	
	НачалоДняДокумента = НачалоДня(Дата);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(НачалоДняДокумента,));
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней,
		|	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ втТЧТовары
		|ИЗ
		|	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
		|ГДЕ
		|	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура,
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТЧТовары.Номенклатура КАК Номенклатура,
		|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		|	втТЧТовары.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ДОБАВИТЬКДАТЕ(&НачалоДня, ДЕНЬ, втТЧТовары.СрокЭксплуатацииДней) КАК СрокЭксплуатации,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
		|	&Период КАК Период,
		|	втТЧТовары.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней
		|ИЗ
		|	втТЧТовары КАК втТЧТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							втТЧТовары.Номенклатура КАК Номенклатура
		|						ИЗ
		|							втТЧТовары КАК втТЧТовары)
		|					И СрокГодности >= &НачалоДня) КАК ОстаткиНоменклатурыОстатки
		|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодности
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	МАКСИМУМ(СрокЭксплуатацииДней)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.СрокЭксплуатацииДней = 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("В карточке номенклатуры %1 не задан срок эксплуатации",
							  ВыборкаНоменклатура.НоменклатураПредставление);
			Сообщение.Сообщить();
		КонецЕсли;			
		
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			ВыборкаНоменклатура.НоменклатураПредставление, ВыборкаНоменклатура.Количество - 
			ВыборкаНоменклатура.КоличествоОстаток);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоКСписанию = ВыборкаНоменклатура.Количество;
		
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоКСписанию > 0 Цикл
			
			Количество = Мин(КоличествоКСписанию, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			
			Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			Иначе
				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток / 
				ВыборкаДетальныеЗаписи.КоличествоОстаток * Количество;
			КонецЕсли;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
			Движение.Количество = Количество;
			Движение.Стоимость = Себестоимость;
			
			Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьПриход();
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
			Движение.Количество = Количество;
			Движение.Стоимость = Себестоимость;			
			
			КоличествоКСписанию = КоличествоКСписанию - Количество;
			
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры
```

##### Документ **ВыбытиеОборудования**
Модуль объекта:
```bsl

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
	Движения.Записать();
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОборудованиеВЭксплуатации.Записывать = Истина;
	
	НачалоДняДокумента = НачалоДня(Дата);
	
	Блокировка = Новый БлокировкаДанных;	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента - 1));
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СтоимостьОстаток КАК Стоимость,
		|	&Период КАК Период
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, СрокГодности < &НачалоДня) 
		|	КАК ОстаткиНоменклатурыОстатки";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОборудованиеВЭксплуатации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(,НачалоДняДокумента - 1));
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОборудованиеВЭксплуатации");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;	
	ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(,НачалоДняДокумента - 1));
	
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОборудованиеВЭксплуатацииОстатки.Номенклатура КАК Номенклатура,
		|	ОборудованиеВЭксплуатацииОстатки.СрокГодности КАК СрокГодности,
		|	ОборудованиеВЭксплуатацииОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ОборудованиеВЭксплуатацииОстатки.КоличествоОстаток КАК Количество,
		|	ОборудованиеВЭксплуатацииОстатки.СтоимостьОстаток КАК Стоимость,
		|	&Период КАК Период
		|ИЗ
		|	РегистрНакопления.ОборудованиеВЭксплуатации.Остатки(
		|			&МоментВремени,
		|			СрокГодности < &НачалоДня
		|				ИЛИ СрокЭксплуатации < &НачалоДня) КАК ОборудованиеВЭксплуатацииОстатки";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДняДокумента);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОборудованиеВЭксплуатации.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);		
	КонецЦикла;
		
КонецПроцедуры

```
