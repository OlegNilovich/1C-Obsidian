###### Первый способ: глобальная процедура ЗаписатьJSON()
```bsl
&НаКлиенте
Процедура Создать_JSON(Команда)

	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Проводник.Заголовок = "Выберите каталог для выгрузки данных";

	Если НЕ Проводник.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	МассивТоваров = Новый Массив;
	
	Для каждого ТекСтрока Из СписокТоваров Цикл
		СтруктураТовара = Новый Структура;
		СтруктураТовара.Вставить("Наименование", ТекСтрока.Наименование);
		СтруктураТовара.Вставить("ВидНоменклатуры", Строка(ТекСтрока.ВидНоменклатуры));
		СтруктураТовара.Вставить("Артикул", ТекСтрока.Артикул);   
		СтруктураТовара.Вставить("ЕдиницаИзмерения", Строка(ТекСтрока.ЕдиницаИзмерения));   
		СтруктураТовара.Вставить("СтавкаНДС", Строка(ТекСтрока.СтавкаНДС));   
		МассивТоваров.Добавить(СтруктураТовара);
	КонецЦикла;                                               
	
	ПутьКФайлу = Проводник.Каталог + "\Список товаров.json";
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлу,,, Новый ПараметрыЗаписиJSON(, Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, МассивТоваров); 
	ЗаписьJSON.Закрыть();
	
	Текст = "Данные выгружены успешно!";
	Нажатие	= Новый ОписаниеОповещения("ОткрытьКаталогВыгрузкиДанных", ЭтотОбъект, Проводник.Каталог);
	Пояснение = "Нажмите, чтобы открыть каталог выгрузки данных";
	Картинка = БиблиотекаКартинок.Оповещения;
	Статус = СтатусОповещенияПользователя.Важное;
	ПоказатьОповещениеПользователя(Текст, Нажатие, Пояснение, Картинка, Статус, Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогВыгрузкиДанных(Каталог) Экспорт
	ЗапуститьПриложение(Каталог);
КонецПроцедуры
```
###### Второй способ: метод объекта "ЗаписьJSON"
```bsl
&НаКлиенте
Процедура Создать_JSON(Команда)

	ПутьКФайлу = МестоВыгрузки + "\" + "JSON-Файл"  + ".json";
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ОткрытьФайл(ПутьКФайлу,,, Новый ПараметрыЗаписиJSON(, Символы.Таб));

	ЗаписьJSON.ЗаписатьНачалоМассива();

	Для каждого Товар Из Товары Цикл
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();

			ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
			ЗаписьJSON.ЗаписатьЗначение(Товар.Наименование);

			ЗаписьJSON.ЗаписатьИмяСвойства("ВидНоменклатуры");
			ЗаписьJSON.ЗаписатьЗначение(Строка(Товар.ВидНоменклатуры));
	
			ЗаписьJSON.ЗаписатьИмяСвойства("Артикул");
			ЗаписьJSON.ЗаписатьЗначение(Товар.Артикул);


			ЗаписьJSON.ЗаписатьИмяСвойства("ЕдиницаИзмерения");
			ЗаписьJSON.ЗаписатьЗначение(Строка(Товар.ЕдиницаИзмерения));
		
			ЗаписьJSON.ЗаписатьИмяСвойства("СтавкаНДС");
			ЗаписьJSON.ЗаписатьЗначение(Строка(Товар.СтавкаНДС));
			
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	КонецЦикла;   
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	ЗаписьJSON.Закрыть();
	ПоказатьОповещениеПользователя("Данные выгружены успешно!");
	ЗапуститьПриложение(МестоВыгрузки);
	
КонецПроцедуры
```