### Задача:

Требуется создать обработку с тремя командами:
Прочитать - читает данные о товарах из текстового файла и заполняет таблицу обработки
Сопоставить - Сопоставляет товары файла и базы данных. Заполняет поле "Уже есть в базе"
Записать - Создает новые товары в базе данных, если для них не нашлось сопоставлений.
![[Pasted image 20240827034213.png]]
Пример текстового файла:
```
Наименование;Артикул;СтавкаНДС;ЕдиницаИзмерения;ВидНоменклатуры
Товар из внешнего источника №1;А-11111;20;Шт;Разное
Товар из внешнего источника №2;А-22222;20;Шт;Разное
Товар из внешнего источника №3;А-33333;20;Шт;Разное
Товар из внешнего источника №4;А-44444;20;Шт;Разное
Товар из внешнего источника №5;А-55555;20;Шт;Разное
```
### Решение:
1. Создаем обработку "АлгоритмЗаписиВнешнихДанныхВБазу1С"
2. Создаем форму обработки и открываем редактор формы
3. Создаем реквизит "Товары" тип (ТаблицаЗначений)
4. Создаем колонки реквизита "Товары":
- Наименование (Строка)
- Артикул (Строка)
- СтавкаНДС (Строка)
- ЕдиницаИзмерения (Строка)
- ВидНоменклатуры (Строка)
- Ссылка (СправочникСсылка.Номенклатура)
5. Переносим реквизит "Товары" на форму
6. Меняем заголовок элемента "ТоварыСсылка" на "Уже есть в базе"
![[Pasted image 20240827033018.png]]
7. Создаем три команды: "Прочитать", "Сопоставить" и "Записать"
![[Pasted image 20240827033332.png]]
8. Создаем обработчики действий команд и переходим в модуль формы:
```bsl
&НаКлиенте
Асинх Процедура Прочитать(Команда)
	
	Товары.Очистить();
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Проводник.Заголовок = "Выберите файл на диске";
	Проводник.Фильтр = "Текстовый документ (*.txt;*.csv)| *.txt;*.csv";	
	
	ВыбранныеФайлы = ЖДАТЬ Проводник.ВыбратьАсинх();
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат
	Иначе
		ПутьКФайлу = ВыбранныеФайлы[0];
	КонецЕсли;
		
	Текст = Новый ТекстовыйДокумент;
	Ждать Текст.ПрочитатьАсинх(ПутьКФайлу, КодировкаТекста.UTF8);
	
	Для НомерСтроки = 2 По Текст.КоличествоСтрок() Цикл
		ТекСтрока	= Текст.ПолучитьСтроку(НомерСтроки);
		ЧастиСтроки = СтрРазделить(ТекСтрока, ";");
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Наименование			= ЧастиСтроки[0];
		НоваяСтрока.Артикул					= ЧастиСтроки[1];
		НоваяСтрока.СтавкаНДС				= ЧастиСтроки[2];
		НоваяСтрока.ЕдиницаИзмерения	= ЧастиСтроки[3];
		НоваяСтрока.ВидНоменклатуры	= ЧастиСтроки[4];
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура Сопоставить(Команда)
	СопоставитьНаСервере();
КонецПроцедуры
&НаСервере
Процедура СопоставитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК				Ссылка,
		|	Номенклатура.Наименование КАК		Наименование
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Наименование В(&МассивНаименований)";
	Запрос.УстановитьПараметр("МассивНаименований", Товары.Выгрузить(,"Наименование"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("Совпадений не найдено");
		Возврат;
	Иначе
		Сообщить("Найдены некоторые совпадения");
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураПоиска = Новый Структура("Наименование", Выборка.Наименование);
		НайденныеСтроки = Товары.НайтиСтроки(СтруктураПоиска);
		НайденныеСтроки[0].Ссылка = Выборка.Ссылка ;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервере();
КонецПроцедуры
&НаСервере
Процедура ЗаписатьНаСервере()
	
	Для каждого Товар Из Товары Цикл
		
		Если Товар.Ссылка.Пустая() Тогда
			
			НовыйТовар = Справочники.Номенклатура.СоздатьЭлемент();
			НовыйТовар.Наименование		= Товар.Наименование;
			НовыйТовар.Артикул					= Товар.Артикул;  
			НовыйТовар.СтавкаНДС				= Перечисления.СтавкиНДС.НДС20;
			НовыйТовар.ЕдиницаИзмерения	= Справочники.КлассификаторЕдиницИзмерения.шт;
			НовыйТовар.ВидНоменклатуры	= 
			Справочники.ВидыНоменклатуры.НайтиПоНаименованию(Товар.ВидНоменклатуры);
			
			Попытка
				НовыйТовар.ОбменДанными.Загрузка = Истина;
				НовыйТовар.Записать();  	       
				Товар.Ссылка = НовыйТовар.Ссылка;
			Исключение
				Сообщить("Не удалось создать товар");
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
```