### Задача:
Требуется создать возможность, для каждого элемента справочника "Номенклатура", хранить присоединяемые файлы. Переход к списку файлов будет осуществляться через гиперссылку на панели навигации. Файлы можно добавлять, просматривать и удалять.
![[Pasted image 20240827015029.png]]
### Решение:
###### Справочник:
1. Создаем справочник "ФайлыНоменклатуры". Подчиненный справочнику "Номенклатура"
2. Создаем реквизит: "ДанныеФайла" тип (ХранилищеЗначения)
3. Создаем реквизит: "РазмерФайла" тип (Число, 10)
4. Создаем реквизит: "Расширение" тип (Строка, 10)
5. Создаем две формы: "ФормаЭлемента" и "ФормаСписка"
![[Pasted image 20240827000435.png]]

###### Форма Элемента:
1. Открываем окно редактирования формы элемента
2. Создаем реквизит "АдресДанных" (Строка)
![[Pasted image 20240827001139.png]]
3. Переносим реквизит "Владелец" в окно элементов формы
![[Pasted image 20240827001459.png]]
4. Создаем два обработчика событий формы: "ПриОткрытии" и "ПередЗаписьюНаСервере"
![[Pasted image 20240827001709.png]]
5. Модуль формы элемента:
```bsl
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресДанных) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресДанных);
		ТекущийОбъект.ДанныеФайла = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	КонецЕсли; 
	
КонецПроцедуры


&НаКлиенте
Асинх Процедура ПриОткрытии(Отказ)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите файл";
	ОписаниеФайла = ЖДАТЬ ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога, УникальныйИдентификатор);
	
	Если ОписаниеФайла = Неопределено Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	АдресДанных					= ОписаниеФайла.Адрес;   
	Объект.Наименование	= ОписаниеФайла.СсылкаНаФайл.Имя;
	Объект.Расширение		= ОписаниеФайла.СсылкаНаФайл.Расширение;
	Объект.РазмерФайла		= ЖДАТЬ ОписаниеФайла.СсылкаНаФайл.Файл.РазмерАсинх() / 1024;

КонецПроцедуры
```

###### Форма Списка:
1. Открываем окно редактирования формы списка
2. Команды => Стандартные => Форма => Переносим команду "Создать" на командную панель
3. Свойства => Отображение => Картинка и текст
![[Pasted image 20240827004103.png]]
4. Переносим команду "Скопировать" на командную панель и отключаем "Видимость"
![[Pasted image 20240827004316.png]]
5. Создаем две команды формы: "ОткрытьФайл" и "СохранитьНаДиск"
6. Свойства => Отображение => Картинка и текст.
7. Свойства => Картинка => Выбираем подходящие картинки для обеих команд
![[Pasted image 20240827004835.png]]
8. Создаем обработчики действия команд и переходим в модуль формы:
```bsl
&НаКлиенте
Асинх Процедура ОткрытьФайл(Команда)
	СсылкаНаФайл = Элементы.Список.ТекущаяСтрока;
	Если СсылкаНаФайл <> Неопределено Тогда
		ДанныеФайлаСтруктура = ПолучитьДанныеФайла(СсылкаНаФайл);
		ДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайлаСтруктура.АдресДанных);
		Расширение = ДанныеФайлаСтруктура.Расширение;
		ИмяФайла = ЖДАТЬ КаталогВременныхФайловАсинх()  + "demo" + Расширение;
		ДанныеФайла.ЗаписатьАсинх(ИмяФайла);
		ЗапуститьПриложениеАсинх(ИмяФайла)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаДиск(Команда)
	СсылкаНаФайл = Элементы.Список.ТекущаяСтрока;
	Если СсылкаНаФайл <> Неопределено Тогда
		Файл = ПолучитьДанныеФайла(СсылкаНаФайл);
		ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
		ПараметрыДиалога.Заголовок = "Выберите каталог для сохранения файла";
		НачатьПолучениеФайлаССервера(Файл.АдресДанных, Файл.ИмяФайла, ПараметрыДиалога);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(СсылкаНаФайл)
	Файл						= СсылкаНаФайл.ПолучитьОбъект();
	ДвоичныеДанные	= Файл.ДанныеФайла.Получить();
	АдресДанных			= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	СтруктураВозврат	= Новый Структура;
	СтруктураВозврат.Вставить("АдресДанных", АдресДанных);
	СтруктураВозврат.Вставить("ИмяФайла", Файл.Наименование);
	СтруктураВозврат.Вставить("Расширение", Файл.Расширение);
	Возврат СтруктураВозврат;
КонецФункции
```