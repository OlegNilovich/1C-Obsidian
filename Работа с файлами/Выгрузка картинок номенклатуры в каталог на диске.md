### Задача:

Создать обработку для выгрузки картинок номенклатуры в каталог на диске.

Описание обработки:
Фильтр по группе номенклатуры (расположен на форме)
Каталог выгрузки (указан на форме)
Выгрузка осуществляется только по тем номенклатурам, которые есть на свободном остатке.
Для каждой номенклатуры должна быть создана отдельная папка в каталоге выгрузки (название папки должно соответствовать названию номенклатуры)

### Решение:

```bsl

&НаКлиенте
АСИНХ Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Если ЖДАТЬ Проводник.ВыбратьАсинх() = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КаталогВыгрузки = Проводник.Каталог;
	
КонецПроцедуры

&НаКлиенте
АСИНХ Процедура ЗагрузитьКартинкиВКаталог()

	Товары = КартинкиТоваров();
	
	Для каждого Товар Из Товары Цикл
		
		НазваниеНоменклатурыБезКовычек = СтрЗаменить(Товар.НазваниеНоменклатуры, """", "");
		КаталогТовара = КаталогВыгрузки + "/" + НазваниеНоменклатурыБезКовычек;
		СоздатьКаталогАсинх(КаталогТовара);
		
		Для каждого Файл Из Товар.ФайлыНоменклатуры Цикл
			ИмяФайлаСРасширением = КаталогТовара + "/" + Файл.НазваниеФайла + Файл.Расширение;
			ЖДАТЬ ПолучитьФайлССервераАсинх(Файл.НавигационнаяСсылка, ИмяФайлаСРасширением);
		КонецЦикла;
	
	КонецЦикла;
	
	ЗапуститьПриложениеАсинх(КаталогВыгрузки);

КонецПроцедуры

&НаСервере
Функция КартинкиТоваров()

	МассивРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураПрисоединенныеФайлы.Владелец.Представление КАК		НазваниеНоменклатуры,
		|	НоменклатураПрисоединенныеФайлы.Представление КАК						НазваниеФайла,
		|	НоменклатураПрисоединенныеФайлы.Владелец КАК								НоменклатураСсылка,
		|	НоменклатураПрисоединенныеФайлы.Ссылка КАК									ФайлСсылка,
		|	НоменклатураПрисоединенныеФайлы.Расширение КАК							Расширение
		|ИЗ
		|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
		|		ПО НоменклатураПрисоединенныеФайлы.Владелец = ТоварыНаСкладахОстатки.Номенклатура
		|ГДЕ
		|	НоменклатураПрисоединенныеФайлы.Владелец.Родитель В ИЕРАРХИИ(&Родитель)
		|ИТОГИ ПО
		|	НазваниеНоменклатуры";
	Запрос.УстановитьПараметр("Родитель", ГруппаНоменклатуры);
	
	ВыборкаТовар = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовар.Следующий() Цикл
		
		СтруктураТовара = Новый Структура;
		СтруктураТовара.Вставить("НазваниеНоменклатуры", ВыборкаТовар.НазваниеНоменклатуры);
		
		ФайлыНоменклатуры = Новый Массив;
		
		ВыборкаФайлы = ВыборкаТовар.Выбрать();
		Пока ВыборкаФайлы.Следующий() Цикл
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ВыборкаФайлы.ФайлСсылка, "ДанныеФайла");
			СтруктураКартинки = Новый Структура;
			СтруктураКартинки.Вставить("НазваниеФайла", ВыборкаФайлы.НазваниеФайла);
			СтруктураКартинки.Вставить("Расширение", ВыборкаФайлы.Расширение);
			СтруктураКартинки.Вставить("НавигационнаяСсылка", НавигационнаяСсылка);
			ФайлыНоменклатуры.Добавить(СтруктураКартинки);
		КонецЦикла;
		
		СтруктураТовара.Вставить("ФайлыНоменклатуры", ФайлыНоменклатуры);
		МассивРезультат.Добавить(СтруктураТовара);
	
	КонецЦикла;
	
	Возврат МассивРезультат;

КонецФункции

```