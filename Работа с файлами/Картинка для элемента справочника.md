### Задача:
Требуется создать возможность хранить изображение товара для элементов справочника "Номенклатура". Изображение будет храниться в реквизите "Картинка"
![[Pasted image 20240826072544.png]]
### Решение:
Создаем реквизит: "Картинка" тип (ХранилищеЗначения) для справочника "Номенклатура"
Создаем обработку: "КартинкаДляНоменклатуры"
Создаем две формы: "ФормаСпискаЭлементов" (Главная) и "ФормаЭлемента"
###### Справочник "Номенклатура"
Создаем реквизит "Картинка" тип (ХранилищеЗначения)
![[Pasted image 20240826071448.png]]

---
###### Форма списка элементов:
1. Открываем редактор формы списка элементов
2. Создаем реквизит "Список" тип (ДинамическийСписок)
3. Указываем основную таблицу реквизита: "Справочник.Номенклатура"
4. Переносим реквизит "Список" в окно элементов формы
![[Pasted image 20240826060651.png]]
5. Создаем событие "Выбор" для элемента формы "Список"
![[Pasted image 20240826061012.png]]
6. Модуль формы:
```bsl
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПутьКФорме = "Обработка.КартинкаДляНоменклатуры.Форма.ФормаЭлемента";
	СтруктураПараметров = Новый Структура("Ключ", ВыбраннаяСтрока);
	ОткрытьФорму(ПутьКФорме, СтруктураПараметров);
КонецПроцедуры
```

---
###### Форма элемента:
1. Открываем редактор формы элемента
2. Создаем реквизит "Объект" тип (СправочникОбъект.Номенклатура)
3. Создаем реквизит "АдресКартинки" тип (Строка)
![[Pasted image 20240826070123.png]]
4. Создаем элемент "ГруппаШапка". Без заголовка. Группировка: Горизонтальная всегда
![[Pasted image 20240826070339.png]]
5. Внутри элемента "ГруппаШапка" создаем два элемента группы:
- Элемент "ГруппаПоля". Без заголовка. Группировка: Вертикальная всегда
- Элемент "ГруппаКартинка". Без заголовка. Группировка: Горизонтальная всегда

6. Для элемента "Форма" создаем три события:
- "ПриСозданииНаСервере"
- "ПриЧтенииНаСервере"
- "ПередЗаписьюНаСервере"
![[Pasted image 20240826070754.png]]
7. Модуль формы:
```bsl

//========================= События формы ==================================
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Элементы.Найти("АдресКартинки") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("АдресКартинки", Тип("ПолеФормы"), Элементы.ГруппаКартинка);
		НовыйЭлемент.ПутьКДанным						= "АдресКартинки";
		НовыйЭлемент.Вид										= ВидПоляФормы.ПолеКартинки;
		НовыйЭлемент.Гиперссылка							= Ложь;
		НовыйЭлемент.РастягиватьПоГоризонтали	= Ложь;
		НовыйЭлемент.РастягиватьПоВертикали		= Ложь;
		НовыйЭлемент.Ширина								= 35;
		НовыйЭлемент.Высота									= 10;
		НовыйЭлемент.ПоложениеЗаголовка			= ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовыйЭлемент.РазмерКартинки					= РазмерКартинки.Пропорционально;
		НовыйЭлемент.ТекстНевыбраннойКартинки	= "Изображение товара";
	КонецЕсли;
	
	Если Элементы.Найти("КомандыКартинки") = Неопределено Тогда
		КомандыКартинки = 
		ЭтаФорма.Элементы.Добавить("КомандыКартинки", Тип("ГруппаФормы"), Элементы.ГруппаКартинка);
		КомандыКартинки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		КомандыКартинки.Отображение = ОтображениеОбычнойГруппы.Нет;
		КомандыКартинки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		КомандыКартинки.ОтображатьЗаголовок	= ЛОЖЬ; 
	КонецЕсли;
	
	НоваяКоманда	= Команды.Добавить("ЗагрузитьКартинку");
	НоваяКоманда.Действие = "ЗагрузитьКартинку";
	НоваяКнопка = Элементы.Добавить("ЗагрузитьКартинку", Тип("КнопкаФормы"), КомандыКартинки);
	НоваяКнопка.ИмяКоманды	= "ЗагрузитьКартинку";
	НоваяКнопка.Заголовок		= "Загрузить";
	НоваяКнопка.Картинка		= БиблиотекаКартинок.СохранитьФайл;
	НоваяКнопка.Отображение	= ОтображениеКнопки.КартинкаИТекст;
	
	НоваяКоманда = Команды.Добавить("СохранитьКартинку");
	НоваяКоманда.Действие = "СохранитьКартинку";
	НоваяКнопка = Элементы.Добавить("СохранитьКартинку", Тип("КнопкаФормы"), КомандыКартинки);
	НоваяКнопка.ИмяКоманды 	= "СохранитьКартинку";
	НоваяКнопка.Заголовок 		= "Сохранить";
	НоваяКнопка.Картинка 		= БиблиотекаКартинок.Записать;
	НоваяКнопка.Отображение	= ОтображениеКнопки.КартинкаИТекст;
	
	НоваяКоманда = Команды.Добавить("УдалитьКартинку");
	НоваяКоманда.Действие = "УдалитьКартинку";
	НоваяКнопка = Элементы.Добавить("УдалитьКартинку", Тип("КнопкаФормы"), КомандыКартинки);
	НоваяКнопка.ИмяКоманды	= "УдалитьКартинку";
	НоваяКнопка.Заголовок		= "Удалить";
	НоваяКнопка.Картинка		= БиблиотекаКартинок.Удалить;
	НоваяКнопка.Отображение	= ОтображениеКнопки.КартинкаИТекст;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресКартинки);
		ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	ИначеЕсли ПустаяСтрока(АдресКартинки) Тогда
		ТекущийОбъект.Картинка = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект, "Картинка");
КонецПроцедуры


//========================= Команды картинки ================================
&НаКлиенте
АСИНХ Процедура ЗагрузитьКартинку()
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Проводник.МножественныйВыбор				= Ложь;
	Проводник.ПредварительныйПросмотр		= Истина;
	Проводник.ПроверятьСуществованиеФайла	= Истина;
	Проводник.Заголовок = "Выберите изображение";
	Проводник.Фильтр = "Изображение | *.jpg; *.png; *.bmp; *.jpeg; *.jfif";
	ПутьКФайлу = ЖДАТЬ Проводник.ВыбратьАсинх();
	Если ПутьКФайлу[0] <> Неопределено Тогда
		ФайлВХранилище = ЖДАТЬ ПоместитьФайлНаСерверАсинх(,,, ПутьКФайлу[0], 
		УникальныйИдентификатор);
		АдресКартинки  = ФайлВХранилище.Адрес;
		Модифицированность = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
АСИНХ Процедура СохранитьКартинку()
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Если ЖДАТЬ Проводник.ВыбратьАсинх() <> Неопределено Тогда
		НавигационнаяСсылкаНаФайл = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Картинка");
		ИмяФайлаСРасширением	  = Проводник.ПолноеИмяФайла + ".jpg";
		ЖДАТЬ ПолучитьФайлССервераАсинх(НавигационнаяСсылкаНаФайл, ИмяФайлаСРасширением);
		ЗапуститьПриложениеАсинх(ИмяФайлаСРасширением);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКартинку()
	АдресКартинки = "";
КонецПроцедуры
```