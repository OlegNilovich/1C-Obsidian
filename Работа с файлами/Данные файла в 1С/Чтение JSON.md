###### Образец файла:
```json
[
	{
		"Наименование": "Тестовый товар № 1",
		"ВидНоменклатуры": "Разное",
		"Артикул": "А-1",
		"ЕдиницаИзмерения": "шт",
		"СтавкаНДС": "20%"
	},
	{
		"Наименование": "Тестовый товар № 2",
		"ВидНоменклатуры": "Разное",
		"Артикул": "А-2",
		"ЕдиницаИзмерения": "шт",
		"СтавкаНДС": "20%"
	},
	{
		"Наименование": "Тестовый товар № 3",
		"ВидНоменклатуры": "Разное",
		"Артикул": "А-3",
		"ЕдиницаИзмерения": "шт",
		"СтавкаНДС": "20%"
	}
]
```

###### Первый способ: "Десериализация"
```bsl
&НаКлиенте
Процедура Прочитать_JSON(Команда)
	
	Товары.Очистить();
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ФайлЗагрузки);
	
	СоответствияТоваров = ПрочитатьJSON(ЧтениеJSON, Истина);
	
	Для каждого Товар Из СоответствияТоваров Цикл
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Наименование			= Товар["Наименование"];
		НоваяСтрока.ВидНоменклатуры	= Товар["ВидНоменклатуры"];
		НоваяСтрока.Артикул					= Товар["Артикул"];
		НоваяСтрока.ЕдиницаИзмерения	= Товар["ЕдиницаИзмерения"];
		НоваяСтрока.СтавкаНДС				= Товар["СтавкаНДС"];
	КонецЦикла;   

	ЧтениеJSON.Закрыть();
	
КонецПроцедуры
```
###### Второй способ: "Последовательное чтение"
```bsl
&НаКлиенте
Процедура Прочитать_JSON(Команда)
	
	Товары.Очистить();
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ФайлЗагрузки);
	
	ИмяТекущегоСвойства = "";
	Пока ЧтениеJSON.Прочитать() Цикл
		
		Если ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда
			НоваяСтрока = Товары.Добавить();
		ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
			ИмяТекущегоСвойства = ЧтениеJSON.ТекущееЗначение;
		ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка
			ИЛИ ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Число
			ИЛИ ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Булево
			ИЛИ ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Null Тогда
			
			Если ИмяТекущегоСвойства = "Наименование" Тогда
				НоваяСтрока.Наименование = ЧтениеJSON.ТекущееЗначение;
			ИначеЕсли ИмяТекущегоСвойства = "ВидНоменклатуры" Тогда
				НоваяСтрока.ВидНоменклатуры = ЧтениеJSON.ТекущееЗначение;
			ИначеЕсли ИмяТекущегоСвойства = "Артикул" Тогда
				НоваяСтрока.Артикул = ЧтениеJSON.ТекущееЗначение;
			ИначеЕсли ИмяТекущегоСвойства = "ЕдиницаИзмерения" Тогда
				НоваяСтрока.ЕдиницаИзмерения = ЧтениеJSON.ТекущееЗначение;
			ИначеЕсли ИмяТекущегоСвойства = "СтавкаНДС" Тогда
				НоваяСтрока.СтавкаНДС = ЧтениеJSON.ТекущееЗначение;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеJSON.Закрыть();

КонецПроцедуры
```