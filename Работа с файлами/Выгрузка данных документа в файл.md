### Задача:
В форме списка и документа "РасходнаяНакладная", требуется создать команду "ВыгрузитьВФайл".
Которая будет выгружать данные одного или нескольких документов в выбранный каталог.
![[Pasted image 20240826045348.png]]
### Решение:
Создаем обработку "ЗагрузкаДанныхДокументаВФайл"
Создаем команду обработки: "ВыгрузитьВФайл"
Создаем формы: "СписокРасходныхНакладных" и "РасходнаяНакладная"
###### Команда "ВыгрузитьВФайл"
1. Создаем команду обработки "ВыгрузитьВФайл"
2. Свойства команды:
- Группа							=> "Командная панель формы.Важное"
- Тип параметра команды			=> "ДокументСсылка.РасходнаяНакладная"
- Режим использования параметра	=> "Множественный"
- Изменяет данные				=> Нет
![[Pasted image 20240826041039.png]]
3. Модуль команды:
```bsl
&НаКлиенте
Асинх Процедура ОбработкаКоманды(МассивСсылок, ПараметрыВыполненияКоманды)
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Проводник.Заголовок = "Выберите каталог";
	
	ВыбранныйКаталог = ЖДАТЬ Проводник.ВыбратьАсинх();
	Если ВыбранныйКаталог = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьККаталогу = ВыбранныйКаталог[0];
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивСсылок);

	Для каждого Документ Из ДанныеДокументов Цикл
		
		ЧастиСтроки	= СтрРазделить(Документ.Наименование, " ", Ложь);
		ТипДокумента	= ЧастиСтроки[0] + " " + ЧастиСтроки[1];
		Контрагент		= СтрЗаменить(Документ.Контрагент, """", "");
		Номер				= Число(ЧастиСтроки[2]);
		Дата				= ЧастиСтроки[4];
		
		ИмяФайла = СтрШаблон("%1 ''%2'' № %3 от %4", ТипДокумента, Контрагент, Номер, Дата);
		
		НовыйФайл = Новый ТекстовыйДокумент;

		Для каждого Товар Из Документ.Товары Цикл
			Количество	= Формат(Товар.Количество, "ЧДЦ=0; ЧГ=0");
			Цена				= Формат(Товар.Цена, "ЧДЦ=2; ЧРД=.; ЧГ=0");
			НоваяСтрока	= СтрШаблон("%1;%2;%3", Товар.Номенклатура, Количество, Цена);
			НовыйФайл.ДобавитьСтроку(НоваяСтрока);
		КонецЦикла;

		НовыйФайл.Записать(ПутьККаталогу + "\" + ИмяФайла  + ".txt", КодировкаТекста.UTF8);
		
	КонецЦикла; 
	
	Сообщить("Выгрузка данных завершена!");
	ЗапуститьПриложение(ПутьККаталогу);
	
КонецПроцедуры


&НаСервере
Функция ПолучитьДанныеДокументов(МассивСсылок)

	МассивРезультат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Представление КАК		Наименование,
		|	РасходнаяНакладная.Контрагент КАК			Контрагент,
		|	РасходнаяНакладная.Товары.(
		|		Номенклатура.Наименование КАК				Номенклатура,
		|		Количество КАК										Количество,
		|		Цена КАК													Цена
		|	) КАК Товары
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка В(&МассивСсылок)";
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураЗаказа = Новый Структура;
		СтруктураЗаказа.Вставить("Наименование", Выборка.Наименование);
		СтруктураЗаказа.Вставить("Контрагент", Выборка.Контрагент);
		
		МассивТоваров = Новый Массив;
		
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СтруктуруТовара = Новый Структура;
			СтруктуруТовара.Вставить("Номенклатура", ВыборкаТовары.Номенклатура);
			СтруктуруТовара.Вставить("Количество", ВыборкаТовары.Количество);
			СтруктуруТовара.Вставить("Цена", ВыборкаТовары.Цена);
			МассивТоваров.Добавить(СтруктуруТовара);
		КонецЦикла;
		
		СтруктураЗаказа.Вставить("Товары", МассивТоваров);
		МассивРезультат.Добавить(СтруктураЗаказа);
		
	КонецЦикла;
	
	Возврат МассивРезультат;
	
КонецФункции
```
###### Форма "СписокРасходныхНакладных"
1. Открываем редактор формы "СписокРасходныхНакладных"
2. Создаем реквизит "Список" с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Документ.РасходнаяНакладная
4. Переносим реквизит "Список" на форму
![[Pasted image 20240823072253.png]]
5. Переносим команду обработки "ВыгрузитьВфайл" на командную панель формы
![[Pasted image 20240826041921.png]]
###### Форма "РасходнаяНакладная"
1. Открываем редактор формы "РасходнаяНакладная"
2. Создаем реквизит "Объект" с типом (ДокументОбъект.РасходнаяНакладная)
3. Переносим необходимые реквизиты из шапки документа на форму
4. Команда "Выгрузить в файл" будет отображаться по умолчанию в командной панели
![[Pasted image 20240826042145.png]]

