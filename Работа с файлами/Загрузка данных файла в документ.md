### Задача:
В форме документа "РасходнаяНакладная", требуется создать команду "ЗагрузитьИзФайла"
Которая будет заполнять табличную часть документа "Товары" данными из текстового файла
![[Pasted image 20240826052321.png]]
### Решение:
Создаем обработку "ЗагрузкаДанныхФайлаВДокумент"
Создаем формы: "СписокРасходныхНакладных" и "РасходнаяНакладная"
###### Форма "СписокРасходныхНакладных"
1. Открываем редактор формы "СписокРасходныхНакладных"
2. Создаем реквизит "Список" с типом (ДинамическийСписок)
3. Свойства => Объект => Основная таблица => Документ.РасходнаяНакладная
4. Переносим реквизит "Список" на форму
![[Pasted image 20240823072253.png]]
###### Форма "РасходнаяНакладная"
1. Открываем редактор формы "РасходнаяНакладная"
2. Создаем реквизит "Объект" с типом (ДокументОбъект.РасходнаяНакладная)
3. Переносим необходимые реквизиты из шапки документа на форму
3. Создаем команду "ЗагрузитьИзФайла" и переносим ее в окно элементов формы
4. Модуль формы:
```bsl
&НаКлиенте
Асинх Процедура ЗагрузитьИзФайла(Команда)
	
	Проводник = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Проводник.Фильтр = "Текстовый документ | *.txt";
	
	ВыбранныйФайл = ЖДАТЬ Проводник.ВыбратьАсинх();
	Если ВыбранныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныйФайл[0];
	
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать(ПутьКФайлу, КодировкаТекста.UTF8); 
	СодержимоеФайла = Текст.ПолучитьТекст();
	ЗаполнитьТабличнуюЧастьДокумента(СодержимоеФайла);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьДокумента(СодержимоеФайла)

	Объект.Товары.Очистить();
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТипСтрока = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150));
	ТипЧисло = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10));
	ТаблицаДанных.Колонки.Добавить("Наименование", ТипСтрока);
	ТаблицаДанных.Колонки.Добавить("Количество", ТипСтрока);
	ТаблицаДанных.Колонки.Добавить("Цена", ТипЧисло);
	
	Для НомерСтроки = 1 По СтрЧислоСтрок(СодержимоеФайла) Цикл
		
		ТекСтрока		= СтрПолучитьСтроку(СодержимоеФайла, НомерСтроки);
		ЧастиСтроки	= СтрРазделить(ТекСтрока, ";");
		Наименование	= ЧастиСтроки[0];
		Количество		= ЧастиСтроки[1];
		Цена				= ЧастиСтроки[2];
		
		СтрокаТаблицы = ТаблицаДанных.Добавить();
		СтрокаТаблицы.Наименование	= Наименование;
		СтрокаТаблицы.Количество		= Количество;
		СтрокаТаблицы.Цена				= Цена;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеФайла.Наименование КАК		Наименование,
		|	ДанныеФайла.Количество КАК			Количество,
		|	ДанныеФайла.Цена КАК					Цена
		|ПОМЕСТИТЬ ВТ_ДанныеФайла
		|ИЗ
		|	&ДанныеФайла КАК ДанныеФайла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК						Номенклатура,
		|	Номенклатура.ЕдиницаИзмерения	КАК		ЕдиницаИзмерения,
		|	Номенклатура.СтавкаНДС КАК					СтавкаНДС,
		|	ВТ_ДанныеФайла.Количество КАК				Количество,
		|	ВТ_ДанныеФайла.Цена КАК						Цена
		|ИЗ
		|	ВТ_ДанныеФайла КАК ВТ_ДанныеФайла
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ВТ_ДанныеФайла.Наименование = Номенклатура.Наименование";
	Запрос.УстановитьПараметр("ДанныеФайла", ТаблицаДанных);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.Номенклатура			= Выборка.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения	= Выборка.ЕдиницаИзмерения;
		НоваяСтрока.СтавкаНДС				= Выборка.СтавкаНДС;
		НоваяСтрока.Количество				= Выборка.Количество;
		НоваяСтрока.Цена						= Выборка.Цена;
		НоваяСтрока.Сумма 						= НоваяСтрока.Количество * НоваяСтрока.Цена;
		НоваяСтрока.СуммаНДС				= НоваяСтрока.Сумма * НДСЧислом(Выборка.СтавкаНДС) / 100;
		НоваяСтрока.СуммаВсего				= НоваяСтрока.Сумма + НоваяСтрока.СуммаНДС;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НДСЧислом(СтавкаНДС)
	СтавкиНДС = Новый Соответствие;
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"), 0);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"),    0);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10"), 10);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"), 18);
	СтавкиНДС.Вставить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"), 20);
	Возврат СтавкиНДС.Получить(СтавкаНДС);
КонецФункции

```

