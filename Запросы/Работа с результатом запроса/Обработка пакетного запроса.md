###### Метод: Выполнить()
В результат запроса будет помещен последний (четвертый) запрос пакета
```bsl
&НаКлиенте
Процедура ЗапросВыполнить(Команда)
	ЗапросВыполнитьНаСервере();
КонецПроцедуры
&НаСервере
Процедура ЗапросВыполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст ="
		|////////////////////////////////////////////////////////////////////////////////
		|//////////////////////  ПЕРВЫЙ ЗАПРОС  ///////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыДокумента.Номенклатура КАК			Номенклатура,
		|	ТоварыДокумента.Количество КАК			    КоличествоПродажи
		|ПОМЕСТИТЬ													ВТ_ТоварыДокумента
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК ТоварыДокумента
		|ГДЕ
		|	ТоварыДокумента.Ссылка = &Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|//////////////////////  ВТОРОЙ ЗАПРОС  ///////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныТоваров.Цена КАК											Цена,
		|	ЦеныТоваров.Номенклатура КАК							Номенклатура
		|ПОМЕСТИТЬ															ВТ_ЦеныТоваров
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		,
		|		ВидЦен = ЗНАЧЕНИЕ(Справочник.ВидыЦен.Оптовая)
		|			И Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ЦеныТоваров
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|//////////////////////  ТРЕТИЙ ЗАПРОС  ///////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваров.КоличествоОстаток КАК			КоличествоОстаток,
		|	ОстаткиТоваров.Номенклатура КАК					Номенклатура
		|ПОМЕСТИТЬ														ВТ_ОстаткиТоваров
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_ТоварыДокумента.Номенклатура КАК Номенклатура
		|				ИЗ
		|					ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ОстаткиТоваров
		|;
		|////////////////////////////////////////////////////////////////////////////////////
		|//////////////////////  ЧЕТВЕРТЫЙ ЗАПРОС  ///////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТоварыДокумента.Номенклатура КАК								Номенклатура,
		|	ВТ_ТоварыДокумента.КоличествоПродажи КАК						КоличествоПродажи,
		|	ЕСТЬNULL(ВТ_ОстаткиТоваров.КоличествоОстаток, 0) КАК		КоличествоОстаток,
		|	ЕСТЬNULL(ВТ_ЦеныТоваров.Цена, 0) КАК								Цена
		|ИЗ
		|	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиТоваров КАК ВТ_ОстаткиТоваров
		|		ПО ВТ_ТоварыДокумента.Номенклатура = ВТ_ОстаткиТоваров.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныТоваров КАК ВТ_ЦеныТоваров
		|		ПО ВТ_ТоварыДокумента.Номенклатура = ВТ_ЦеныТоваров.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", РасходнаяНакладная);
	
	РезультатЗапроса = Запрос.Выполнить();
	Товары.Загрузить(РезультатЗапроса.Выгрузить());

КонецПроцедуры
```
###### Метод: ВыполнитьПакет()
Получаем массив результатов запроса, каждый из которых можно обработать обратившись по индексу. Доступ к данным временных таблиц  отсутствует
```bsl
&НаКлиенте
Процедура ЗапросВыполнитьПакет(Команда)
	ЗапросВыполнитьПакетНаСервере();
КонецПроцедуры
&НаСервере
Процедура ЗапросВыполнитьПакетНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Контрагент КАК		Контрагент,
		|	РасходнаяНакладная.Склад КАК 				Склад
		|ИЗ
		|	Документ.РасходнаяНакладная КАК			РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &РасходнаяНакладная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК		Номенклатура,
		|	РасходнаяНакладнаяТовары.Количество КАК			Количество
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК			РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &РасходнаяНакладная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
	Запрос.УстановитьПараметр("РасходнаяНакладная", РасходнаяНакладная);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	Пока ВыборкаШапка.Следующий() Цикл
		Сообщить("Контрагент: " + ВыборкаШапка.Контрагент + " Склад: " + ВыборкаШапка.Склад);
		
		ВыборкаТаблица = РезультатЗапроса[1].Выбрать();
		Пока ВыборкаТаблица.Следующий() Цикл
			Текст = "Номенклатура: %1 Количество: %2";
			Сообщить(СтрШаблон(Текст, ВыборкаТаблица.Номенклатура, ВыборкаТаблица.Количество));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
```
###### Метод: ВыполнитьПакетСПромежуточнымиДанными()
Получаем массив результатов запроса, каждый из которых можно обработать обратившись по индексу. Доступ к данным временных таблиц присутствует
```bsl
&НаКлиенте
Процедура ЗапросВыполнитьПакетСПромежуточнымиДанными(Команда)
	ЗапросВыполнитьПакетСПромежуточнымиДаннымиНаСервере();
КонецПроцедуры
&НаСервере
Процедура ЗапросВыполнитьПакетСПромежуточнымиДаннымиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладная.Контрагент КАК		Контрагент,
		|	РасходнаяНакладная.Склад КАК				Склад
		|ПОМЕСТИТЬ												ВТ_ШапкаДокумента
		|ИЗ
		|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
		|ГДЕ
		|	РасходнаяНакладная.Ссылка = &РасходнаяНакладная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК		Номенклатура,
		|	РасходнаяНакладнаяТовары.Количество КАК			Количество
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &РасходнаяНакладная";
	Запрос.УстановитьПараметр("РасходнаяНакладная", РасходнаяНакладная);
	
	РезультатЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	Пока ВыборкаШапка.Следующий() Цикл
		Сообщить("Контрагент: " + ВыборкаШапка.Контрагент + " Склад: " + ВыборкаШапка.Склад);
		
		ВыборкаТаблица = РезультатЗапроса[1].Выбрать();
		Пока ВыборкаТаблица.Следующий() Цикл
			Текст = "Номенклатура: %1 Количество: %2";
			Сообщить(СтрШаблон(Текст, ВыборкаТаблица.Номенклатура, ВыборкаТаблица.Количество));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
```