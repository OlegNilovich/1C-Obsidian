#ЗАПРОСЫ 

```bsl

&НаКлиенте
Процедура ПродажиТоваров(Команда)
	Табдок = Новый ТабличныйДокумент;
	РезультатОтчета = СформироватьОтчет(ТабДок);
	РезультатОтчета.Показать();
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьОтчет(Табдок)

	Макет = Обработки.РаботаСЗапросами.ПолучитьМакет("Макет1");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.Товар КАК Товар,
		|	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Товар),
		|	ПродажиОбороты.Клиент КАК Клиент,
		|	ПРЕДСТАВЛЕНИЕ(ПродажиОбороты.Клиент),
		|	ПродажиОбороты.ВыручкаОборот КАК ВыручкаОборот
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты КАК ПродажиОбороты
		|ИТОГИ
		|	СУММА(ВыручкаОборот)
		|ПО
		|	ОБЩИЕ,
		|	Товар,
		|	Клиент";
	Результат = Запрос.Выполнить();

//======================================================================

	ОбластьЗаголовок							= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы					= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьКлиентШапка						= Макет.ПолучитьОбласть("КлиентШапка");
	ОбластьЗаголовокИтогаПоСтроке	= Макет.ПолучитьОбласть("ЗаголовокИтогаПоСтроке");

	ОбластьТовар									= Макет.ПолучитьОбласть("Товар");
	ОбластьКлиент								= Макет.ПолучитьОбласть("Клиент");
	ОбластьИтогПоСтроке						= Макет.ПолучитьОбласть("ИтогПоСтроке");

	ОбластьЗаголовкаИтога					= Макет.ПолучитьОбласть("ЗаголовокИтога");
	ОбластьИтогПоКолонке					= Макет.ПолучитьОбласть("ИтогПоКолонке");
	ОбластьОбщийИтог							= Макет.ПолучитьОбласть("ОбщийИтог");
	ОбластьПодвал								= Макет.ПолучитьОбласть("Подвал");

//======================================================================

	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаТаблицы);

	ВыборкаКлиентИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Клиент");
	Пока ВыборкаКлиентИтог.Следующий() Цикл
		ОбластьКлиентШапка.Параметры.Заполнить(ВыборкаКлиентИтог);
		ТабДок.Присоединить(ОбластьКлиентШапка);
	КонецЦикла;
	
	ТабДок.Присоединить(ОбластьЗаголовокИтогаПоСтроке);

//======================================================================
		
	ВыборкаТовар = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Товар");
	Пока ВыборкаТовар.Следующий() Цикл
		ОбластьТовар.Параметры.Заполнить(ВыборкаТовар);
		ТабДок.Вывести(ОбластьТовар);

		ВыборкаКлиент = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Клиент", "ВСЕ");
		Пока ВыборкаКлиент.Следующий() Цикл
			ОбластьКлиент.Параметры.Заполнить(ВыборкаКлиент);
			ТабДок.Присоединить(ОбластьКлиент);
		КонецЦикла;
		
		ОбластьИтогПоСтроке.Параметры.Заполнить(ВыборкаТовар);
		ТабДок.Присоединить(ОбластьИтогПоСтроке);
	КонецЦикла;

//======================================================================
	
	ТабДок.Вывести(ОбластьЗаголовкаИтога);

	ВыборкаКлиентИтог.Сбросить();
	Пока ВыборкаКлиентИтог.Следующий() Цикл
		ОбластьИтогПоКолонке.Параметры.Заполнить(ВыборкаКлиентИтог);
		ТабДок.Присоединить(ОбластьИтогПоКолонке);
	КонецЦикла;

	ВыборкаОбщийИтог = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	ОбластьОбщийИтог.Параметры.Заполнить(ВыборкаОбщийИтог);
	ТабДок.Присоединить(ОбластьОбщийИтог);	
	ТабДок.Вывести(ОбластьПодвал);

	Возврат ТабДок;
	
КонецФункции
```