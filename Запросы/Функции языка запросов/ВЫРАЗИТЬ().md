#ЗАПРОСЫ

**ВЫРАЗИТЬ** – приводит значение составного типа к одному из составляющих это значение типов.
А также функцию ВЫРАЗИТЬ используют для получения результатов нужной длины и точности.

**Пример:** [[Не ограничивать получение данных через точку от полей составного ссылочного типа]]
### Пример с сайта ИТС: 
Иногда, при составлении запроса известно, какая ссылка будет находиться в том или ином поле, ссылающемся на несколько таблиц. В этом случае можно явно указать в запросе нужную таблицу для того, чтобы неявное соединение выполнялось только с указанной таблицей,  а не со всеми таблицами, на которые ссылается данное поле.

Пусть поле «*Регистратор*» регистра накопления ссылается на два документа: «**РасходнаяНакладная**» и «**ПриходнаяНакладная**», и в предложении «ГДЕ» выборка ограничивается только значениями регистратора, ссылающимися на «**РасходнуюНакладную**»:
```bsl
ВЫБРАТЬ  
    УчетНоменклатуры.Регистратор.Номер,  
    УчетНоменклатуры.Количество  
ИЗ  
    РегистрНакопления.УчетНоменклатуры КАК УчетНоменклатуры   
ГДЕ  
    УчетНоменклатуры.Регистратор ССЫЛКА Документ.РасходнаяНакладная
```
Так как в данном запросе мы четко знаем, что в результат попадут только документы, у которых регистратор ссылается на таблицу расходных накладных, мы можем несколько доработать запрос для того, чтобы он выполнялся более эффективно:
```bsl
ВЫБРАТЬ  
    ВЫРАЗИТЬ(УчетНоменклатуры.Регистратор КАК Документ.РасходнаяНакладная).Номер,  
    УчетНоменклатуры.Количество  
ИЗ  
    РегистрНакопления.УчетНоменклатуры КАК УчетНоменклатуры  
ГДЕ  
    УчетНоменклатуры.Регистратор ССЫЛКА Документ.РасходнаяНакладная
```
При исполнении такого запроса будет создано соединение лишь с таблицей «Документ.РасходнаяНакладная», вместо соединения с обеими таблицами, на которые ссылается поле «Регистратор». Это положительно повлияет на скорость выполнения запроса.

---
### Еще примеры использования:
###### Выводим общее кол-во и сумму всех заказов товара, и узнаем среднюю сумму заказа
```bsl
ВЫБРАТЬ
	КОЛИЧЕСТВО(*) КАК Количество,
	СУММА(ЗаказТовара.СуммаЗаказа) КАК СуммаЗаказа,
	ВЫРАЗИТЬ(СУММА(ЗаказТовара.СуммаЗаказа) / КОЛИЧЕСТВО(*) КАК ЧИСЛО(8, 2)) КАК СреднийЗаказ
ИЗ
	Документ.ЗаказТовара КАК ЗаказТовара
```
###### Строка неограниченной длины
```bsl
ВЫБРАТЬ
	ВЫРАЗИТЬ(Контрагенты.Город КАК СТРОКА(100)) КАК Город,
	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Контрагенты.Ссылка) КАК Ссылка
ИЗ
	Справочник.Контрагенты КАК Контрагенты

СГРУППИРОВАТЬ ПО
	ВЫРАЗИТЬ(Контрагенты.Город КАК СТРОКА(100))
```