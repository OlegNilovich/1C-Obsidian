#ЗАПРОСЫ 

Если в запросе используется соединение с виртуальной таблицей и запрос работает с плохой производительностью.
Рекомендуется вынести обращение к виртуальной таблице в отдельный запрос с сохранением результатов во временной таблице.
##### Правильно:
```bsl
МенеджерВТ = Новый МенеджерВременныхТаблиц;
Запрос = Новый Запрос;
Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос.Текст =
"ВЫБРАТЬ
|	РеализацияТоваровСостав.Номенклатура КАК				Номенклатура,
|	СУММА(РеализацияТоваровСостав.Количество) КАК		Количество
|ПОМЕСТИТЬ СоставДокумента
|ИЗ
|	Документ.РеализацияТоваров.Состав КАК						РеализацияТоваровСостав
|ГДЕ
|	РеализацияТоваровСостав.Ссылка = &Ссылка
|	И РеализацияТоваровСостав.Номенклатура.Услуга = ЛОЖЬ
|
|СГРУППИРОВАТЬ ПО
|	РеализацияТоваровСостав.Номенклатура
|
|ИНДЕКСИРОВАТЬ ПО
|	Номенклатура";
Запрос.УстановитьПараметр("Ссылка", Документ);
Результат = Запрос.Выполнить();


Запрос2 = Новый Запрос;
Запрос2.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос2.Текст =
"ВЫБРАТЬ
|	ТоварыНаСкладахОстатки.Номенклатура КАК			Номенклатура,
|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК	Остаток
|ПОМЕСТИТЬ ОстаткиТоваров
|ИЗ
|	РегистрНакопления.ТоварыНаСкладах.Остатки(
|			,
|			Номенклатура В
|				(ВЫБРАТЬ
|					СоставДокумента.Номенклатура
|				ИЗ
|					СоставДокумента)) КАК ТоварыНаСкладахОстатки
|
|ИНДЕКСИРОВАТЬ ПО
|	Номенклатура";
Результат = Запрос2.Выполнить();


Запрос3 = Новый Запрос;
Запрос3.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос3.Текст =
"ВЫБРАТЬ
|	СоставДокумента.Номенклатура КАК								Номенклатура,
|	СоставДокумента.Номенклатура.Представление КАК		Товар,
|	СоставДокумента.Количество КАК									КоличествоВДокументе,
|	ЕСТЬNULL(ОстаткиТоваров.Остаток, 0) КАК					Остаток
|ИЗ
|	СоставДокумента КАК СоставДокумента
|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТоваров КАК ОстаткиТоваров
|		ПО (ОстаткиТоваров.Номенклатура = СоставДокумента.Номенклатура)
|ГДЕ
|	ЕСТЬNULL(ОстаткиТоваров.Остаток, 0) < ЕСТЬNULL(СоставДокумента.Количество, 0)";
Результат = Запрос3.Выполнить();
```