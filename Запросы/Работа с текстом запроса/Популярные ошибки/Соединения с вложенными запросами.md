#ЗАПРОСЫ 

В запросах часто требуется получить данные из нескольких источников, связанных по определенному условию. При этом источниками запроса могут выступать не только реальные таблицы информационной базы, но и виртуальные таблицы, и вложенные запросы.

##### Неправильно:
Выполнение запроса для получения остатков номенклатуры, содержащихся в документе
```bsl
Запрос = Новый Запрос;
Запрос.Текст =
"ВЫБРАТЬ
|	СоставДокумента.Номенклатура,
|	СоставДокумента.Номенклатура.Представление КАК Товар,
|	СоставДокумента.Количество КАК КоличествоВДокументе,
|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК Остаток
|ИЗ
|	(ВЫБРАТЬ
|		РеализацияТоваровСостав.Номенклатура КАК Номенклатура,
|		СУММА(РеализацияТоваровСостав.Количество) КАК Количество
|	ИЗ
|		Документ.РеализацияТоваров.Состав КАК РеализацияТоваровСостав
|	ГДЕ
|		РеализацияТоваровСостав.Ссылка = &Ссылка
|		И РеализацияТоваровСостав.Номенклатура.Услуга = ЛОЖЬ
|	
|	СГРУППИРОВАТЬ ПО
|		РеализацияТоваровСостав.Номенклатура) КАК СоставДокумента
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
|				,
|				Номенклатура В
|					(ВЫБРАТЬ
|						Документ.РеализацияТоваров.Состав.Номенклатура
|					ИЗ
|						Документ.РеализацияТоваров.Состав
|					ГДЕ
|						Документ.РеализацияТоваров.Состав.Ссылка = &Ссылка)) КАК ТоварыНаСкладахОстатки
|		ПО (ТоварыНаСкладахОстатки.Номенклатура = СоставДокумента.Номенклатура)
|ГДЕ
|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) < ЕСТЬNULL(СоставДокумента.Количество, 0)";

Запрос.УстановитьПараметр("Ссылка", Документ);
Результат = Запрос.Выполнить();
```
##### Правильно:
Получение остатков номенклатуры, содержащихся в документе, – правильный вариант
```bsl
МенеджерВТ = Новый МенеджерВременныхТаблиц;

Запрос = Новый Запрос;
Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос.Текст =
"ВЫБРАТЬ
|	РеализацияТоваровСостав.Номенклатура КАК				Номенклатура,
|	СУММА(РеализацияТоваровСостав.Количество) КАК		Количество
|ПОМЕСТИТЬ СоставДокумента
|ИЗ
|	Документ.РеализацияТоваров.Состав КАК РеализацияТоваровСостав
|ГДЕ
|	РеализацияТоваровСостав.Ссылка = &Ссылка
|	И РеализацияТоваровСостав.Номенклатура.Услуга = ЛОЖЬ
|
|СГРУППИРОВАТЬ ПО
|	РеализацияТоваровСостав.Номенклатура
|
|ИНДЕКСИРОВАТЬ ПО
|	Номенклатура";
Запрос.УстановитьПараметр("Ссылка", Документ);
Результат = Запрос.Выполнить();


Запрос2 = Новый Запрос;
Запрос2.МенеджерВременныхТаблиц = МенеджерВТ;
Запрос2.Текст =
"ВЫБРАТЬ
|	СоставДокумента.Номенклатура КАК												Номенклатура,
|	СоставДокумента.Номенклатура.Представление КАК						Товар,
|	СоставДокумента.Количество КАК													КоличествоВДокументе,
|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК	Остаток
|ИЗ
|	СоставДокумента КАК СоставДокумента
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
|				,
|				Номенклатура В
|					(ВЫБРАТЬ
|						СоставДокумента.Номенклатура
|					ИЗ
|						СоставДокумента)) КАК ТоварыНаСкладахОстатки
|		ПО (ТоварыНаСкладахОстатки.Номенклатура = СоставДокумента.Номенклатура)
|ГДЕ
|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) < ЕСТЬNULL(СоставДокумента.Количество, 0)";
Результат = Запрос2.Выполнить();
```
**Важно:** Вместо вложенных запросов в соединениях ВСЕГДА нужно использовать **Временные таблицы**. 
При этом их необходимо **Проиндексировать** по полям, участвующим в условии соединения.