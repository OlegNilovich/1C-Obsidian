#ЗАПРОСЫ 
### Задача:
![[Pasted image 20241007155524.png]]

### Решение:
Необходимые данные будем получать в запросе при помощи ТЕТА-соединения
```bsl

// "ДатаЗаписи" это дата документа на начало дня
ВЫБРАТЬ
	ПриходнаяНакладная.Ссылка КАК										Документ,
	ПриходнаяНакладная.Валюта КАК										Валюта,
	ПриходнаяНакладная.Дата КАК											ДатаДокумента,
	НАЧАЛОПЕРИОДА(ПриходнаяНакладная.Дата, ДЕНЬ) КАК	ДатаЗаписи
ПОМЕСТИТЬ ВТ_ДокументПоступления
ИЗ
	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
;

////////////////////////////////////////////////////////////////////////////////
// Получаем таблицу ВТ_ЗаписиРегистра соединением с физической таблицей "КурсыВалют"
// по условию "ДатаЗаписи" документа больше или равна полю "Период" Курса валют.
// Группируем таблицу и получаем максимальный "Период" для Курса валют.
ВЫБРАТЬ
	ВТ_ДокументПоступления.Документ КАК				Документ,
	ВТ_ДокументПоступления.Валюта КАК				Валюта,
	ВТ_ДокументПоступления.ДатаДокумента КАК	ДатаДокумента,
	МАКСИМУМ(КурсыВалют.Период) КАК					Период
ПОМЕСТИТЬ ВТ_ЗаписиРегистра
ИЗ
	ВТ_ДокументПоступления КАК ВТ_ДокументПоступления
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		ПО ВТ_ДокументПоступления.Валюта = КурсыВалют.Валюта
			И ВТ_ДокументПоступления.ДатаЗаписи >= КурсыВалют.Период

СГРУППИРОВАТЬ ПО
	ВТ_ДокументПоступления.Документ,
	ВТ_ДокументПоступления.Валюта,
	ВТ_ДокументПоступления.ДатаДокумента
;

////////////////////////////////////////////////////////////////////////////////
// Соединяемся еще раз с физической таблицей "Курсы валют"
// по "Периоду" из прошлого запроса и "Периоду" из таблицы "Курсы валют"
ВЫБРАТЬ
	ВТ_ЗаписиРегистра.Документ КАК				Документ,
	ВТ_ЗаписиРегистра.ДатаДокумента КАК		ДатаДокумента,
	ВТ_ЗаписиРегистра.Валюта КАК					Валюта,
	КурсыВалют.Курс КАК								Курс
ИЗ
	ВТ_ЗаписиРегистра КАК ВТ_ЗаписиРегистра
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		ПО ВТ_ЗаписиРегистра.Валюта = КурсыВалют.Валюта
			И ВТ_ЗаписиРегистра.Период = КурсыВалют.Период
```