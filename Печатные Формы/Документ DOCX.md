### Задача:
Для документа "ЗаказКлиента", требуется разработать печатную форму и создать команду для печати "ПечатьДоговорWord", в командной панели документа
![[Pasted image 20240901114800.png]]
### Шаблон:
![[Pasted image 20240901114934.png]]
### Решение:
#### Команда:
1. Создаем команду "ПечатьДоговорWord" для документа "ЗаказКлиента"
2. Свойства команды:
- Группа (Командная панель формы.Печать)
- Тип параметра команды (ДокументСсылка.ЗаказКлиента)
- Режим использования команды (Одиночный)
- Изменяет данные (Нет)
3. Модуль команды:
```bsl
&НаКлиенте
Процедура ОбработкаКоманды(СсылкаНаДокумент, ПараметрыВыполненияКоманды)
	
	//1. Создание COM-объекта
	Word = Новый COMОбъект("Word.Application");
	
	АдресДанныхШаблона = ПолучитьАдресШаблонаВХранилище("ПФ_DOCX_ШаблонДоговора");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДанныхШаблона);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("docx"); // либо "doc"
	
	Попытка
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
	Исключение
		Сообщить("Не удалось получить шаблон договора. Обратитесь в службу поддержи!");	
		Возврат;
	КонецПопытки;
	
	//2. Создание нового документа на основе шаблона
	Документ = Word.Documents.Add(ИмяВременногоФайла);
	
	//3. Получение данных для заполнения
	ДанныеЗаполнения = ПолучитьДанныеЗаказа(СсылкаНаДокумент);
	
	//4. Заполнение шапки документа
	Заполнить(Документ, "$(Номер)$", ДанныеЗаполнения.Номер);
	Заполнить(Документ, "$(Дата)$", ДанныеЗаполнения.Дата);
	Заполнить(Документ, "$(Организация)$", ДанныеЗаполнения.Организация);
	Заполнить(Документ, "$(Контрагент)$", ДанныеЗаполнения.Контрагент);

	//4. Заполнение табличной части документа
	МассивТоваров = ДанныеЗаполнения.Товары;
	
	Range = Документ.Range();
	Range.Find.Execute("#[ЯкорьТаблицы]#");
	Table = Range.Tables(1);
	СтрокаОбразец = Range.Rows(1);
	
	СуммаИтого = 0;

	Для каждого СтрокаТовара Из МассивТоваров Цикл
		НоваяСтрока = Table.Rows.Add(СтрокаОбразец);
		НоваяСтрока.Cells(1).Range.Text = СтрокаТовара.НомерСтроки;
		НоваяСтрока.Cells(2).Range.Text = СтрокаТовара.Номенклатура;
		НоваяСтрока.Cells(3).Range.Text = СтрокаТовара.ЕдиницаИзмерения;
		НоваяСтрока.Cells(4).Range.Text = СтрокаТовара.Количество;
		НоваяСтрока.Cells(5).Range.Text = СтрокаТовара.Цена;
		НоваяСтрока.Cells(6).Range.Text = СтрокаТовара.Сумма;
	
		СуммаИтого = СуммаИтого + СтрокаТовара.Сумма;
	КонецЦикла; 
	
	Заполнить(Документ, "$(СуммаИтого)$", СуммаИтого);
	
	СтрокаОбразец.Delete();
	
	//5. Показать документ пользователю
	Word.Visible = Истина;
	
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Документ, Параметр, Значение)
	Range = Документ.Content;
	Если Range.Find.Execute(Параметр) Тогда
		Range.Text = Значение;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ПолучитьАдресШаблонаВХранилище(НазваниеМакета)
	ДвоичныеДанные = Документы.ЗаказКлиента.ПолучитьМакет(НазваниеМакета);
	АдресДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	Возврат АдресДанных;
КонецФункции


&НаСервере
Функция ПолучитьДанныеЗаказа(СсылкаНаДокумент)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.Номер КАК										Номер,
	|	ЗаказКлиента.Дата КАК											Дата,
	|	ЗаказКлиента.Организация.Наименование КАК		Организация,
	|	ЗаказКлиента.Организация.Контрагент КАК			Контрагент,
	|	ЗаказКлиента.Товары.(
	|		НомерСтроки КАК												НомерСтроки,
	|		Номенклатура.Наименование КАК						Номенклатура,
	|		ЕдиницаИзмерения.Наименование КАК				ЕдиницаИзмерения,
	|		Количество КАК												Количество,
	|		Цена КАК															Цена,
	|		Сумма КАК														Сумма
	|	) КАК Товары
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДанныеДокумента = Новый Структура;
	
	// Заполняем ключи структуры "ДанныеДокумента"
	Для каждого КолонкаРезультата Из РезультатЗапроса.Колонки Цикл
		Если КолонкаРезультата.Имя = "Товары" Тогда
			Продолжить;
		КонецЕсли; 
		ДанныеДокумента.Вставить(КолонкаРезультата.Имя, "");	
	КонецЦикла; 
	
	// Заполняем значения ключей структуры "ДанныеДокумента"
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
	
	// Создаем и заполняем массиив Товаров, структурами Товаров
	МассивТоваров = Новый Массив;
	ВыборкаТовары = Выборка.Товары.Выбрать();

	Пока ВыборкаТовары.Следующий() Цикл
		СтруктураТовара = Новый Структура;
		Для каждого Колонка Из Выборка.Товары.Колонки Цикл
			СтруктураТовара.Вставить(Колонка.Имя, ВыборкаТовары[Колонка.Имя]);			
		КонецЦикла;
		МассивТоваров.Добавить(СтруктураТовара);
	КонецЦикла;

	// Помещаем массив товаров в результирующую структуру
	ДанныеДокумента.Вставить("Товары", МассивТоваров);
	
	Возврат ДанныеДокумента;
	
КонецФункции
```