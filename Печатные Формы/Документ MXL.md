### Задача:
Для документа "Расходная накладная", требуется разработать печатную форму и создать команду для печати, которая будет располагаться в командной панели документа и списка документов
![[Pasted image 20240901102214.png]]
![[Pasted image 20240901102050.png]]
### Решение:
#### Команда:
1. Создаем команду "ПФ_MXL_РасходнаяНакладная" для документа "РасходнаяНакладная"
2. Свойства команды:
- Группа (Командная панель формы.Печать)
- Тип параметра команды (ДокументСсылка.РасходнаяНакладная)
- Режим использования команды (Множественный)
- Изменяет данные (Нет)
![[Pasted image 20240901102604.png]]
3. Модуль команды:
```bsl
&НаКлиенте
Процедура ОбработкаКоманды(РасходныеНакладные, ПараметрыВыполненияКоманды)
	ТабДок = Новый ТабличныйДокумент;
	ПФ_MXL_РасходнаяНакладная(ТабДок, РасходныеНакладные);
	ТабДок.ОтображатьГруппировки = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.Показать();
КонецПроцедуры

&НаСервере
Процедура ПФ_MXL_РасходнаяНакладная(ТабДок, РасходныеНакладные)
	Документы.РасходнаяНакладная.ПФ_MXL_РасходнаяНакладная(ТабДок, РасходныеНакладные);
КонецПроцедуры
```
#### Макет:
1. Создаем макет "ПФ_MXL_РасходнаяНакладная" тип (ТабличныйДокумент)
![[Pasted image 20240901103032.png]]
2. Выделяем колонки в строке 2 выделяем колонки 2-33 и объединяем их
![[Pasted image 20240901103416.png]]
3. Свойства => Макет => Заполнение => Шаблон. В квадратных скобках [Номер] и [Дата] теперь будут параметрами и заполняться программно.
![[Pasted image 20240901103328.png]]
4. Для ячеек выделенных красной рамкой устанавливаем свойство: Параметр
![[Pasted image 20240901103850.png]]
5. Для ячейки с параметром <Номенклатура> устанавливаем "РазмещениеТекста": Переносить. Если наименование номенклатуры не будет помещаться в строке, то оно будет переноситься на следующую строку
![[Pasted image 20240901104008.png]]
6. Создаем три области с помощью команды или комбинации клавиш Ctrl + Shift + N:
- Шапка (1-8)
- Таблица (9)
- Подвал (10-15)
![[Pasted image 20240901104822.png]]
7. Устанавливаем границы вокруг таблицы
![[Pasted image 20240901105141.png]]
8. Устанавливаем необходимый размер и жирность шрифта, а так же выравнивание
![[Pasted image 20240901105248.png]]
#### Модуль менеджера:
1. Открываем модуль менеджера документа "РасходнаяНакладная"
![[Pasted image 20240901105406.png]]
2. Создаем процедуру для формирование табличного документа
```bsl
Процедура ПФ_MXL_РасходнаяНакладная(ТабДок, РасходныеНакладные) Экспорт
	
	Макет = Документы.РасходнаяНакладная.ПолучитьМакет("ПФ_MXL_РасходнаяНакладная");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходнаяНакладная.Дата КАК						Дата,
	|	РасходнаяНакладная.Номер КАК					Номер,
	|	РасходнаяНакладная.Контрагент КАК			Покупатель,
	|	РасходнаяНакладная.Организация КАК			Поставщик,
	|	РасходнаяНакладная.Склад КАК					Склад,
	|	РасходнаяНакладная.Ответственный КАК		Ответственный,
	|	РасходнаяНакладная.Товары.(
	|		Номенклатура КАК									Номенклатура,
	|		Количество КАК										Количество,
	|		ЕдиницаИзмерения КАК							ЕдиницаИзмерения,
	|		Цена КАК													Цена,
	|		СтавкаНДС КАК											СтавкаНДС,
	|		Сумма КАК												Сумма,
	|		СуммаНДС КАК											СуммаНДС,
	|		СуммаВсего КАК										СуммаВсего
	|	) КАК Товары
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|ГДЕ
	|	РасходнаяНакладная.Ссылка В(&РасходныеНакладные)";
	Запрос.Параметры.Вставить("РасходныеНакладные", РасходныеНакладные);
	Выборка = Запрос.Выполнить().Выбрать();

	ТабДок.Очистить();

	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();	
		КонецЕсли; 

		Шапка = Макет.ПолучитьОбласть("Шапка");
		Шапка.Параметры.Заполнить(Выборка);
		Шапка.Параметры.Номер = Число(Выборка.Номер);
		Шапка.Параметры.Дата = Формат(Выборка.Дата, "ДЛФ=DD");
		ТабДок.Вывести(Шапка, Выборка.Уровень());
	
		Итоги = Новый Структура("ИтогСумма,ИтогСуммаНДС,ИтогСуммаВсего,ИтогСуммаВсегоПрописью",0,0,0);

		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			Итоги.ИтогСумма			= Итоги.ИтогСумма + ВыборкаТовары.Сумма;
			Итоги.ИтогСуммаНДС		= Итоги.ИтогСуммаНДС + ВыборкаТовары.СуммаНДС;
			Итоги.ИтогСуммаВсего	= Итоги.ИтогСуммаВсего + ВыборкаТовары.СуммаВсего;
			
			Таблица = Макет.ПолучитьОбласть("Таблица");
			Таблица.Параметры.Заполнить(ВыборкаТовары);
			ТабДок.Вывести(Таблица, ВыборкаТовары.Уровень());
		КонецЦикла;
		
		Параметры = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
		Итоги.ИтогСуммаВсегоПрописью = ЧислоПрописью(Итоги.ИтогСуммаВсего, "ДП=Истина", Параметры); 

		Подвал = Макет.ПолучитьОбласть("Подвал");
		Подвал.Параметры.Заполнить(Выборка);
		Подвал.Параметры.Заполнить(Итоги);
		ТабДок.Вывести(Подвал);
		
	КонецЦикла;
	
КонецПроцедуры
```