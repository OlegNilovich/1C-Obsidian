
Модуль объекта
```bsl

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");
	ПараметрыРегистрации.Вставить("Назначение", ПолучитьНазначениеОбработки());
	ПараметрыРегистрации.Вставить("Наименование", "Договор с клиентом (Word)");
	ПараметрыРегистрации.Вставить("Информация", "Договор с клиентом (Word)");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Версия", "1.0");
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд,
					"Договор с клиентом (Word)",
					"ПФ_DOCX_ШаблонДоговора",
					"ВызовКлиентскогоМетода",
					Истина,
					"ПечатьXML");
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);				
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьНазначениеОбработки()
	
	Массив = Новый Массив;
	Массив.Добавить("Справочник.ДоговорыКонтрагентов"); 
	Возврат Массив;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры


```

Форма
![[Pasted image 20241129000946.png]]

Модуль формы
```bsl

&НаКлиенте
Процедура ТестПечать(ИдентификаторКоманды) Экспорт
	
	//Массив ссылок договоров с формы (для тестирования)
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Договор1);
	Если ЗначениеЗаполнено(Договор2) Тогда
		МассивОбъектов.Добавить(Договор2);
	КонецЕсли;
	
	ПечатьДоговора(ИдентификаторКоманды, МассивОбъектов);	
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ИдентификаторКоманды, МассивОбъектов) Экспорт
	
	ПечатьДоговора(ИдентификаторКоманды, МассивОбъектов);
	
КонецПроцедуры


&НаКлиенте
Процедура ПечатьДоговора(ИдентификаторКоманды, МассивОбъектов) Экспорт
	
	//1. Получение данных для заполнения
	ДанныеДоговоров = ПолучитьДанныеДоговоров(МассивОбъектов);
	
	Для Каждого ДанныеДоговора Из ДанныеДоговоров Цикл
		
		//1. Создание COM-объекта
		Word = Новый COMОбъект("Word.Application");
		
		АдресДанныхШаблона = ПолучитьАдресШаблонаВХранилище("ПФ_DOCX_ШаблонДоговора");
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДанныхШаблона);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("docx"); // либо "doc"
		
		Попытка
			ДвоичныеДанные.Записать(ИмяВременногоФайла);
		Исключение
			Сообщить("Не удалось получить шаблон договора. Обратитесь в службу поддержи!");	
			Возврат;
		КонецПопытки;
		
		//2. Создание нового документа на основе шаблона
		Документ = Word.Documents.Add(ИмяВременногоФайла);
		
		//2.1 Заголовок документа, пример:     Договор РР-00122 от 27.11.2024
		ИмяФайла = "Договор " + ?(ЗначениеЗаполнено(ДанныеДоговора.Номер),ДанныеДоговора.Номер, " без номера ") +
		" от " + ?(ЗначениеЗаполнено(ДанныеДоговора.Дата),Формат(ДанныеДоговора.Дата,"ДФ=""дд ММММ гггг 'г.'"""), " ");
		Dialog = Word.Dialogs(86);
		Dialog.Title = ИмяФайла;
		Dialog.Execute();
		
		
		//4.2 Заполнение шапки документа 
		ЗаполнитьДок(Документ, "{Номер}",							ДанныеДоговора.Номер);
		ЗаполнитьДок(Документ, "{Дата}",							Формат(ДанныеДоговора.Дата,"ДФ='«dd» ММММ yyyy'")+" г.");
		ЗаполнитьДок(Документ, "{Контрагент}",						ДанныеДоговора.Контрагент);
		ЗаполнитьДок(Документ, "{ДолжностьРуководителя}",		ДанныеДоговора.ДолжностьРуководителя);
		ЗаполнитьДок(Документ, "{Руководитель}",					ДанныеДоговора.Руководитель);
		ЗаполнитьДок(Документ, "{Контрагент}",						ДанныеДоговора.Контрагент);
		ЗаполнитьДок(Документ, "{ИННКонтрагента}",				ДанныеДоговора.ИННКонтрагента);
		ЗаполнитьДок(Документ, "{КППКонтрагента}",				ДанныеДоговора.КППКонтрагента);
		ЗаполнитьДок(Документ, "{ЮрАдресКонтрагента}",			ДанныеДоговора.ЮрАдресКонтрагента);
		ЗаполнитьДок(Документ, "{ФактАдресКонтрагента}",			ДанныеДоговора.ФактАдресКонтрагента);
		ЗаполнитьДок(Документ, "{РасчетныйСчетКонтрагента}",	ДанныеДоговора.РасчетныйСчетКонтрагента);
		ЗаполнитьДок(Документ, "{БанкКонтрагента}",				ДанныеДоговора.БанкКонтрагента);
		ЗаполнитьДок(Документ, "{ГородБанка}",					ДанныеДоговора.ГородБанка);
		ЗаполнитьДок(Документ, "{КС}",								ДанныеДоговора.КС);
		ЗаполнитьДок(Документ, "{БИК}",							ДанныеДоговора.БИК);
		ЗаполнитьДок(Документ, "{ТелефонКонтрагента}",			ДанныеДоговора.ТелефонКонтрагента);
		ЗаполнитьДок(Документ, "{EmailКонтрагента}",				"mailto:"+ДанныеДоговора.EmailКонтрагента);
		ЗаполнитьДок(Документ, "{ФИОРук}",						ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ДанныеДоговора.ФИОРук));
		
		//5. Показать документ пользователю
		Word.Visible = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДок(Документ, Параметр, Значение)
	Range = Документ.Content;
	Если Range.Find.Execute(Параметр) Тогда
		Range.Text = Значение;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ПолучитьАдресШаблонаВХранилище(НазваниеМакета)
	ДвоичныеДанные = РеквизитФормыВЗначение("Объект").ПолучитьМакет(НазваниеМакета);
	АдресДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	Возврат АдресДанных;
КонецФункции


&НаСервере
Функция ПолучитьДанныеДоговоров(МассивОбъектов)
	
	// Получаем массив ссылок контрагентов от массива договоров
	КонтрагентыСоответствие = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивОбъектов, "Контрагент");
	Контрагенты = Новый Массив;
	Для Каждого ТекЭлемент Из КонтрагентыСоответствие Цикл
		Контрагенты.Добавить(ТекЭлемент.Значение);
	КонецЦикла;
	
	//// Определяем необходимые типы и виды контактной информации контрагентов
	//МассивТипов = Новый Массив;
	//МассивТипов.Добавить(Перечисления.ТипыКонтактнойИнформации.Адрес);
	//МассивТипов.Добавить(Перечисления.ТипыКонтактнойИнформации.Телефон);
	//МассивТипов.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	//МассивВидов = Новый Массив;
	//МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	//МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);

	// Получаем таблицу значений с контактной информацией контрагентов
	КонтактыТЗ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Контрагенты,,,ТекущаяДата());
	//КонтактыТЗ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Контрагенты,МассивТипов,МассивВидов,ТекущаяДата());

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Номер КАК													Номер,
	|	ДоговорыКонтрагентов.Дата КАК														Дата,
	|	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.Контрагент) КАК							Контрагент,
	|	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.КонтактноеЛицо) КАК						Руководитель,
	|	""Директор"" КАК																		ДолжностьРуководителя,
	|	ДоговорыКонтрагентов.Контрагент.ИНН КАК											ИННКонтрагента,
	|	ДоговорыКонтрагентов.Контрагент.КПП КАК											КППКонтрагента,
	|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента.НомерСчета КАК				РасчетныйСчетКонтрагента,
	|	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.БанковскийСчетКонтрагента.Банк) КАК	БанкКонтрагента,
	|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента.Банк.Город КАК				ГородБанка,
	|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента.КоррСчетБанка КАК			КС,
	|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента.БИКБанка КАК					БИК,
	|	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.КонтактноеЛицо) КАК						ФИОРук,
	|	ДоговорыКонтрагентов.Контрагент КАК												КонтрагентСсылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК												ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В(&МассивОбъектов)";
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	МассивДоговоров = ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	Для Каждого Договор Из МассивДоговоров Цикл
		
		//1 Склонение фио и должности руководителя в родительном падеже
		Договор.Руководитель				= СклонениеПредставленийОбъектов.ПросклонятьФИО(Договор.Руководитель, 2,, 1);
		Договор.ДолжностьРуководителя	= СклонениеПредставленийОбъектов.ПросклонятьПредставление(Договор.ДолжностьРуководителя, 2);
		
		//2 заполнение контактной информации контрагента в строке
		КонтактнаяИнформацияКонтрагента = КонтактыТЗ.НайтиСтроки(Новый Структура("Объект", Договор.КонтрагентСсылка));
		Для каждого СтрокаКИ Из КонтактнаяИнформацияКонтрагента Цикл
			Если СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента Тогда
				Договор.Вставить("ЮрАдресКонтрагента", СтрокаКИ.Представление); 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента Тогда
				Договор.Вставить("ФактАдресКонтрагента", СтрокаКИ.Представление); 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
				Договор.Вставить("ТелефонКонтрагента", СтрокаКИ.Представление); 
			ИначеЕсли СтрокаКИ.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
				Договор.Вставить("EmailКонтрагента", СтрокаКИ.Представление); 
			КонецЕсли;	
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат МассивДоговоров;
	
КонецФункции
```