```bsl
// Отбор в динамическом списке (программно)

// 1. Создаем реквизит "ВидНоменклатуры" (СправочникСсылка.ВидыНоменклатуры)
// 2. Размещаем его на форме и создаем событие "ПриИзменении"
&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	УстановитьЭлементОтбораДинамическогоСписка(Товары, "ВидНоменклатуры", ВидНоменклатуры, ВидСравненияКомпоновкиДанных.Равно);
КонецПроцедуры

// 3. При изменении элемента "ВидНоменклатуры", будет вызываться процедура отбора динамического списка
&НаКлиенте
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ЗначениеПоля, ВидСравнения)
	
	Для Каждого ЭлементОтбора Из ДинамическийСписок.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ОтборКомпоновкиДанных") Тогда
			
			ПользовательскийОтбор = ЭлементОтбора.Элементы;
			НовыйЭлементОтбора = Неопределено;
			
			Для Каждого СуществующийЭлементОтбора Из ПользовательскийОтбор Цикл
				
				Если СуществующийЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля) Тогда
					НовыйЭлементОтбора = СуществующийЭлементОтбора;
					Прервать;
				КонецЕсли;	
					
			КонецЦикла;
			
			Если НовыйЭлементОтбора = Неопределено Тогда
				НовыйЭлементОтбора = ПользовательскийОтбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
			КонецЕсли;
		
			НовыйЭлементОтбора.ВидСравнения		= ВидСравнения;
			НовыйЭлементОтбора.Использование	= ЗначениеЗаполнено(ЗначениеПоля);
			НовыйЭлементОтбора.РежимОтображения	= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
			НовыйЭлементОтбора.ПравоеЗначение	= ЗначениеПоля;			
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры
```