Требуется в форме списка номенклатуры, создать поле *Вид номенклатуры*, при выборе его значения, список номенклатуры будет динамически фильтроваться по виду.

// 1. Создаем форму списка справочника *Номенклатура*
// 2. Создаем реквизит "ВидНоменклатуры" (СправочникСсылка.ВидыНоменклатуры)
// 3. Размещаем его на форме и создаем событие для элемента *ПриИзменении*

Модуль формы:
```bsl
&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)

	ДинамическийСписок = Список;
	ИмяПоля = "ВидНоменклатуры";
	ЗначениеПоля = ВидНоменклатуры;
	ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
	
	УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ЗначениеПоля, ВидСравненияКД);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ЗначениеПоля, ВидСравненияКД)
	
	Для Каждого ЭлементОтбора Из ДинамическийСписок.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ОтборКомпоновкиДанных") Тогда
			
			ПользовательскийОтбор = ЭлементОтбора.Элементы;
			НовыйЭлементОтбора = Неопределено;
			
			Для Каждого СуществующийЭлементОтбора Из ПользовательскийОтбор Цикл
				
				Если СуществующийЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля) Тогда
					НовыйЭлементОтбора = СуществующийЭлементОтбора;
					Прервать;
				КонецЕсли;	
					
			КонецЦикла;
			
			Если НовыйЭлементОтбора = Неопределено Тогда
				НовыйЭлементОтбора = ПользовательскийОтбор.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
			КонецЕсли;
		
			НовыйЭлементОтбора.ВидСравнения	= ВидСравненияКД;
			НовыйЭлементОтбора.Использование = ЗначениеЗаполнено(ЗначениеПоля);
			НовыйЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
			НовыйЭлементОтбора.ПравоеЗначение = ЗначениеПоля;			
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры
```