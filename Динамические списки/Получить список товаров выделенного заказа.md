```bsl
// 1. Создаем динамический список с "ЗаказыКлиентов". Размещаем его на форме
// 2. Создаем таблицу значений "Товары". Размещаем на форме
// 3. При выделении конкретного заказа, будет меняться список товаров

&НаКлиенте
Процедура СписокЗаказовПриАктивизацииСтроки(Элемент)
	
	Товары.Очистить();
	
	ТекущийЗаказ = Элементы.СписокЗаказов.ТекущаяСтрока;
	МассивТоваров = ПолучитьТоварыЗаказа(ТекущийЗаказ); 
	
	Для каждого СтруктураТовара Из МассивТоваров Цикл
		НоваяСтрока = Товары.Добавить(); 
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураТовара); 
	КонецЦикла;

КонецПроцедуры                        


&НаСервереБезКонтекста
Функция ПолучитьТоварыЗаказа(Заказ)
	
	МассивТоваров = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество	КАК Количество
		|ИЗ
		|	Документ.ЗаказКлиента.Товары	КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Заказ);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтруктураТовара = Новый Структура("Номенклатура, Количество");
		ЗаполнитьЗначенияСвойств(СтруктураТовара, Выборка);
		МассивТоваров.Добавить(СтруктураТовара);
	КонецЦикла;   
	
	Возврат МассивТоваров;

КонецФункции
```