### Задача:
Требуется создать обработку, на форме которой будет расположен реквизит **Вид цен** и команда **Открыть форму динамического списка**, которая открывает список номенклатуры с их ценами.

Если вид цен **Не заполнен** (пустая ссылка) - откроется список номенклатуры без колонки **Цена**
![[Pasted image 20240918202241.png]]

Если вид цен **Оптовая** - откроется список номенклатуры с оптовыми ценами
![[Pasted image 20240918202608.png]]

Если вид цен **Розничная** - откроется список номенклатуры с розничными ценами
![[Pasted image 20240918202711.png]]
### Решение:
Создаем обработку **ПередачаПараметровВЗапросДинамическогоСписка**
Создаем две формы обработки **ГлавнаяФорма** и **ФормаДинамическогоСписка**
##### Главная форма:
1. Создаем реквизит **ВидЦен** с типом (*СправочникСсылка.ВидыЦен*)
2. Создаем команду **ОткрытьФормуДинамическогоСписка** 
3. Создаем обработчик действия команды *&НаКлиенте*
![[Pasted image 20240918200937.png]]
4. Модуль формы:
```bsl
&НаКлиенте
Процедура ОткрытьФормуДинамическогоСписка(Команда)
	ПутьКФорме = "Обработка.ПередачаПараметровВЗапросДинамическогоСписка.Форма.ФормаДинамическогоСписка";	
	СтруктураПараметров = Новый Структура("ВидЦен", ВидЦен);
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ПутьКФорме, СтруктураПараметров, ЭтотОбъект,,,,, РежимОткрытия);
КонецПроцедуры
```

##### Форма динамического списка:
1. Создаем реквизит **Список** с типом (*ДинамическийСписок*)
2. Свойства => Объект => Включаем **ПроизвольныйЗапрос** => Открываем **Настройки списка**
![[Pasted image 20240918201318.png]]
3. Определяем **Текст запроса**, **Основную таблицу** и включаем **Динамическое считывание данных**
![[Pasted image 20240918201611.png]]
4. Текст запроса:
```bsl
ВЫБРАТЬ
	СправочникНоменклатура.Ссылка КАК						Ссылка,
	СправочникНоменклатура.Родитель КАК					Родитель,
	СправочникНоменклатура.ЭтоГруппа КАК					ЭтоГруппа,
	СправочникНоменклатура.Код КАК							Код,
	СправочникНоменклатура.Наименование КАК			Наименование,
	СправочникНоменклатура.ВидНоменклатуры КАК		ВидНоменклатуры,
	СправочникНоменклатура.Артикул КАК						Артикул,
	СправочникНоменклатура.ЕдиницаИзмерения КАК		ЕдиницаИзмерения,
	СправочникНоменклатура.СтавкаНДС КАК					СтавкаНДС,
	СправочникНоменклатура.ТипНоменклатуры КАК		ТипНоменклатуры,
	ЦеныНоменклатурыСрезПоследних.Цена КАК			Цена
ИЗ
	Справочник.Номенклатура КАК СправочникНоменклатура
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		ПО СправочникНоменклатура.Ссылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
```
5. Переносим реквизит **Список** в окно элементов формы, с требуемыми колонками
6. Для элемента **Форма**, создаем событие **ПриСозданииНаСервере**
![[Pasted image 20240918201829.png]]
7. Модуль формы:
```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ВидЦен") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ВидЦен", Параметры.ВидЦен);
		Если Параметры.ВидЦен = Справочники.ВидыЦен.ПустаяСсылка() Тогда
			Элементы.Цена.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
```