#ФОРМЫ 
### Задача:
В документе **Закал клиента**, требуется запретить стандартный выбор **Контрагента** через выпадающий список. Вместо этого, требуется создать команду выбора контрагента, которая будет открывать специальную **Форму выбора контрагента**, с возможностью выбрать **контрагента** или отказаться.
Если в документе уже заполнен **Контрагент**, то он должен переноситься в форму выбора.
![[Pasted image 20240918190730.png]]

---
### Решение:
##### Форма элемента:
1. Создаем форму элемента документа **ЗаказКлиента**
2. Для элемента **Контрагент** устанавливаем флажок на свойстве **ТолькоПросмотр**
3. Для элемента **Форма** создаем событие **ОбработкаВыбора**
![[Pasted image 20240918185845.png]]
4. Создаем команду **ВыбратьКонтрагента** и обработчик действия *&НаКлиенте*
![[Pasted image 20240918185542.png]]
5. Модуль формы:
```bsl
&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		Объект.Контрагент = ВыбранноеЗначение;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКонтрагента(Команда)
	СтруктураПараметров = Новый Структура("Контрагент", Объект.Контрагент);
	ПутьКФорме = "Обработка.ПередачаДанныхМеждуФормами.Форма.ФормаВыбораКонтрагента";
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ПутьКФорме, СтруктураПараметров, ЭтаФорма,,,,, Режим);
КонецПроцедуры
```
##### Форма выбора контрагента:
1. Создаем форму **ФормаВыбораКонтрагента**
2. Создаем реквизит **Контрагент** с типом (*СправочникСсылка.Контрагенты*)
3. Создаем событие формы **ПриСозданииНаСервере**
![[Pasted image 20240918190350.png]]
4. Создаем две команды **ОК** и **Отмена**, создаем их обработчики действия *&НаКлиенте* 
![[Pasted image 20240918190137.png]]
5. Модуль формы:
```bsl
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Контрагент") Тогда
		Контрагент = Параметры.Контрагент;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	ОповеститьОВыборе(Контрагент);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры
```